ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

!?FU9023;                             [Определение и возврат в x5 ценности действия для ИИ]
*** x1-x4 - параметры действия (тип/подтип/значение/бонус. маны), x6 - герой, x7 - герой-оппонент, x8 - номер z-переменной с описанием
*** v9901 - бонус для заклинаний, не наносящих урон. Базовое значение: 1 х уровень заклинания
*** v9902 - бонус для заклинаний урона. Базовое значение: 2 х уровень заклинания
*** v9903 - предпочитаемая специализация
*** v9904 - бонус для призыва существ. Базовое значение: FV отряда / 500 + 1 для стрелков +2 для БМ
*** v9905 - бонус для отстройки здания. Базовое значение: 2 х уровень здания
*** v9906 - бонус для атакующих параметров командира. Базовое значение: 1 (для удвоенного бонуса - 2)
*** v9907 - бонус для атакующих способностей командира. Базовое значение: 3
*** v9908 - бонус для защитных параметров командира. Базовое значение: 2 (для удвоенного бонуса - 4)
*** v9909 - бонус для защитных способностей командира. Базовое значение: 6
*** v9910 - бонус для атакующих параметров героя. Базовое значение: 1 (для удвоенного бонуса - 2)
*** v9911 - бонус для атакующих навыков героя. Базовое значение: 1/2/4 (баз./продв./эксп.)
*** v9912 - бонус для защитных параметров героя (защита/знание). Базовое значение: 2/3 (для удвоенного бонуса - 4/6)
*** v9913 - бонус для защитных навыков героя. Базовое значение: 2/4/8 (баз./продв./эксп.)
*** v9914 - бонус для магических навыков героя. Базовое значение: 1/2/4 (баз./продв./эксп.)
*** v9915 - бонус для тактических навыков героя. Базовое значение: 3/6/9 (баз./продв./эксп.)
*** v9916 - бонус для действий гильдий. Базовое значение: 1/2 от мана-стоимости действия + 1
*** v9917 - номер используемой схемы
***
*** Определение типа действия y1, его базовой ценности y2 и множителя ценности y3
!!VRy3:S1;                            [y3 - множитель ценности действия по умолчанию 1]
!!if&x1=1:;                           [Призыв существ]
    !!VRy1:S4;                        [тип действия 4]
    !!MA:Fx2/?y2 Xx2/?y4;             [y2 - FV существа, y4 - флаги]
    !!VRy4:&4;                        [y4>0 для стрелков]
    !!VRy4&y4>0:S1;                   [+1 для стрелков]
    !!VRy2:*x3 :500 +y4;              [Базовое значение: FV отряда / 500 + 1 для стрелков]
    !!VRv1:C0/0;                      [инициализация v1,v2]
    !!DO9038/0/20/1&x6=-10:Px2;       [v1 - количество аналогичных призванных существ]
    !!DO9038/21/41/1&x6=-20:Px2;      [v2=1, если отряд ослеплен, окаменен, парализован или стрелок с боезапасом <4]
    !!VRi&v1>0/v2=0:Sv1 :x3 +1;       [бонус ценности 1 + 1 за каждый полный уже призванный аналогичный отряд]
    !!VRy2&v1>0/v2=0:+i;              [повышение бонуса ценности, не распространяется на БМ]
    !!VRy2&v2=1:S-10;                 [ценность допризыва в ослепленный/окамененный/парализованный/состаренный отряд равна -10]
    !!if&x2=146:;                     [для Баллисты]
      !!HEx6:S10/?y10 S1/?y11;        [y10/y11 - Навыки БМ/Стрельбы]
      !!VRy2&x2=147:S5 +y10 +y11;     [ценность 5 + Уровень навыка Артиллерии + Уровень навыка Стрельбы]
    !!en:;
    !!if&x2=147:;                     [для Палатки первой помощи]
      !!HEx6:S10/?y10;                [y10 - Навык БМ]
      !!VRy2&x2=147:S7 +y10;          [ценность 7 + Уровень навыка Первой помощи]
    !!en:;
    !!if&x2=148:;                     [для Тележки с боеприпасами]
      !!VRv1:S0;                      [инициализация v1]
      !!DO9026/0/20/1&x6=-10:P;       [v1=1, если есть отряд-стрелок...]
      !!DO9026/21/41/1&x6=-20:P;      [...с боезапасом менее 4]
      !!VRy2:Sv1 *5;                  [ценность +5 за каждый отряд стрелков с заканчивающимся боезапасом]
      !!VRy2&v1=0:S-10;               [если нет ни одного стрелка с боезапасом менее 5, ценность призыва Тележки с боеприпасами -10]
    !!en:;
!!en:;
!!if&x1=2:;                           [Изучение заклинания]
  !!SSx2:F?y1 L?y2;                   [y1 - флаги заклинания, y2 - уровень заклинания]
  !!VRy1:&512 :512 *2 +1;             [баф/урон 1..3]
  !!VRy2:*y1;                         [Базовое значение: 1..3 х уровень заклинания]
  !!VRy2&x2=37:S10;                   [Базовая ценность заклинания "Лечение" - 10]
!!en:;
!!if&x1=3:;                           [Повышение первичного параметра героя]
  !!if|x2=31/x2=32;                   [Боевые параметры героя (Атака/Защита)]
    !!VRy1:S10;                       [тип действия 10]
    !!VRy2&x2=31:S2;                  [Базовое значение для Атаки: 2]
    !!VRy2&x2=32:S2;                  [Базовое значение для Защиты: 2]
  !!en:;
  !!if|x2=33/x2=34;                   [Магические параметры героя]
    !!VRy1:S12;                       [тип действия 12]
    !!HEx6:S7/?i;                     [i - Уровень Мудрости героя]
    !!VRy2&x2=33:S2 +i;               [Базовое значение для Силы: 2 + Уровень Мудрости]
    !!HEx6:Fd/d/d/?i;                 [i - текущее Знание героя]
    !!VRy2&x2=34:S-6;                 [Базовое значение для Знания: -6]
    !!VRy2&x2=34/i<20:S-3;            [Если Знание менее 20: -3]
    !!VRy2&x2=34/x3=2:S0;             [для удвоенного бонуса: 0]
    !!VRy2&x2=34/i<15:S0;             [Если Знание менее 15: 0]
    !!VRy2&x2=34/i<10:S3;             [Если Знание менее 10: 3]
    !!VRy2&x2=34/i<5:S6;              [Если Знание менее  5: 6]
  !!en:;
  !!VRy3:Sx3;                         [y3 - удвоенный коэффициент для удвоенного бонуса]
!!en:;
!!if&x1=4:;                           [Вторичные навыки героя]
  !!if|x2=1/x2=2/x2=9/x2=10/x2=22/x2=6:;   [Атакующие навыки героя (Стрельба, Логистика, Удача, Боевые машины (Баллистика), Атака, Лидерство)]
    !!VRy1:S11;                       [тип действия 11]
    !!VRy2:Sx3;                       [Базовое значение: 1/2/4]
    !!VRy2&y2=3:S4;                   [...]
  !!en:;
  !!if|x2=23/x2=26:;                  [Защитные навыки героя (Защита,Сопротивление)]
    !!VRy1:S13;                       [тип действия 13]
    !!VRy2:Sx3 *2;                    [Базовое значение: 2/4/8]
    !!VRy2&y2=6:S8;                   [...]
    !!if&x2=26:;
      !!HEx7:Fd/d/?i/d S7/?j;         [i/j - Сила/Мудрость вражесткого героя]
      !!VRi::3 +j;                    [i - бонус зависящий от параметров вражеского героя]
      !!VRy2:+i;                      [для сопротивления ценность повышена на 1 за кадлый уровень Мудрости врага и 1 за кадыые 3 Силы врага]
    !!en:;
  !!en:;
  !!if|x2=7/x2=8/x2=25/x2=14/x2=15/x2=16/x2=17:; [Магические навыки героя (Мудрость,Мистицизм,Волшебство,Магии стихий)]
    !!VRy1:S14;                       [тип действия 14]
    !!VRy2:Sx3;                       [Базовое значение: 1/2/4]
    !!VRy2&y2=3:S4;                   [...]
    !!VRy2&x2=8:S8;                   [Ценность Мистицизма - 8]
  !!en:;
  !!if|x2=11/x2=24/x2=4/x2=19/x2=18/x2=21/x2=12/x2=5:; [Тактические навыки героя (Орлиный глаз,Интеллект,Дипломатия,Тактика,Грамотность,Обучение,Некромантия,Специализация)]
    !!VRy1:S15;                       [тип действия 15]
    !!VRy2:Sx3 *3;                    [Базовое значение: 3/6/9]
    !!VRy2&x2=19:S10;                 [Базовое значение Тактики - 10]
    !!VRy2&x2=5:S10;                  [Базовое значение Специализации - 10]
  !!en:;
  !!if|x2=13:;                        [Поместья]
    !!VRy1:S15;                       [тип действия 5 (отстройка)]
    !!VRy2&x6=-10:Sv9980 +v9981 +v9982 *2; [Базовая ценность: +2 для каждого отстроенного здания]
    !!VRy2&x6=-20:Sv9983 +v9984 +v9985 *2; [...]
  !!en:;
!!en:;
!!if&x1=5:;                           [Постройка здания]
  !!VRy1:S5;                          [тип действия 5]
  !!VRy2:Sx3 -30 %7 +1 *2;            [Базовое значение: 2 х уровень здания]
  !!VRy2&x2=9:S10;                    [Базовое значение для Спецстроений: 10]
!!en:;
!!if|x1=6/x1=10:;                     [улучшение командира]
  !!if|x2=0/x2=3/x2=5:;               [атакующие параметры (атака, урон, сила, скорость)]
    !!VRy3&x2=0/x3>4:S2;              [y3 - удвоенный уоэффициент для удвоенного бонуса]
    !!VRy3&x2=3/x3>4:S2;              [...]
    !!VRy3&x2=5/x3>1:S2;              [...]
    !!VRy1:S6;                        [тип действия 6]
    !!VRy2:S1;                        [Базовое значение: 1]
  !!en:;
  !!if|x2=1/x2=2/x2=6:;               [защитные параметры (Защита, здоровье, сопротивление)]
    !!VRy3&x2=1/x3>4:S2;              [y3 - удвоенный уоэффициент для удвоенного бонуса]
    !!VRy3&x2=2/x3>250:S2;            [...]
    !!VRy3&x2=6/x3>10:S2;             [...]
    !!VRy1:S8;                        [тип действия 8]
    !!VRy2:S2;                        [Базовое значение: 2]
    !!if&x2=6:;
      !!HEx7:Fd/d/?i/d S7/?j;         [i/j - Сила/Мудрость вражесткого героя]
      !!VRi::3 +j;                    [i - бонус зависящий от параметров вражеского героя]
      !!VRy2:+i;                      [для сопротивления ценность повышена на 1 за кадлый уровень Мудрости врага и 1 за кадыые 3 Силы врага]
    !!en:;
  !!en:;
  !!if&x2=7:;                         [способности]
    !!if|x3=0/x3=2/x3=5/x3=6/x3=9:;   [атакующие способности (-50% защ, макс. урон, ответ всем, круговой удар, двойной удар)]
      !!VRy1:S7;                      [тип действия 7]
      !!VRy2:S3;                      [Базовое значение: 3]
    !!en:;
    !!if|x3=3/x3=7/x3=8/x3=11/x3=4:;  [защитные способности (страх, безответка, огн. щит, блок, регенерация, стрельба)]
      !!VRy1:S9;                      [тип действия 9]
      !!VRy2:S6;                      [Базовое значение: 6]
    !!en:;
  !!en:;
  !!if&x1=10:;                        [магия]
    !!VRy1:S9;                        [тип действия 9]
    !!VRy1&x2>=15/x2<=18:S9;          [тип действия 7 для ударных заклинаний]
    !!VRy2:S6;                        [Базовое значение: 6]
  !!en:;
!!en:;
!!if&x1=7:;                           [действия гильдии Некромантов]
  !!VRy1:S16;                         [тип действия 16]
  !!VRy2:Sx4 *-1 :2 +1;               [Базовое значение: 1/2 от мана-стоимости действия + 1]
!!en:;
!!if&x1=8:;                           [действия гильдии Чародеев]
  !!VRy1:S16;                         [тип действия 16]
  !!VRy2&x2=2:Sx3 :10;                [Ценность Карт Судьбы: 1 за каждые 10% шанса (3/4/5/6)]
  !!VRy2&x2=2:Sx3 :10;                [Ценность перекачки маны: 1 +1 за каждые 10 маны врага]
  !!VRy2&x2=3:Sx3 :10 +1;             [Ценность Дракона маны: 1 +1 за каждые 10 здоровья ДМ]
!!en:;
!!if&x1=9:;                           [выбор специализации]
  !!VRx5:Sv9903;                      [специализации из схемы]
  !!VRx5&x5=0:S1 R5;                  [если специализация не задана - случайная]
  !!FU:E;                             [выход]
!!en:;
!!VRy1:+9900;                         [y1 - индекс v-переменной со значением бонуса действия]
!!VRy2:*y3;                           [y2 - базовая стоимость с учетом удвоенного эффекта]
***
*** Расчет ценности карты на основании базовой ценности и схемы предпочтений
!!VRy4:S0;                            [инициализация y4]
!!VRy5:S0;                            [инициализация y5]
!!VRy6:S0;                            [инициализация y6]
!!FU9043&v9917>0:Px1/x2/x3/?y4/?y5/?y6;[y4/y5 - признак наличия бонуса/бонус ценности карты в расширенном описании схемы]
!!VRx5&y4=0:Sy3 *vy1 +y2;             [расчитываем ценность по базовому описанию]
!!VRx5&y4=1:Sy3 *y5 +y2;              [расчитываем ценность для расширенного описания бонуса "+"]
!!VRx5&y4=2:Sy5 *y3;                  [расчитываем ценность для расширенного описания бонуса "="]
!!VRx5&y4=3:Sy2 *y5;                  [расчитываем ценность для расширенного описания бонуса "*"]
!!VRx5&y4=4:Sy3 *y5 -y2 *-1;          [расчитываем ценность для расширенного описания бонуса "1"]
!!VRzx8&v9900>0:+^ [%X5]^;            [добавление ценности действия к сообщению для всех режимов отладки]
***
*** Корректировки поведения ИИ в специфических боевых ситуациях
*** Герой-маг (SP:7+,Kn:7+) при высоком сопротивлении вражеского командира (70+) не учит ударную магию, но начинает учить заклинания призыва
!!if&x1=2:;                           [Изучение заклинания]
  !!HEx6:Fd/d/?i/?j;                  [i,j - Сила, Знание героя]
  !!HEx7:N?k; !!COk:P6/?l;            [l - сопротивление вражеского командира]
  !!if&i>=7/j>=7/l>=70:;              [при выполнении условий на Силу/Знание и сопротивление вражеского командира]
    !!VRx5&x2>=15/x2<=26:S0;          [ценность ударной магии снижена до 0]
    !!VRx5&x2>=66/x2<=69:S100;        [ценность призывной магии повышена до 100]
  !!en:;
!!en:;
***
*** DEBUG SECTION
!!if&v9900=1:;
  !!VRy9:Svy1;
  !!VRy1:%100;
  !!VRz77:Szx8;
  !!VRy10:Sx1 *1000 +x2 *100 +x3;
  !!VRy11:Sx1 *1000 +x2;
  !!VRy12:Sx1;
  !!VRz78&y6=3:S^{~o}[%Y10]{~} [%Y11] [%Y12]^;
  !!VRz78&y6=2:S^[%Y10] {~o}[%Y11]{~} [%Y12]^;
  !!VRz78&y6=1:S^[%Y10] [%Y11] {~o}[%Y12]{~}^;
  !!VRz78&y6=0:S^[%Y10] [%Y11] [%Y12]^;
  !!IF&y4=0:M^{~o}%Z77{~}
=======================================
Sheme:             %V9917
Card:                %X1 / %X2 / %X3
Templ.:            %Z78
{~o}Value:              %X5{~}

Base Value:    {~o}%Y2{~} (x{~o}%Y3{~})

Category:        {~o}%Y1{~}
Base Bonus:   {~o}%Y9{~}
Bonus Type:   %Y4
Ext. Bonus:     %Y5


^;
  !!IF&y4>0:M^{~o}%Z77{~}
=======================================
Sheme:             %V9917
Card:                %X1 / %X2 / %X3
Templ.:            %Z78
{~o}Value:              %X5{~}

Base Value:    {~o}%Y2{~} (x{~o}%Y3{~})

Category:        %Y1
Base Bonus:   %Y9
Bonus Type:   {~o}%Y4{~}
Ext. Bonus:     {~o}%Y5{~}


^;
!!en:;
*** END OF DEBUG SECTION

!?FU9043;                             [возврат в x4/x5 признака/бонуса расширенной схемы для карты x1/x2/x3]
!!VRx4:S0;
!!FU&x1=9:E;                          [выход если выбор специализации (нет расширенных схем)]
!!VRx1&x1=10:S9;                      [10->9 тип действия для расширенных схем]
!!SN:W^BH.AI.Sheme^/?z77;             [z77 - имя файла схемы]
!!VRz78:S^scheme^;                    [z78 - секция "scheme"]
**
!!VRy1:Sx1 *1000 +x2 *100 +x3;        [поиск совпадения по трем параметрам]
!!VRz79:S^^;                          [обнуление z79]
!!UN:N6/z79/y1/78/77;                 [z79 - текстовое значение опции]
!!VRz79:H7;                           [проверка на пустое значение]
!!if&7:;                              [если есть совпадение]
  !!VRz80:S^^ M1/z79/0/1 H8;          [z80 - модификатор]
  !!if&-8:;                           [исключение: нет модификатора]
    !!IF:M^AI DEBUG: empty modifier
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRy2:S-1;                         [y2 - код модификатора]
  !!VRz80:U^+^; !!VRy2&1:S1;          [модификатор +]
  !!VRz80:U^=^; !!VRy2&1:S2;          [модификатор =]
  !!VRz80:U^*^; !!VRy2&1:S3;          [модификатор *]
  !!VRz80:U^-^; !!VRy2&1:S4;          [модификатор -]
  !!if&y2=-1:;                        [исключение: неизвестный модификатор]
    !!IF:M^AI DEBUG: unknown modifier
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRz81:S^^ M1/z79/1/5 M2/z81/0 H9; [z81 - значение]
  !!if&-9:;                           [исключение: нет значения]
    !!IF:M^AI DEBUG: empty value
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRx4:Sy2;                         [x4 - тип бонуса]
  !!VRx5:Vz81;                        [возвращаем бонус]
  !!VRx6:S3;                          [возвращаем шаблон]
  !!FU:E;                             [и прерываем поиск]
!!en:;
**
!!VRy1:Sx1 *1000 +x2;                 [поиск совпадения по двум параметрам]
!!VRz79:S^^;                          [обнуление z79]
!!UN:N6/z79/y1/78/77;                 [z79 - текстовое значение опции]
!!VRz79:H7;                           [проверка на пустое значение]
!!if&7:;                              [если есть совпадение]
  !!VRz80:S^^ M1/z79/0/1 H8;          [z80 - модификатор]
  !!if&-8:;                           [исключение: нет модификатора]
    !!IF:M^AI DEBUG: empty modifier
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRy2:S-1;                         [y2 - код модификатора]
  !!VRz80:U^+^; !!VRy2&1:S1;          [модификатор +]
  !!VRz80:U^=^; !!VRy2&1:S2;          [модификатор =]
  !!VRz80:U^*^; !!VRy2&1:S3;          [модификатор *]
  !!VRz80:U^-^; !!VRy2&1:S4;          [модификатор -]
  !!if&y2=-1:;                        [исключение: неизвестный модификатор]
    !!IF:M^AI DEBUG: unknown modifier
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRz81:S^^ M1/z79/1/5 M2/z81/0 H9; [z81 - значение]
  !!if&-9:;                           [исключение: нет значения]
    !!IF:M^AI DEBUG: empty value
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRx4:Sy2;                         [x4 - тип бонуса]
  !!VRx5:Vz81;                        [возвращаем бонус]
  !!VRx6:S2;                          [возвращаем шаблон]
  !!FU:E;                             [и прерываем поиск]
!!en:;
**
!!VRy1:Sx1;                           [поиск совпадения по одному параметру]
!!VRz79:S^^;                          [обнуление z79]
!!UN:N6/z79/y1/78/77;                 [z79 - текстовое значение опции]
!!VRz79:H7;                           [проверка на пустое значение]
!!if&7:;                              [если есть совпадение]
  !!VRz80:S^^ M1/z79/0/1 H8;          [z80 - модификатор]
  !!if&-8:;                           [исключение: нет модификатора]
    !!IF:M^AI DEBUG: empty modifier
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRy2:S-1;                         [y2 - код модификатора]
  !!VRz80:U^+^; !!VRy2&1:S1;          [модификатор +]
  !!VRz80:U^=^; !!VRy2&1:S2;          [модификатор =]
  !!VRz80:U^*^; !!VRy2&1:S3;          [модификатор *]
  !!VRz80:U^-^; !!VRy2&1:S4;          [модификатор -]
  !!if&y2=-1:;                        [исключение: неизвестный модификатор]
    !!IF:M^AI DEBUG: unknown modifier
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRz81:S^^ M1/z79/1/5 M2/z81/0 H9; [z81 - значение]
  !!if&-9:;                           [исключение: нет значения]
    !!IF:M^AI DEBUG: empty value
    Sheme:%Z77
    Parameter: %Y1^;                  [сообщение]
    !!FU:E;                           [выход]
  !!en:;
  !!VRx4:Sy2;                         [x4 - тип бонуса]
  !!VRx5:Vz81;                        [возвращаем бонус]
  !!VRx6:S1;                          [возвращаем шаблон]
  !!FU:E;                             [и прерываем поиск]
!!en:;
**
!!VRx4:S0;                            [выставляем признак ненайденого бонуса]
!!VRx5:S0;                            [выставляем признак ненайденого бонуса]
!!VRx6:S0;                            [выставляем признак ненайденого бонуса]

!?FU9024;                             [Загрузка из ini-файла параметров схемы предпочтений в v9901-v9915, x1 - номер ИИ героя]
** Определение общих параметров
!!VRz1:S^BattleHeroes.ini^;           [z1 - имя ini-файла]
!!VRz2:S^schemes^;                    [z2 - секция управления схемами]
!!UN:N6/z3/0/2/1;                     [z3 - текстровое значение опции 0 (количество схем)]
!!VRy1:Vz3;                           [y1 - числовое значение опции 0. Количество схем для случайного выбора.]
!!VRy1&y1>0:-1;                       [y1 - Количество схем для случайного выбора - 1]
!!UN:N6/z3/1/2/1;                     [z3 - текстровое значение опции 1 (принудительный выбор схемы)]
!!VRy2:Vz3;                           [y2 - числовое значение опции 1. Принудительный номер схемы.]
!!if&y2=0:;                           [если номер схемы не задан принудительно в ini-файле]
  !!VRy3&y2=0:S1 Ty1;                 [y3 - Случайный номер схемы (1..n), если не задан принудительный]
  !!HEx1:E?y4;                        [y4 - указатель на используемую героем схему предпочтений...]
  !!VRy4:%1000;                       [...последние 3 цифры опыта героя]
  !!VRy3&y4<=y1/y4>0/y4<999:Sy4;      [если схема существует, она выбирается. указатель 0 оставляет случайную схему]
!!el:;
  !!VRy3:Sy2;                         [если задан принудителный выбор схемы, выбираем ее]
!!en:;

!!UN:N6/z3/2/2/1;                     [z3 - текстровое значение опции 2 (режим отладки)]
!!VRv9900:Vz3;                        [v9900=0..2 - тип режима отладки]
!!UN:N6/z3/3/2/1;                     [z3 - текстровое значение опции 3 (путь к каталогу схем)]
!!SN:W^BH.AI.Sheme^/^%Z3\AI_%Y3.txt^; [Сохраняем путь к файлу схемы в *BH.AI.Sheme*]
!!SN:W^BH.AI.LOG^/^AI_%Y3^;           [Сохраняем имя схемы как базу для имени лога ИИ]
!!UN:N6/z3/4/2/1;                     [z3 - текстровое значение опции 4 (логирование ИИ)]
!!VRy2:Vz3;                           [y2 - числовое значение опции 4]
!!SN:W^BH.AI.LOG^/y2;                 [сохраняем параметр "логирование ИИ"]
!!VRv9901:C0/0/0/0/0/0/0/0/0/0/0/0/0/0/0/0; [обнуляем параметры схемы (схема по умолчанию)]
!!VRv9917:S0;                         [обнуляем номер используемой схемы]
!!if&y3>0:;                           [если схема выбрана, считываем ее параметры]
  !!VRv9917:Sy3;                      [сохраняем номер используемой схемы]
  !!SN:W^BH.AI.Sheme^/?z1;            [z1 - файл схемы]
  !!VRz2:S^scheme^;                   [z2 - секция схемы]
  !!UN:N6/z3/0/2/1;                   [z3 - параметры схемы (строка базового описания)]
  !!DO9025/0/15/1:P;                  [если схема существует, разбираем параметры базового описания]
!!en:;

!?FU9025;                             [слово x16 из строки z3 складывается в v(9901+x16). При возникновении ошибки цикл прерывается и устанавливаются параметры по умолчанию. В z2 помещается текст ошибки]
!!VRz1:M2/z3/x16 H1;                  [выделяем слово из z3 в z1 и устанавливаем f1=0, если слово отсутствует (пустое)]
!!if&1:;                              [если слово получено]
  !!VRy1:S9901 +x16;                  [y1 - индекс v-переменной]
  !!VRvy1:Vz1;                        [сохраняем параметр в v-переменную]
!!el:;
  !!VRv9901:C0/0/0/0/0/0/0/0/0/0/0/0/0/0/0/0; [обнуляем параметры схемы (схема по умолчанию)]
  !!VRv9917:S0;                       [обнуляем номер используемой схемы]
  !!VRx16:S20;                        [прерываем цикл]
!!en:;

!?FU9038;                             [Подсчет в v1 призванных существ типа x1 (для стрелков учитываются только отряды с боезапасом > 4)]
!!BMx16:N?y1 T?y2;                    [y1 - количество/тип существ в отряде]
!!FU|y1<1/y2<>x1:E;                   [выход, если отряд не существует или не требуемого типа]
!!VRv1:+y1;                           [v1 - количество существ в отряде]
!!BMx16:F?y3 U3/?y4;                  [y3/y4 - флаги/выстрелы отряда]
!!BMx16:G62/?y5/?i G70/?y6/?i G74/?y7/?i G75/?y8/?i G60/?y9/?i G59/?y10/?i; [y5-y10 - слепота/окаменение/паралич/старость/гипноз/берсерк]
!!VRy3:&4;                            [y3=4 для стреляющего отряда]
!!VRv2&y3>0/y4<4:S1;                  [v2 - признак ограниченности для стрелков с боезапасом < 4]
!!VRv2|y5>0/y6>0/y7>0/y8>0/y9>0/y10>0:S1; [v2 - признак ограниченности для отряда под слепотой, окаменением, параличом, старостью]
!!VRx16:S9001;                        [прерывание цикла]

!?FU9026;                             [x1=0: увеличивает на v1 на 1, если x16 - отряд стрелков с боезапасом менее 4]
** x1=1: возвращает в x2 первого найденного стрелка с боезапасом менее 4
!!BMx16:F?y1 U3/?y2 N?y3;             [y1 - флаги отряда, y2 - кол-во выстрелов, y3 - кол-во существ в отряде]
!!VRx2:S-1;                           [пока не найден отряд x2=-1]
!!VRy1:&4;                            [y1>0 для стрелков]
!!VRv1&x1=0/y1>0/y2<4/y3>0:+1;        [x1=0: v1+=1 если отряд - стрелок с боезапасом менее 4]
!!VRx2&x1=1/y1>0/y2<4/y3>0:Sx16;      [x1=1: возвращаем номер стрелка в x2...]
!!VRx16&x1=1/y1>0/y2<4/y3>0:S50;      [... и прерываем поиск]

!?BG0&-997;                           [перед действием в бою (локальная битва)]
!!FU:E;
!!BG:A?y1 N?y2 E?y3 D?y20 S?y10 Q?y4; [y1/y2/y3/y20/y10/y4 - действие/отряд/цель/место цели/место атаки/сторона (0/1)]
!!BA&y4=0:O?i/?j; !!BA&y4=1:O?j/?i;   [i - игрок-владелец активного отряда]
!!OW:Ii/?j;  !!FU|j=0/y1<>6:E;        [выход, если не рукопашная атака ИИ]
!!BMy2:P?y5; !!FU&y5<>y10:E;          [выход, если не атака с места]
** позиции двухгексовых существ
** полет
!!VRy11:Sy10 :17; !!VRy12:Sy10 %17;   [y11/y12 - номер ряда, места атакующего]
!!VRy21:Sy20 :17; !!VRy22:Sy20 %17;   [y21/y22 - номер ряда, места защищающегося]
!!VRy6:Sy21 %2;                       [y6 - четность ряда защищающегося (0/1)]
!!FU&y11=y21/y4=1/y12<y22:E;          [выход, если атакующий максимально близко к вражеской стороне]
!!FU&y11=y21/y4=0/y12>y22:E;          [...]
!!FU&y4=0/y6=0/y12>y22:E;             [...]
!!FU&y4=1/y6=0/y12<=y22:E;            [...]
!!FU&y4=0/y6=1/y12>=y22:E;            [...]
!!FU&y4=1/y6=1/y12<y22:E;             [...]
