ZVSE
ERMS_ScriptDate=12.4(April).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

*** Укрепление боевых гномов: после получения урона отряд имеет шанс (равный кол-ву гномов) повысить свою защиту на 1.

!?PI;                         [пост-инструкция: установка описаний]
!!VRz873:Sz179263;            [получаем из ert новую подсказку по существу]
!!UN:G1/17/2/873;             [установка новой подсказки]
!!MA:F17/?y1;                 [y1 - FV боевых гномов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F17/y1;                  [обновляем FV боевых гномов]
!!VRv7906:S-1;                [инициализация v7906]

!?MF1&v7906=-1;               [при нанесении урона]
!!MF:N?y1;                    [y1 - номер отряда, получающего урон]
!!BMy1:T?y2;                  [y2 - тип отряда, получающего урон (17 - боевые гномы)]
!!VRv7906&y2=17:Sy1;          [v7906 - номер отряда боевых гномов, получающих физ. урон]

!?BG1&v7906>-1;               [после действия]
!!BU:C?y2;                    [y2=1, если бой закончен]
!!VRv7906&y2=1:S-1;           [сброс v7906, если бой закончен]
!!FU&y2=1:E;                  [выход если бой закончен]
!!BMv7906:N?y1;               [y1 - текущее количество гномов в отряде]
!!VRv7906&y1<1:S-1;           [сброс v7906 если отряд уничтожен]
!!FU&y1<1:E;                  [выход, если отряд уничтожен]
!!VRy2:S0 R99;                [y2 - случайное число 0..99]
!!if&y2<y1:;                  [если абилка сработала]
  !!VRz1:Sz179264;            [получаем текст для вывода в лог битвы из ert]
  !!MM:Sz1;                   [вывод текста в лог битвы]
  !!VRz1:S^SWRDDFND^;         [z1 - звуковой файл]
  !!SN:P1;                    [вывод звука]
  !!BMv7906:V85 Dd1;          [анимация и укрепление отряда]
!!en:;
!!VRv7906:S-1;                [сброс v7906]

*** Рогатые демоны получают способность способность перед атакой впадать в ярость, теряя 1 защиты, но увеличивая на 3 силу атаки.

!?PI;
!!VRz872:Sz179265;            [получаем из ert новую подсказку по существу]
!!UN:G1/49/2/872;             [установка новой подсказки]
!!MA:F49/?y1;                 [y1 - FV рогатых демонов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F49/y1;                  [обновляем FV]

!?BG0;
!!BG:N?y1 A?y2;               [y1/y2 - активный отряд/тип действия]
!!BMy1:T?y3;                  [y3 - тип существ в активном отряде]
!!FU|y2<>6/y3<>49:E;          [выход, если не атака рогатых демонов]
!!BMy1:D?y4;                  [y4 - текущая защита рогатых демонов]
!!if&y4>0:;                   [если защиту можно уменьшить]
  !!VRz1:Sz179266;            [получаем текст для вывода в лог битвы из ert]
  !!MM:Sz1;                   [вывод текста в лог битвы]
  !!VRz1:S^FRENZY^;           [z1 - звуковой файл бешенства]
  !!SN:P1;                    [вывод звука]
  !!BMy1:Dd-1 Ad3 V17;        [снижение защиты, повышение атаки и анимация бешенства]
!!en:;

*** Зомби получают возможность при добивании вражеского отряда живых существ поедать их трупы и восстанавливаться.

!?PI;                         [пост-инструкция: установка описаний]
!!VRz871:Sz179267;            [получаем из ert новую подсказку по существу]
!!UN:G1/59/2/871;             [установка новых текстов]
!!MA:F59/?y1;                 [y1 - FV зомби]
!!VRy1:*110 :100;             [увеличиваем FV на 10%]
!!MA:F59/y1;                  [обновляем FV зомби]

!?BG0;                        [перед действием]
!!VRv7903:C-1/-1/-1;          [обнуляем v-переменные]
!!BG:N?y1 A?y2;               [y1 - активный отряд, y2 - действие (6 - атака)]
!!BMy1:T?y3;                  [y3 - тип атакующих существ (59 - зомби)]
!!FU|y3<>59/y2<>6:E;          [выход если не атака зомби]
!!BG:E?y2;                    [y2 - целевой отряд]
!!BMy2:F?y3;                  [y3 - флаги существ целевого отряда]
!!VRy4:Sy3 &16;               [y4=16, если существа целевого отряда живые]
!!FU&y4=0:E;                  [выход если зомби атакуют не отряд живых существ]
!!BMy2:N?y5 H?y6 L?y7;        [y5/y6/y7 - количество существ/макс. здоровье/потер. здоровье целевого отряда]
!!VRy5:*y6 -y7;               [y5 - суммарное здоровье целевого отряда перед атакой]
!!VRv7903:Cy1/y2/y5;          [v7903/v7904/v7905 - номер атакующего отряда/номер целевого отряда/здоровье целевого отряда]

!?MF1&v7903>=0;               [при нанесении урона]
!!MF:F?y1;                    [y1 - урон, который получит целевой отряд]
!!if&y1<v7905:;               [если атака не уничтожит целевой отряд полностью]
  !!VRv7903:C-1/-1/-1;        [обнуляем v-переменные]
  !!FU:E;                     [выход]
!!en:;
!!BMv7903:N?y1 H?y2 L?y3 B?y4;[y1/y2/y3/y4 - количество существ/макс. здоровье/потер. здоровье/начальное количество отряда зомби]
!!if&y1=y4/y3=0:;             [если атака не уничтожит целевой отряд полностью]
  !!VRv7903:C-1/-1/-1;        [обнуляем v-переменные]
  !!FU:E;                     [выход если у отряда зомби нет потерь и раненных]
!!en:;
!!VRy5:Sy1 *y2 -y3 +v7905;    [y5 - новое суммарное здоровье отряда зомби]
!!VRy6:Sy4 *y2;               [y6 - максимально допустимое здоровье отряда зомби]
!!VRy5&y5>y6:Sy6;             [y5 - фактическое здоровье отряда зомби после трупоедства]
!!VRy6:Sy5 :y2;               [y6 - количество нераненых сомби после трупоедства]
!!VRy7:Sy5 %y2;               [y7 - здоровье раненого зомби]
!!VRy6&y7>0:+1;               [добавляем раненого зомби к общему количеству]
!!VRy8&y7>0:Sy2 -y7;          [y8 - потерянное здоровье раненого зомби]
!!VRz1:Sz179268;              [получаем текст для вывода в лог битвы из ert]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!VRz1:S^ANIMDEAD^;           [z1 - звуковой файл поднятия нежити]
!!SN:P1;                      [вывод звука поднятия нежити]
!!BMv7903:V74 Ny6 Ly8;        [анимация вампирской регенерации и восстановление отряда зомби]
!!BMv7904:F?y1 N?y10;         [y1/y10 - флаги/количество существ целевого отряда]
!!VRy1:|813694976;            [добавляем цели флаги жертвы/клона/неизменяемости цвета]
!!BMv7904:Fy1 E0 K1;          [обновляем флаги и убиваем цель без возможности воскрешения]
!!VRi:S9920 +v7904;           [i - индекс счетчика Некромантии для уничтоженного отряда]
!!VRvi:-y10;                  [уменьшаем начальное кол-во сущест в отряде на уничтоженное количество...]
!!VRvi&v1<0:S0;               [...чтобы они не учитывались при подъеме Некромантией]
!!VRv7903:C-1/-1/-1;          [обнуляем v-переменные]

*** Ловкость Хоббитов позволяет им уклоняться от атаки или заклинания урона не направленного непосредственно на них.
*** Эффекты обездвиживания или изменения сознания отменяют способность к уклонению.

!?PI;
!!VRz870:Sz179269;            [получаем из ert новую подсказку по существу]
!!UN:G1/138/2/870;            [установка новой подсказки]
!!MA:F138/?y1;                [y1 - FV хоббитов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F138/y1;                 [обновляем FV]

!?MF1;
!!BG:D?y1;                    [y1 - атакуемый гекс]
!!MF:W?y2 N?y3;               [y2/y3 - тип урона/отряд получающий урон]
!!BMy3:T?y4 P?y5 N?y6;        [y4/y5/y6 - тип/позиция/численность отряда, получающего урон]
!!BG:N?y7;                    [y7 - активный отряд]
!!FU|y2<>0/y4<>138/y1=y5/y3=y7:E; [выход, если урон рва/башен, урон получают не Хоббиты, урон прямой или ответная атака]
!!BMy3:G59/?y11/d G60/?y12/d G61/?y13/d G62/?y14/d G70/?y15/d G72/?y16/d G74/?y17/d; [y11-y17 - длительность заклинаний обездвиживания/разума]
!!FU|y11>0/y12>0/y13>0/y14>0/y15>0/y16>0/y17>0:E; [выход, если на Хоббитах есть заклинание обездвиживания/разума]
!!MF:E0;                      [отмена урона]
!!VRz1:S179270;               [z1 - текст сообщения для вывода в лог]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!VRz1:S^FDFLWNCE^;           [z1 - звуковой файл]
!!SN:P1;                      [вывод звука]
!!BMy3:V86;                   [анимация блок урона урона на отряде]

!?MR1;
!!MR:M?y1;                    [y1 - тип существа получающего урон]
!!FU&y1<>138:E;               [выход, если урон получают не Хоббиты]
!!BG:A?y1 D?y2;               [y1/y2 - действие/атакуемый гекс]
!!FU&y1<>1/y1<>10:E;          [выход, если не каст заклинания]
!!UN:C42231940/4/?y11;        [спасибо за код Berserker'у]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y3;              [y3 - гекс отряда, получающего урон]
!!FU|y3<0/y3>186:E;           [выход, если гекс за пределами поля]
!!BU:Ey3/?y4;                 [y4 - номер отряда на позиции]
!!FU&y4<0:E;                  [выход, если живого отряда нет]
!!BMy4:N?y6;                  [y6 - численность отряда, получающего урон]]
!!FU|y2=y3/y6<1:E;            [выход, если урон прямой или отряд мертв]
!!BMy4:G59/?y11/d G60/?y12/d G61/?y13/d G62/?y14/d G70/?y15/d G72/?y16/d G74/?y17/d; [y11-y17 - длительность заклинаний обездвиживания/разума]
!!FU|y11>0/y12>0/y13>0/y14>0/y15>0/y16>0/y17>0:E; [выход, если на Хоббитах есть заклинание обездвиживания/разума]
!!MR:F0;                      [обнуление магического урона]
!_!VRz1:S179270;               [z1 - текст сообщения для вывода в лог]
!_!MM:Sz1;                     [вывод текста в лог битвы]
!_!VRz1:S^FDFLWNCE^;           [z1 - звуковой файл]
!_!SN:P1;                      [вывод звука]
!_!BMy4:V86;                   [анимация блок урона урона на отряде]

*** Привидения - шанс уклониться от физической атаки равен защите отряда
!?PI;                         [пост-инструкция: установка описаний]
!!VRz869:Sz179271;            [получаем из ert новую подсказку по существу]
!!UN:G1/159/2/869;            [установка новых текстов]
!!MA:F159/?y1;                [y1 - FV Привидения]
!!VRy1:*110 :100;             [увеличиваем FV на 5%]
!!MA:F159/y1;                 [обновляем FV Привидения]

!?MF1;
!!MF:N?y1;                    [y1 - отряд получающий урон]
!!BMy1:T?y2 D?y3;             [y2/y3 - тип/защита отряда]
!!VRy4:S0 R99;                [y4 = 0..99]
!!FU|y2<>159/y3<=y4:E;        [выход, если атакуют не привидений или если уклонение сработало]
!!MF:E0;                      [отмена урона]
!!VRz1:S179272;               [z1 - текст сообщения для вывода в лог]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!VRz1:S^FDFLWNCE^;           [z1 - звуковой файл]
!!SN:P1;                      [вывод звука]
!!BMy1:V86;                   [анимация блок урона на отряде]

*** Головорезы получают дополнительный ход при добивании вражеского отряда.

!?PI;                         [установка описаний]
!!VRz864:Sz179273;            [получаем из ert новую подсказку]
!!UN:G1/143/2/864;            [установка подсказки]
!!MA:F143/?y1;                [y1 - FV Головорезов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F143/y1;                 [обновляем FV/флаги боевых гномов]

!?BA52;                       [перед боем]
!!SN:W^opt758s^/-1 W^opt758d^/-1 W^opt758m^/-1; [сбрасываем номера атакующего отряда и цели]

!?BG0;                        [перед действием]
!!BG:A?y1 N?y2 E?y3;          [y1/y2/y3 - тип действия/отряд/цель]
!!FU|y1<>6/y2<0/y3<0:E;       [выход, если не атака ближнего боя, нет отряда или цели]
!!BMy2:T?y4;                  [y4 - тип атакующего отряда]
!!SN&y4=143:W^opt758s^/y2 W^opt758d^/y3; [если атака Головорезов, сохраняем номера атакующего отряда и цели]

!?FU77007;                    [фаза регенерации]
!!SN:W^opt758s^/?y1 W^opt758d^/?y2; [y1/y2 - атакующий отряд/цель]
!!FU&y1<0/y2<0:E;             [выход, если не было атаки Головорезов]
!!BMy1:N?y3;                  [y3/y4 - количество существ в отрядах]
!!BMy2:N?y4;                  [...]
!!BMy1:T?y5;                  [y5 - тип отряда головорезов]
!!if|y3<1/y4>0/y5<>143:;      [если головорезы убиты/преобразованы или не убит целевой отряд]
  !!SN:W^opt758s^/-1 W^opt758d^/-1; [обнуляем номера]
  !!FU:E;                     [и выходим]
!!en:;
!!SN&y1<21:W^opt758m^/0;      [устанавливаем признак стороны Головорезов]
!!SN&y1>20:W^opt758m^/1;      [...]
!!SN:X?i/?j/1;                [блокируем фазу регенерации]
!!SN:Q;                       [прерывание фазы]

!?FU77006;                    [при получении хода в бою]
!!SN:W^opt758s^/?y1 W^opt758d^/?y2 W^opt758m^/?y3; [y1/y2/y3 отряд/цель/сторона]
!!FU|y1<0/y2<0/y3<0:E;        [выход, если одно из значений не установлено]
!!VRz1:Sz179274;              [z1 - текст сообщения для вывода в лог]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!VRz1:S^MIRTH^;              [z1 - звуковой файл морали]
!!SN:P1;                      [вывод звука морали]
!!BMy1:V20;                   [анимация морали на отряде]
!!VRy1&y1>20/y3=1:-21;        [y1 - номер отряда (0..20)]
!!SN:Xy3/y1;                  [передаем ход головорезам]
!!SN:W^opt758s^/-1 W^opt758d^/-1 W^opt758m^/-1; [обнуляем номера]
!!SN:Q;                       [прерывание фазы]

** Орки на кабанах имеюш шанс при атаке ошеломить противника (действует только на живых существ).
** Ошеломленный отряд теряет способность ходить и контратаковать в текущем раунде.
** Шанс ошеломления тем больше, чем сильнее отряд орков на кабанах и слабее их противник.
** FV орков на кабанах повышено на 10%.

!?PI;
!!VRz863:Sz179275;            [получаем из ert новую подсказку по существу]
!!UN:G1/140/2/863;            [установка новой подсказки]
!!MA:F140/?y1;                [y1 - FV кабанов]
!!VRy1:*110 :100;             [увеличиваем FV на 10%]
!!MA:F140/y1;                 [обновляем FV]

!?MF1;
!!BG:N?y1;                    [y1 - активный отряд]
!!BMy1:T?y2 N?y3 H?y4 L?y5;   [y2/y3/y4/y5 - тип/численность/макс./потерянное здоровье активного отряда]
!!FU&y2<>140:E;               [выход, если атакуют не кабаны]
!!MF:N?y6;                    [y6 - отряд получающий урон]
!!FU&y1=y6:E;                 [выход, если контрудар]
!!BMy6:F?y7 N?y8 H?y9 L?y10;  [y7/y8/y9/y10/y11 - флаги/численность/макс./потерянное здоровье целевого отряда]
!!VRy11:Sy7 &16;              [y11>0 если в отряде получающем урон "живые" существа]
!!FU&y11=0:E;                 [выход, если целевой отряд - не "живой"]
!!VRy4:*y3 -y5;               [y4 - суммарное здоровье отряда Кабанов]
!!VRy8:*y9 -y10 +y4;          [y8 - суммарное здоровье целевого отряда и отряда Кабанов]
!!FU&y8<1:E;                  [выход, если целевой отряд уже мертв]
!!VRy4:*100 :y8;              [шанс срабатывания оглушения (в %)]
!!VRy11:S1 R99;               [y11 - случайное число (1..100)]
!!FU&y4<y11:E;                [выход, если оглушение не сработало]
!!VRy7:|67108864;             [y7 +флаг отряд уже ходил в этом раунде]
!!BMy6:R0 Fy7;                [цель теряет возможность контратаковать и ходить в текущем раунде]
!!VRz1:Sz179276;              [z1 - текст для вывода в лог битвы]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!BMy6:V82;                   [анимация на вражеском отряде]

*** Адские отродья при получении хода приносят в жертву самых слабых сущест армии героя для призыва демонов.
*** Здоровье и численность призвываемых демонов не превышает здоровье и численность жертвенных существ.
*** Максимальное количество призвываемых демонов не превышает количество Адских Отродий.

!?PI;
!!VRz862:Sz179277;            [получаем из ert новую подсказку по существу]
!!UN:G1/51/2/862;             [установка новой подсказки]
!!MA:F51/?y1;                 [y1 - FV отродий]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F51/y1;                  [обновляем FV]

!?FU77006;                    [при получении хода в бою]
!!SN:X?y2/?y1;                [y1/y2 - отряд на стороне/сторона]
!!VRy1&y2=1:+21;              [y1 - номер отряда]
!!BMy1:T?y3 F?y4;             [y3/y4 - тип/флаги отряда]
!!VRy4:&16777216;             [y4>0 для отряда уже колдовавшего в этом раунде]
!!FU|y3<>51/y4>0:E;           [выход, если отряд в ожидании или не отродья]
!!VRy4:Sy2 *21;               [y4..y5 - отряды для поиска жертвы]
!!VRy5:Sy4 +20;               [...]
!!MA:F48/?y6 P48/?y7;         [y6 - FV демона (макс. FV существа-жертвы) y7 - здоровье демона (мин. здоровье отряда-жертвы)]
!!VRv1:C-1/100500;            [v1/v2 - номер/FV жертвенного отряда]
!!DO9059/y4/y5/1:Py6/y7;      [поиск номера отряда-жертвы]
!!FU&v1<0:E;                  [выход, если жертвенный отряд не найден]
!!VRy10:Sv1;                  [y10 - отряд-жертва]
!!BMy1:N?y8;                  [y8 - кол-во отродий]
!!VRy8:*y7;                   [y8 - макс. жертвенное здоровье]
!!BMy10:N?y11 H?y12 L?y13;    [y11-y13 - параметры жертвенного отряда]
!!VRy9:Sy11 *y12 -y13;        [y9 - здоровье жертвенного отряда]
!!VRy8&y8>y9:Sy9;             [y8 - жертвенное здоровье]
!!VRy14:Sy8 :y7;              [y14 - кол-во призываемых демонов]
!!VRy15:Sy14 *y7 -y8 *-1;     [y15 - потерянное здоровье демона]
!!VRy14&y15>0:+1;             [добавляем раненного демона, если осталось жертвенное здоровье]
!!VRz1:Sz179278;              [z1 - текст для вывода в лог битвы]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!BMy1:G-87/12/d G-86/0/d;    [анимация каста отродий]
!!BU:R;                       [...]
!!VRz1:S^SACRIF1^;            [z1 - звуковой файл жертвы]
!!SN:P1;                      [вывод звука жертвы]
!!BMy10:N?y16 B?y17 V51 Ky8;  [анимация жертвы на вражеском отряде, нанесение урона]
!!BU:R;                       [обновление боя]
!!BMy10:N?y18;                [y18 - новая численность жертвенного отряда]
!!VRy19:Sy16 -y18;            [y19 - кол-во пожертвованных существ]
!!VRy17&y17>=y19:-y19;        [y17 - макс.численность жертвенного отряда для воскрешения]
!!BMy10&y18>0:By17;           [уменьшаем макс.численность жертвенного отряда для воскрешения]
!!VRi:S9920 +y10;             [i - индекс счетчика Некромантии для уничтоженного отряда]
!!VRvi:-y19;                  [уменьшаем начальное кол-во сущест в отряде на уничтоженное количество...]
!!VRvi&v1<0:S0;               [...чтобы они не учитывались при подъеме Некромантией]
!!BMy1:F?y20;                 [добавляем отряду отродий флаг морали]
!!VRy20:|16777216;            [для предотвращения 2го призыва в одном раунде]
!!BMy1:Fy20;                  [...]
!!FU9010:P48/y2/y14/y15/3;    [призыв демонов на сторону героя]

!?FU9059;                     [поиск жертвенного отряда в v1. v2 - FV жертвенного отряда. x1- макс. FV существа-жертвы, x2 - мин. здоровье отряда-жертвы]
!!BMx16:T?y1 N?y2;            [y1/y2 - тип/численность отряда]
!!FU|y1<0/y2<1/y1=48/y1=51:E; [выход, если отряда нет или это отряд демонов]
!!BMx16:H?y3 L?y4 F?y5;       [y3/y4/y5 - макс.здоровье/потер.здоровье/флаги отряда]
!!VRy5:&16;                   [y5>0 для живых существ]
!!VRy6:Sy2 *y3 -y4;           [y6 - здоровье отряда]
!!MA:Fy1/?y7;                 [y7 - FV жертвенного существа]
!!VRy8:Sy7 *y2;               [y8 - FV жертвенного отряда]
!!FU|y5=0/y6<x2/y7>=x1/y8>=v2:E;[выход если существа отряда не живые, здоровья отряда недостаточно на одного демона, существа отряда не слабее демонов или отряд не самый слабый из подходящих]
!!VRv1:Sx16;                  [v1 - номер жертвенного отряда]
!!VRv2:Sy8;                   [v2 - FV жертвенного отряда]

** end
