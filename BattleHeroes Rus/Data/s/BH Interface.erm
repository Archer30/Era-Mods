ZVSE2
** Author orig.  : Algor
** Name          : Battle Heroes: dialogs

!#DC(BH_CHOSING_DLG_ID) = 72;
*** Показ диалога-подсказки

!?FU9030;                             [Показ диалога x1, пока зажата клавиша мыши (левая или правая)]
!!VRy1:Sv1;
!!SN:E7510739/1/x1;                   [find HDlg -> v1]
!!VRy2:Sv1;                           [v2 = HDlg]
!!if&y2<>0; 
  !!SN:E5864096/1;                    [stop animation]
  !!UN:Cy2/4/?v1;                     [v1 = dialog]
  !!SN:E6245264/2/v1;                 [show while holding mouse button]
  !!SN:E5864272/1;                    [continue animation]
  !!SN:E7518240/2/y2;                 [destroy HDlg]
!!en; 
!!VRv1:Sy1;
!_!SN:D;                               [обновление поля боя (вылет?)]

*** Подсказка командира DL70

!?FU(OnBattleScreenMouseClick)&i^mouse_action^=(MOUSE_RMB_PRESSED);                                [клик на поле боя]

!!if&i^mouse_battleHex^>=0/i^mouse_battleHex^<=186;
  !!BU:Ei^mouse_battleHex^/?(stackId:y);                         [y2 - номер живого отряда в позиции]
  !!if&(stackId)<>(NO_STACK); [выход, если нет отряда]
    !!if|(stackId)=i^bh_commander_stack_0^/(stackId)=i^bh_commander_stack_1^;[выход, если клик не на командира]
      !!CM:R0;                              [отмена стандартной реакции на клик]
      !!FU(BH_Dlg_CommanderStats_Build):P(stackId);[отображение диалога командира]
    !!en;
  !!en;
!!el&i^mouse_battleHex^>=252/i^mouse_battleHex^<=253;[Выход, если не ПКМ на герое]
  !!VR(side:y):Si^mouse_battleHex^ -252;
  !!FU(BH_Dlg_HeroStats_Build):P(side:y);                         [отображение диалога героя]
!!en;



!?FU(BH_Dlg_CommanderStats_Build);
!#VA(stackId:x);
!!VRy2:Sx1;                           [...]
!!DL70:N^cstat.txt^;                  [подгрузка 70 диалога (статус командира)]
!!if&-1;                              [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                             [и выходим]
!!en; 
!!VRy1&y2=i^bh_commander_stack_0^:Sv9998;               [y1 - Номер командира]
!!VRy1&y2=i^bh_commander_stack_1^:Sv9999;               [...]
!!COy1:N?z1 ;                         [z1 - имя командира]
!!DL70:A1/3/z1/1;                     [установка имени в диалог]
!!DO9029/0/9/1:Py1/y2;                [установка иконок навыков в диалог]
!!BMy2:T?y3 A?y11 D?y12 U3/?y13 U1/?y14 U2/?y15 H?y16 L?y17 S?y18 G213/?y20/d F?y4; [параметры отряда командира]
!!COy1:P6/?y21 P4/?y22;               [y21/y22 - сопротивление/сила командира]
!!VRy17:-y16 *-1;                     [y17 - текущее здоровье командира]
!!MA:Ny3/?y23;                        [y23 - макс. боезапас]
!!VRy4:&4;                            [y4=0, если командир не может стрелять]
!!VRz11:S^%Y11^;                      [z10-z19 - строки параметров для диалога]
!!VRz12:S^%Y12^;                      [...]
!!VRz13:S^%Y13 / %Y23^;               [...]
!!VRz13&y4=0:S^-^;                    [...]
!!VRz14:S^%Y14 - %Y15^;               [...]
!!VRz14&y14=y15:S^%Y14^;              [...]
!!VRz15:S^%Y16^;                      [...]
!!VRz16:S^%Y18^;                      [...]
!!VRz17:S^%Y21%%^;                    [...]
!!VRy30&y2=i^bh_commander_stack_0^:Sv9964;              [y30=1, если есть способность Повышенная регенерация]
!!VRy30&y2=i^bh_commander_stack_1^:Sv9965;              [...]
!!VRy30&y30=1:Sy16 :50;               [y36 - регенерация командира]
!!VRy30&y30=0:Sy16 :100;              [...]
!!VRz18:S^%Y17  (+%Y30)^;             [z1 - строка регенерации для диалога]
!!VRz19:S^%Y22^;                      [...]
!!VRz10:S^%Y20^;                      [...]
!!DL70:A30/3/z11/1 A31/3/z12/1 A32/3/z13/1 A33/3/z14/1 A34/3/z15/1 A35/3/z16/1 A36/3/z17/1 A2/3/z18/1 A38/3/z19/1 A39/3/z10/1; [установка параметров в диалог]
!!VRz11:S^csp00.pcx^;                 [иконка эффекта по умолчанию]
!!DL70:A40/11/z11/1 A41/11/z11/1 A42/11/z11/1 A43/11/z11/1 A44/11/z11/1; A45/11/z11/1 A46/11/z11/1 A47/11/z11/1; [сброс картинок эффектов]
!!VRz11:S^^;                          [длительность эффекта по умолчанию]
!!DL70:A50/3/z11/1 A51/3/z11/1 A52/3/z11/1 A53/3/z11/1 A54/3/z11/1; A55/3/z11/1 A56/3/z11/1 A57/3/z11/1; [сброс длительностей эффектов]
!!VRv1:S40;                           [инизиализация v1 - место для отображения эффекта]
!!DO9031/0/75/1:Py2;                  [установка иконок и длительностей эффектов в диалог]
!!VRy1&y2=i^bh_commander_stack_0^:S0;                   [y1 - сторона]
!!VRy1&y2=i^bh_commander_stack_1^:S1;                   [...]
!!SN:W^Cmndr%Y1Sp0^/?y10 W^Cmndr%Y1Sp1^/?y11 W^Cmndr%Y1Sp2^/?y12 W^Cmndr%Y1Sp3^/?y13 W^Cmndr%Y1Sp4^/?y14; [получение магии командира]
!!SN:W^Cmndr%Y1Sp5^/?y15 W^Cmndr%Y1Sp6^/?y16 W^Cmndr%Y1Sp7^/?y17 W^Cmndr%Y1Sp8^/?y18 W^Cmndr%Y1Sp9^/?y19; [...]
!!VRz11:S^cm%Y10.pcx^;                [формирование имен файлов]
!!VRz12:S^cm%Y11.pcx^;                [...]
!!VRz13:S^cm%Y12.pcx^;                [...]
!!VRz14:S^cm%Y13.pcx^;                [...]
!!VRz15:S^cm%Y14.pcx^;                [...]
!!VRz16:S^cm%Y15.pcx^;                [...]
!!VRz17:S^cm%Y16.pcx^;                [...]
!!VRz18:S^cm%Y17.pcx^;                [...]
!!VRz19:S^cm%Y18.pcx^;                [...]
!!VRz10:S^cm%Y19.pcx^;                [...]
!!DL70:A60/11/z11/1 A61/11/z12/1 A62/11/z13/1 A63/11/z14/1 A64/11/z15/1 A65/11/z16/1 A66/11/z17/1 A67/11/z18/1 A68/11/z19/1 A69/11/z10/1; [установка картинок в диалог]
!!BMy2:T?y11;                         [y11 - тип командира]
!!VRz11:S^card_c%Y11.pcx^;            [...]
!!DL70:A9/11/z11/1;                   [установка портрета командира в диалог]
!!UN:C4199496/2/?i C4199503/2/?j;     [i,j - размер экрана]
!!VRk:S-179 +i;                       [...]
!!VRi&x1<=20:-806 :2 +2;              [i,j - смещения диалога]
!!VRi&x1<=20/i<0:S0;                  [...]
!!VRi&x1>=21:-806 :2 +806 -180 -2;    [...]
!!VRi&x1>=21/i>k:Sk;                  [...]
!!VRj:-630 :2 +3;                     [...]
!!VRj&j<0:S0;                         [...]
!!FU88000:P70/i/j;                    [изменение координат диалога]
!!FU9030:P70;                         [отображение диалога]

!?FU9029;                             [x1 - номер командира, x2 - номер отряда командира, x16 - способность]
!!VRy1:Sx16;                          [y1 - способность]
!!VRy1&x16=1:S11;                     [...]
!!COx1:B1/y1/?y2;                     [y2 = 0/1 нет/есть способность]
!!VRy2&y1=11/x2=i^bh_commander_stack_0^:Sv9964;         [...]
!!VRy2&y1=11/x2=i^bh_commander_stack_1^:Sv9965;         [...]
!!if&y1=8;                            [проверка способности «Выносливость»]
  !!VRy98:Sx2 :21;                    [y98 - сторона командира]
  !!SN:W^BH.Commander%Y98.Hardiness^/?y2; [y2 - есть ли способность]
!!en; 
!!VRz11:S^css0%X16%Y2.pcx^;           [z1 - название файла с иконкой навыка]
!!VRy3:Sx16 +10;                      [y3 - номер элемента в диалоге]
!!DL70:Ay3/11/z11/1;                  [установка иконки в диалог]

!?FU9031;                             [x1 - отряд, x2 - номер диалога, x16 - эффект]
!!BMx1:Gx16/?y1/d;                    [y1 - продолжительность эффекта x16]
!!FU|y1<1/y1>99:E;                    [выход, если эффекта нет или эффект постоянный (огн. щит)]
!!BMx1:G32/?y3/d;                     [y1 - продолжительность эффекта защиты от стихий]
!!FU&x16=30/y1=y3:E;                  [не отображаем компоненты защиты от стихий]
!!FU&x16=31/y1=y3:E;                  [не отображаем компоненты защиты от стихий]
!!FU&x16=33/y1=y3:E;                  [не отображаем компоненты защиты от стихий]
!!VRy2:Sv1 +10;                       [y2 - номер элемента "длительность эффекта"]
!!VRz11:S^csp%X16.pcx^;               [z1 - иконка эффекта]
!!VRz12:S^х %Y1^;                     [z2 - текст эффекта]
!!DL70:Av1/11/z11/1 Ay2/3/z12/1;      [установка иконки и длительности эффекта в диалог]
!!VRv1:+1;                            [инкремент v1]
!!VRx16&v1>44:S100;                   [прерываем цикл, если места для отображения эффектов закончились]

*** Подсказка героя DL71



!?FU(BH_Dlg_HeroStats_Build);                             [отображение диалога героя (x1 - номер героя -10/-20)]
  !#VA(side:x);
  !!VRy1:Sx1;                           [...]
  !!HEi^battle_hero_%(side)^:B0/?z11 N?i O?j;               [z11/i/j - имя/номер/владелец героя]
  !!OW:Ij/?y2;                          [y2=1 для ИИ]
  !!FU&v7909<>0/v7909<>2/y2=1:E;        [выход, если просмотр состояния ИИ героя запрещен]
  !!CM:R0;                              [отмена стандартной реакции на клик]
  !!DL71:N^hstat.txt^;                  [подгрузка 71 диалога (статус героя)]
  !!if&-1;                              [если диалог не найден]
    !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
    !!FU:E;                             [и выходим]
  !!en; 
  !!DL71:A30/3/z11/1;                   [установка имени в диалог]
  *!VRz11:S^card_h%Vi.pcx^;             [названеи портрета героя]
  !!DL71:A80/(DLG_CMD_SET_DEF_FRAME)/i^battle_hero_%(side)^/1;                  [установка портрета в диалог]
  !!DO9028/1/27/1:P(side);                  [установка иконок навыков в диалог]
  !!HEy1:F?y11/?y12/?y13/?y14 I?y18/1;  [параметры героя]
  !!FU9047:Py1/?y19;                    [y19 - регенерация маны героя]
  !!VRy20:Sy14 *10 +10;                 [y20 - макс. мана героя.]
  !!VRz11:S^%Y11^;                      [z1-z5 - строки параметров для диалога]
  !!VRz12:S^%Y12^;                      [...]
  !!VRz13:S^%Y13^;                      [...]
  !!VRz14:S^%Y14^;                      [...]
  !!VRz15:S^%Y18 / %Y20 (+%Y19)^;       [...]
  !!DL71:A41/3/z11/1 A42/3/z12/1 A43/3/z13/1 A44/3/z14/1 A36/3/z15/1; [установка параметров в диалог]
  ** информация об отстройке зданий в городах
  !!HEy1:B2/?y2;                        [y2 - класс героя]
  !!VRy2::2;                            [y2 - родной город героя]
  !!VRy3|y2=0/y2=1/y2=2:S0;             [y3 - 1-й город для класса]
  !!VRy3|y2=3/y2=4/y2=5:S3;             [y3 - 1-й город для класса]
  !!VRy3|y2=6/y2=7/y2=8:S6;             [y3 - 1-й город для класса]

  !!if&y1=(BATTLE_LEFT);
    !!VRy4:Sv9980 -1 :2;           [y4 - уровень отстроенного здания в 1-м городе]
    !!VRy5:Sv9980 -1 %2;           [y5 - грейд отстроенного здания в 1-м городе]
  !!el;
    !!VRy4:Sv9983 -1 :2;           [...]
    !!VRy5:Sv9983 -1 %2;           [...]
  !!en;

  !!UN:Ty3/y4/y5/?y6;                   [y6 - тип существа]
  *!VRz12:S^card_c%Y6.pcx^;             [z12 - картинка 1-го существа]
  !!DL71:A82/(DLG_CMD_SET_DEF_FRAME)/y6/1;                  [установка картинок 1-го города]

  !!VRy3|y2=0/y2=1/y2=2:S1;             [y3 - 2-й город для класса]
  !!VRy3|y2=3/y2=4/y2=5:S4;             [y3 - 2-й город для класса]
  !!VRy3|y2=6/y2=7/y2=8:S7;             [y3 - 2-й город для класса]

  !!if&y1=(BATTLE_LEFT);
    !!VRy4:Sv9981 -1 :2;           [y4 - уровень отстроенного здания в 1-м городе]
    !!VRy5:Sv9981 -1 %2;           [y5 - грейд отстроенного здания в 1-м городе]
  !!el;
    !!VRy4:Sv9984 -1 :2;           [...]
    !!VRy5:Sv9984 -1 %2;           [...]
  !!en;

  !!UN:Ty3/y4/y5/?y6;                   [y6 - тип существа]
  *!VRz12:S^card_c%Y6.pcx^;             [z12 - картинка 2-го существа]

  !!DL71:A84/(DLG_CMD_SET_DEF_FRAME)/y6/1;                  [установка картинок 2-го города]
  !!VRy3|y2=0/y2=1/y2=2:S2;             [y3 - 3-й город для класса]
  !!VRy3|y2=3/y2=4/y2=5:S5;             [y3 - 3-й город для класса]
  !!VRy3|y2=6/y2=7/y2=8:S8;             [y3 - 3-й город для класса]

  !!if&y1=(BATTLE_LEFT);
    !!VRy4:Sv9982 -1 :2;           [y4 - уровень отстроенного здания в 1-м городе]
    !!VRy5:Sv9982 -1 %2;           [y5 - грейд отстроенного здания в 1-м городе]
  !!el;
    !!VRy4:Sv9985 -1 :2;           [...]
    !!VRy5:Sv9985 -1 %2;           [...]
  !!en;

  !!UN:Ty3/y4/y5/?y6;                   [y6 - тип существа]
  *!VRz12:S^card_c%Y6.pcx^;             [z12 - картинка 3-го существа]
  !!DL71:A86/(DLG_CMD_SET_DEF_FRAME)/y6/1;                  [установка картинок 3-го города]
  ** Информация о городах и строениях
  ** y3 - 3й город, x1 - герой -10/-20
  !!VRx2:Sx1; :-20;                      [x2 - сторона 0/1]
  !!VRy3|y2=0/y2=1/y2=2:S0;             [y3 - 1-й город для класса]
  !!VRy3|y2=3/y2=4/y2=5:S3;             [y3 - 1-й город для класса]
  !!VRy3|y2=6/y2=7/y2=8:S6;             [y3 - 1-й город для класса]
  !_!VRz12:S^card_b%Y3.pcx^;             [z12 - картинка города]
  !_!DL71:A132/11/z12/1;                 [установка картинки 3-го города]
  !!SN:W^b%X2.%Y3.0^/?y8 W^b%X2.%Y3.1^/?y9 W^b%X2.%Y3.2^/?y11; [y8/y9/y11 - 1/2/3 строения]
  !!VRy10:S0;                           [1 здание]
  !!VRz12&y8=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
  !!VRz12&y8=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
  
  !!DL71:A142/11/z12/1;                 [установка картинки 1го здания 1-го города]
  !!VRy10:S1;                           [2 здание]
  !!VRz12&y9=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
  !!VRz12&y9=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
  !!DL71:A152/11/z12/1;                 [установка картини 2го здания 1-го города]
  !!VRy10:S2;                           [2 здание]
  !!VRz12&y11=0:S^bn%Y3%Y10.pcx^;       [z12 - картинка города]
  !!VRz12&y11>0:S^b%Y3%Y10.pcx^;        [z12 - картинка города]
  !!DL71:A162/11/z12/1;                 [установка картини 3го здания 1-го города]
  !!VRy11:-1;                           [перезарядка Грааля]
  !!VRz11:S^^;                          [...]
  !!VRz11&y11>0:S^%Y11^;                [...]
  !!DL71:A167/3/z11/1;                  [установка времени перезарядки Грааля в диалог]
  **
  !!VRy3|y2=0/y2=1/y2=2:S1;             [y3 - 2-й город для класса]
  !!VRy3|y2=3/y2=4/y2=5:S4;             [y3 - 2-й город для класса]
  !!VRy3|y2=6/y2=7/y2=8:S7;             [y3 - 2-й город для класса]
  !_!VRz12:S^card_b%Y3.pcx^;             [z12 - картинка города]
  !_!DL71:A134/11/z12/1;                 [установка картинки 3-го города]
  !!SN:W^b%X2.%Y3.0^/?y8 W^b%X2.%Y3.1^/?y9 W^b%X2.%Y3.2^/?y11; [y8/y9/y11 - 1/2/3 строения]
  !!VRy10:S0;                           [1 здание]
  !!VRz12&y8=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
  !!VRz12&y8=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
  !!DL71:A144/11/z12/1;                 [установка картинки 1го здания 2-го города]
  !!VRy10:S1;                           [2 здание]
  !!VRz12&y9=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
  !!VRz12&y9=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
  !!DL71:A154/11/z12/1;                 [установка картини 2го здания 2-го города]
  !!VRy10:S2;                           [2 здание]
  !!VRz12&y11=0:S^bn%Y3%Y10.pcx^;       [z12 - картинка города]
  !!VRz12&y11>0:S^b%Y3%Y10.pcx^;        [z12 - картинка города]
  !!DL71:A164/11/z12/1;                 [установка картини 3го здания 2-го города]
  !!VRy11:-1;                           [перезарядка Грааля]
  !!VRz11:S^^;                          [...]
  !!VRz11&y11>0:S^%Y11^;                [...]
  !!DL71:A168/3/z11/1;                  [установка времени перезарядки Грааля в диалог]
  **

  !!VRy3|y2=0/y2=1/y2=2:S2;             [y3 - 3-й город для класса]
  !!VRy3|y2=3/y2=4/y2=5:S5;             [y3 - 3-й город для класса]
  !!VRy3|y2=6/y2=7/y2=8:S8;             [y3 - 3-й город для класса]
  !_!VRz12:S^card_b%Y3.pcx^;             [z12 - картинка города]
  !_!DL71:A136/11/z12/1;                 [установка картинки 1-го города]
  !!SN:W^b%X2.%Y3.0^/?y8 W^b%X2.%Y3.1^/?y9 W^b%X2.%Y3.2^/?y11; [y8/y9/y11 - 1/2/3 строения]
  !!VRy10:S0;                           [1 здание]
  !!VRz12&y8=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
  !!VRz12&y8=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
  !!DL71:A146/11/z12/1;                 [установка картинки 1го здания 3-го города]
  !!VRy10:S1;                           [2 здание]
  !!VRz12&y9=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
  !!VRz12&y9=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
  !!DL71:A156/11/z12/1;                 [установка картини 2го здания 3-го города]
  !!VRy10:S2;                           [2 здание]
  !!VRz12&y11=0:S^bn%Y3%Y10.pcx^;       [z12 - картинка города]
  !!VRz12&y11>0:S^b%Y3%Y10.pcx^;        [z12 - картинка города]
  !!DL71:A166/11/z12/1;                 [установка картини 3го здания 3-го города]
  !!VRy11:-1;                           [перезарядка Грааля]
  !!VRz11:S^^;                          [...]
  !!VRz11&y11>0:S^%Y11^;                [...]
  !!DL71:A169/3/z11/1;                  [установка времени перезарядки Грааля в диалог]
  ** Расположение диалога
  !!UN:C4199496/2/?i C4199503/2/?j;     [i,j - размер экрана]
  !!VRk:S-184 +i;                       [...]
  !!if&(side)=(BATTLE_LEFT);
    !!VRi:-806 :2 +2;              [i,j - смещения диалога]
    !!VRi&i<0:S0;                  [...]
  !!el;
    !!VRi:-806 :2 +806 -185 -2;    [...]
    !!VRi&i>k:Sk;                  [...]
  !!en;


  !!VRj:-630 :2 +3;                     [...]
  !!VRj&j<0:S0;                         [...]
    *!IF:M^%i(battle_hero_%(side))^;

  !!FU88000:P71/i/j;                    [изменение координат диалога]
  !!FU9030:P71;                         [отображение диалога]

!?FU9028;                             [x1 - герой, x16 - втор.навык]
  !#VA(side:x);
  !!FU|x16=0/x16=20/x16=3/x16=27:E;     [выход, если навык не используется]
  !!HEi^battle_hero_%(side)^:Sx16/?(skillLvl:y);                      [y1  - уровень навыка]

  !!VR(skillLvl)&x1=0/x16=(SKILL_NECROMANCY):Sv9970;          [для Некромантии актуальный уровень навыка в v-переменной]
  !!VR(skillLvl)&x1=1/x16=(SKILL_NECROMANCY):Sv9971;          [...]
  !!VR(frameId:y):Sx16 *4 +(skillLvl);
  *!VRz11:S^hss%X16%Y1.pcx^;            [имя картинки]
  *!VRy2:Sx1 :-10 -1;                   [y2 - сторона]
  *!IF:M^%(frameId)^;
  !!if&x16=(SKILL_NAVIGATION)/i^spec_%(side)^;

    !!if&i^spec_%(side)^=3;
      !!DL71:Ax16/(DLG_CMD_SET_DEF)/^crd_hrs.def^/0;                 [установка картинки]
      !!VR(frameId):Si^battle_hero_%(side)^;

    !!el&i^spec_%(side)^=5;
      !!DL71:Ax16/(DLG_CMD_SET_DEF)/^crd_crt.def^/0;                 [установка картинки]
      !!BMi^bh_commander_stack_%(side)^:T?(frameId);

    !!el;
      !!DL71:Ax16/(DLG_CMD_SET_DEF)/^crd_spec.def^/0;                 [установка картинки]
      !!VR(frameId):Si^spec_%(side)^;
    !!en;

    *!VRz11&x16=5/j=1:S^card_hm.pcx^;     [Иконка специализации]
    *!VRz11&x16=5/j=2:S^card_ps34.pcx^;   [Иконка специализации]
    *!HEx1&x16=5/j=3:N?i;                 [i - номер героя]
    *!VRz11&x16=5/j=3:S^card_h%Vi.pcx^;   [Иконка специализации]
    *!VRz11&x16=5/j=4:S^card_bs.pcx^;     [Иконка специализации]
    *!HEi^battle_hero_%(side)^&x16=5/j=5:B2/?i;               [i - номер существа командира]
    *!VRi&x16=5/j=5::6;                   [...]
    *!VRi&i=1/x16=5/j=5:S4;               [...]
    *!VRi&i=2/x16=5/j=5:S8;               [...]
    *!VRi&x16=5/j=5:+174;                 [...]
    *!VRz11&x16=5/j=5:S^card_c%Vi.pcx^;   [Иконка специализации]
    *!VRz11&x16=5/j=6:S^card_ga.pcx^;     [Иконка специализации]
  !!en;

  !!DL71:Ax16/(DLG_CMD_SET_DEF_FRAME)/(frameId)/1;                 [установка картинки]
  !!if&(skillLvl);
    !!VR(offset:y):S(skillLvl) -1 *(UNC_INT);
    !!UN:C6976984/(offset)/(UNC_INT)/?(basLvlTextPtr:y);
    !!SN:B(basLvlTextPtr)/d/?(str:z) K(str)/0/?(char:z);
    !!VRy2:Sx16 +50;                      [y2 - номер текста]
    !!DL71:Ay2/(DLG_CMD_SET_TEXT)/(char)/1;                   [установка обозначения уровня навыка]

  !!en;

  *!VRz12&(skillLvl)=0:S^^;                     [z2 - обозначение уровня навыка]
  *!VRz12&(skillLvl)=1:Sz179908;                [...]
  *!VRz12&(skillLvl)=2:Sz179909;                [...]
  *!VRz12&(skillLvl)=3:Sz179910;                [...]
  *!DL71:Ay2/3/z12/1;                   [установка обозначения уровня навыка]

*** отображение диалога выбора карты

!?FU(BH_BattleDlg_Prepare);                             [v101-v124 - параметры 1-6 карты, z1-z6 - описания действий 1-6 карты, v125 - количество карт, 126 - номер героя, 127 - тип командира. Возврат выбранной карты в v1.]
!!DL(BH_CHOSING_DLG_ID):N^cards_6.txt^;            [подгрузка 72 диалога (выбор карты)]
!!if&-1;                              [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                             [и выходим]
!!en;

!!VRy1:Sv997 +1;                      [y1 - номер раунда]
*!VRz10:Sz179911;                     [z10 - текст "номер раунда"]
!!DL(BH_CHOSING_DLG_ID):A100/3/^%z179911^/1;                  [установка номера раунда]
*!HE-10:N?y2;                         [y2 - номер атакующего героя]
*!VRz9:S^card_h%Y2.pcx^;              [z9 - иконка героя]
!!HEv126:O?y1;                        [y1 - номер игрока]
*!VRz10:S^flag_%Y1.pcx^;              [z10 - иконка флага]
!!BMi^bh_commander_stack_0^:T?y3;                       [y3 - тип атакующего командира]
*!VRz11:S^card_c%Y3.pcx^;             [z11 - иконка командира]
!!DL(BH_CHOSING_DLG_ID):A70/(DLG_CMD_SET_DEF_FRAME)/i^battle_hero_0^/1 A71/(DLG_CMD_SET_DEF_FRAME)/y1/1 A72/(DLG_CMD_SET_DEF_FRAME)/y3/1; [установка иконок героя, флага и командира в диалог]
*!HE-20:N?y2;                         [y2 - номер защищающегося героя]
*!VRz9:S^card_h%Y2.pcx^;              [z9 - иконка героя]
!!BMi^bh_commander_stack_1^:T?y3;                       [y3 - тип защищающегося командира]
*!VRz11:S^card_c%Y3.pcx^;             [z11 - иконка командира]
!!DL(BH_CHOSING_DLG_ID):A80/(DLG_CMD_SET_DEF_FRAME)/i^battle_hero_1^/1 A82/(DLG_CMD_SET_DEF_FRAME)/y3/1; [установка иконок героя, флага и командира в диалог]
**установка информации о мане и здоровье в диалог

!!HEv9998:I?y11/1;                    [...]
*!VRz11:S^{~a}%Y11{~}^;               [...]
!!BMi^bh_commander_stack_0^:H?y12 L?y13;                [...]
!!VRy12:-y13;                         [...]
*!VRz12:S^{~r}%Y12{~}^;               [...]
!!BMi^bh_commander_stack_1^:H?y13 L?y14;                [...]
!!VRy13:-y14;                         [...]
*!VRz13:S^{~r}%Y13{~}^;               [...]
!!HEv9999:I?y14/1;                    [...]
!!DL72:A87/3/^{~a}%Y11{~}^/1 A88/3/^{~r}%Y12{~}^/1 A89/3/^{~r}%Y13{~}^/1 A90/3/^{~a}%Y14{~}^/1; [установка информации о мане и здоровье]
*!VRz14:S^{~a}%Y14{~}^;               [...]


**
!!DO9033/1/v125/1:P;                  ["отрисовка" карт]
!!VRv1:C0/0;                          [инициализация v1/v2]
!!UN:C4199496/2/?i C4199503/2/?j;     [i,j - размер экрана]
!!VRk:S-600 +j;                       [...]
!!VRj:-630 :2 +26;                    [...]
!!VRj&j>k:Sk;                         [...]
!!VRj&j<0:S0;                         [...]
*!FU(DL_Coords):P(BH_CHOSING_DLG_ID)/-1/j;
!!FU88000:P72/-1/j;                   [изменение координат диалога]
!!DL(BH_CHOSING_DLG_ID):S;                             [отображение диалога 72]

!?FU9033;                             ["отрисовка" карты x16]
!#VA(usedY[35]:y);
  !!VRy1:Sx16 -1 *4 +101 Svy1;          [y1-y4 - параметры карты]
  !!VRy2:Sx16 -1 *4 +102 Svy2;          [...]
  !!VRy3:Sx16 -1 *4 +103 Svy3;          [...]
  !!VRy4:Sx16 -1 *4 +104 Svy4;          [...]
  !!VRy5:Sx16 +2;                       [y5 - индекс подложки карты]
  *!VRz9:S^crd_bg_0.pcx^;               [z9 - подложка карты]
  *!DL72:Ay5/11/z9/1;                   [установка подложки карты]
  !!VRy5:Sx16 +90;                      [y5 - индекс бордюра карты]
  *!IF:M^%v126^;
  !!HEv126:O?y6;                        [y6 - цвет героя]
  *!VRz9:S^crd_brd_%Y6.pcx^;            [z9 - бордюр активной карты]
  *!IF:M^%y5^;
  !!DL72:Ay5/(DLG_CMD_SET_DEF_FRAME)/y6/1;                   [установка бордюра карты]
  !!VRz12:S^^;                          [обнуление строк описания карты]
  !!VRz13:S^^;                          [...]
  !!VRz14:S^^;                          [...]
  !!VRz15:S^^;                          [...]
  !!VRh:Sx16 +20;                       [h - индекс подсказки]
  !!VRzh:Szx16;                         [подсказка по умолчанию - описание действия]
  *** призыв и призывные действия гильдий
  !!if&y1=1;                            [...]
    !!VRz9:S^crd_gld.def^;           [Иконка специализации]
    !!VRz10:S^crd_crt.def^;           [...]
    !!VR(leftFrameId:y):S0; [z9/z10,z12-z17 - иконка типа/подтипа, тексты действия]
    !!VR(rightFrameId:y):Sy2;

    !!UN&y3=1:N3/11/y2/0;             [...]
    !!UN&y3>1:N3/11/y2/1;             [...]
    !!VRy11:Sy3;                        [...]
    !!VRz12:Sz179801;                    [...]
    !!VRz78:M2/z11/0;                   [...]
    !!VRz79:M2/z11/1;                   [...]
    !!VRz80:M2/z11/2;                   [...]
    !!VRz13:S^{~Magenta}%Y11 %Z78{~}^;  [...]
    !!VRz14:S^{~Magenta}%Z79 %Z80{~}^;  [...]
    !!VRzh:Sz179871;                     [подсказка]
    !!if|y2=140/y2=142/y2=143/y2=144/y2=168/y2=194/y2=195;  [Для Гильдии Головорезов]
      !!VR(leftFrameId):S1;            [...]
      !!VRz12:Sz179809;                  [...]
      !!VRy11:Sy4;                      [...]
      !!VRz15:Sz179847;                  [...]
      !!VRzh:Sz179872;                   [подсказка]
    !!en; 
    !!if|y2=137/y2=138/y2=170/y2=171/y2=192;  [Для Гильдии Стрелков]
      !!VR(leftFrameId):S2;            [...]
      !!VRz12:Sz179894;                  [...]
      !!VRy11:Sy4;                      [...]
      !!VRz15:Sz179847;                  [...]
      !!VRzh:Sz179872;                   [подсказка]
    !!en; 
    !!if|y2=136/y2=193/y2=169;          [Для Гильдии Чародеев]
      !!VR(leftFrameId):S3;            [...]
      !!VRz12:Sz179810;                  [...]
      !!VRz13:Sz179889;                  [...]
      !!VRz14:Sz179890;                  [...]
      !!VRy11:Sy4;                      [...]
      !!VRz15:Sz179847;                  [...]
      !!VRzh&y2=169:Sz179891;            [подсказка Фанатики Войны]
      !!VRzh&y2=193:Sz179892;            [подсказка Волшебницы]
      !!VRzh&y2=136:Sz179893;            [подсказка Чародеи]
    !!en; 
    !!if|y2=145/y2=146/y2=147/y2=148/y2=116/y2=117;  [Для Гильдии Кузнецов]
      !!VR(leftFrameId):S4;            [...]
      !!VRz12:Sz179808;                  [...]
      !!VRy11:Sy4;                      [...]
      !!VRz15:Sz179847;                  [...]
      !!VRzh|y2=116/y2=117:Sz179873;     [подсказка големов]
      !!VRzh|y2=145:Sz179882;            [подсказка Катапульты]
      !!VRzh|y2=146:Sz179879;            [подсказка Баллисты]
      !!VRzh|y2=147:Sz179880;            [подсказка Палатки]
      !!VRzh|y2=148:Sz179881;            [подсказка Тележки]
    !!en; 
  !!en; 
  *** изучение заклинаний
  !!if&y1=2; 
    !!VRz9:S^crd_hrs.def^;          [...]
    !!VRz10:S^crd_sp.def^;          [...]

    !!VR(leftFrameId:y):Sv126;
    !!VR(rightFrameId:y):Sy2;
    *!IF:M^%v126 %y2^;
    !!UN:N1/11/y2;                    [...]
    !!VRz12:Sz179802;                    [...]
    !!VRz13:Sz179836;                    [...]
    !!SSy2:S?y20;                       [y20 - биты школ магии]
    !!HEv126:S15/?y31 S14/?y32 S16/?y33 S17/?y34; [y31-y34 - уровни навыков школ героя]
    !!VRy21:Sy20 &1;                    [y30 - уровень максимаьно-развитой школы магии, которой принадлежит заклинание]
    !!VRy22:Sy20 &2;                    [...]
    !!VRy23:Sy20 &4;                    [...]
    !!VRy24:Sy20 &8;                    [...]
    !!VRy30:S0;                         [...]
    !!VRy30&y21>0/y30<y31:Sy31;         [...]
    !!VRy30&y22>0/y30<y32:Sy32;         [...]
    !!VRy30&y23>0/y30<y33:Sy33;         [...]
    !!VRy30&y24>0/y30<y34:Sy34;         [...]
    !!VRy30:*4;                         [х4]
    !!VRi:Sy2 *136 +8114880 +120 +y30;  [получение подсказки по заклинанию - описание текущего уровня Школы]
    !!UN:Ci/4/?j;                       [спасибо Berserker'у]
    !!SN:X?k Xj X?z77 Xk;               [...]
    *!SN:H^spell^/y2/0/?z11 H^spell^/y2/y30/?z77;
    !!VRzh:S^{%Z11}

  %Z77^;                                [Формирование подсказки по заклинанию]
  !!en; 
  *** тренировка героя
  !!if&y1=3; 
    !!VRz9:S^crd_hrs.def^;          [...]
    !!VRz10:S^crd_hps.def^;          [...]
    !!VR(leftFrameId:y):Sv126;
    !!VR(rightFrameId:y):Sy2 -31;

    !!VRz12:Sz179803;                    [...]
    !!VRy11:Sy3;                        [...]
    !!VRz13&y2=31:Sz179814;              [...]
    !!VRzh&y2=31:Sz179867;               [подсказка]
    !!VRz13&y2=32:Sz179815;              [...]
    !!VRzh&y2=32:Sz179868;               [подсказка]
    !!VRz13&y2=33:Sz179816;              [...]
    !!VRzh&y2=33:Sz179869;               [подсказка]
    !!VRz13&y2=34:Sz179817;              [...]
    !!VRzh&y2=34:Sz179870;               [подсказка]
  !!en; 
  *** обучение героя
  !!if&y1=4; 
    !!VRz9:S^crd_hrs.def^;          [...]
    !!VRz10:S^crd_ss.def^;       [...]
    !!VR(leftFrameId:y):Sv126;
    !!VR(rightFrameId:y):Sy2 *4 +y3;

    !!UN:N4/11/y2;                    [...]
    !!VRz12:Sz179804;                    [...]
    !!VRz13:S^{~Lime}%Z11{~}^;
    !!VRz14&y3=1:Sz179818;               [...]
    !!VRz14&y3=2:Sz179819;               [...]
    !!VRz14&y3=3:Sz179820;               [...]
    !!VRi:Sy2 *2 +901;                  [i - индекс z-переменной с описание втор.навыка]
    !!VRz77:Szi;                        [z77 - описание втор.навыка]
    !!VRzh:S^{%Z11}

  %Z77^;                                [подсказка для изучения втор.навыка - описание втор.навыка]
  !!en; 
  *** Постройка жилищ
  !!if&y1=5; 
    !!if&y2=9;                          [Спецстроения]
      !!VRy5:Sy3 :3;                    [y5 - город]
      !!VRy6:Sy3 %3;                    [y6 - строение]

      !!VRz9:S^crd_twn.def^;          [1я иконка карты]
      !!VRz10:S^crd_bldn.def^;           [2я иконка карты]
      !!VR(leftFrameId:y):Sy5;
      !!VR(rightFrameId:y):Sy5 *3 +y6;

      !!VRy7&y6<2:S179226 +y5 +y5 +y6;  [y7 - индекс ert-переменной с названием строения]
      !!VRy7&y6=2:S179298 +y5;          [y7 - индекс ert-переменной с названием строения]
      !!VRz11:Szy7;                     [z11 - название спецстроения]
      !!VRz12:Sz179244;                  [Тип карты "Постройка Здания"]
      !!VRz13:S^{~w}%Z11{~}^;           [1я строка карты]
      !!VRz14:S^^;                      [2я строка карты]
      !!VRy7&y6<2:S179245 +y5 +y5 +y6;  [y7 - индекс ert-переменной с описанием строения]
      !!VRy7&y6=2:S179307 +y5;          [y7 - индекс ert-переменной с описанием строения]
      !!VRzh:Szy7;                      [Подсказка карты]
      !!VRy2:Sy5;
    !!el; 

      !!VRy5:Sy3 -30 %7;                [y7 - тип существа в отстраиваемом жилище]
      !!VRy6&y3<37:S0;                  [...]
      !!VRy6&y3>36:S1;                  [...]
      !!UN:Ty2/y5/y6/?y7;               [...]

      !!VRz9:S^crd_twn.def^;          [...]
      !!VRz10:S^crd_crt.def^;         [...]
      !!VR(leftFrameId:y):Sy2;
      !!VR(rightFrameId:y):Sy7;

      !!UN:N2/11/y2/y3;               [...]
      !!VRz12:Sz179805;                  [...]
      !!VRz78:M2/z11/0;                 [...]
      !!VRz79:M2/z11/1;                 [...]
      !!VRz80:M2/z11/2;                 [...]
      !!VRz81:M2/z11/3;                 [...]
      !!VRz13:S^{~w}%Z78 %Z79{~}^;      [...]
      !!VRz14:S^{~w}%Z80 %Z81{~}^;      [...]
      !!UN:N2/31/y2/y3 N3/32/y7/1;  [формирование подсказки]
      !!VRzh:Sz179866;                  [...]
    !!en; 
  !!en; 
  *** Тренировка/обучение командира
  !!if&y1=6; 
    !!VRz9:S^crd_crt.def^;          [...]
    !!VR(leftFrameId:y):Sv127;
    !!VR(rightFrameId:y):Sy2;


    !!VRz10&y2<7:S^crd_cps.def^;     [...]
    !!VRz12&y2<7:Sz179806;               [...]
    !!VRy11:Sy3;                        [...]
    !!VRz13&y2=0:Sz179814;               [...]
    !!VRzh&y2=0:Sz179859;                [...]
    !!VRz13&y2=1:Sz179815;               [...]
    !!VRzh&y2=1:Sz179860;                [...]
    !!VRz13&y2=2:Sz179821;               [...]
    !!VRzh&y2=2:Sz179861;                [...]
    !!VRz13&y2=3:Sz179823;               [...]
    !!VRzh&y2=3:Sz179863;                [...]
    !!VRz13&y2=4:Sz179824;               [...]
    !!VRzh&y2=4:Sz179864;                [...]
    !!VRz13&y2=5:Sz179822;               [...]
    !!VRzh&y2=5:Sz179862;                [...]
    !!VRz13&y2=6:Sz179825;               [...]
    !!VRzh&y2=6:Sz179865;                [...]
    !!if&y2=7; 
      !!VRz10:S^crd_css.def^;        [...]
      !!VR(rightFrameId:y):Sy3;
      !!VRz12:Sz179807;                  [...]
      !!VRz13&y3=0:Sz179826;             [...]
      !!VRzh&y3=0:Sz179849;              [...]
      !!VRz13&y3=2:Sz179827;             [...]
      !!VRzh&y3=2:Sz179850;              [...]
      !!VRz13&y3=3:Sz179828;             [...]
      !!VRzh&y3=3:Sz179851;              [...]
      !!VRz13&y3=4:Sz179829;             [...]
      !!VRzh&y3=4:Sz179852;              [...]
      !!VRz13&y3=5:Sz179830;             [...]
      !!VRzh&y3=5:Sz179853;              [...]
      !!VRz13&y3=6:Sz179831;             [...]
      !!VRzh&y3=6:Sz179854;              [...]
      !!VRz13&y3=7:Sz179832;             [...]
      !!VRzh&y3=7:Sz179855;              [...]
      !!VRz13&y3=8:Sz179833;             [...]
      !!VRzh&y3=8:Sz179856;              [...]
      !!VRz13&y3=9:Sz179834;             [...]
      !!VRzh&y3=9:Sz179857;              [...]
      !!VRz13&y3=11:Sz179835;            [...]
      !!VRzh&y3=11:Sz179858;             [...]
    !!en; 
  !!en; 
  *** Помощь Гильдии Некромантов
  !!if&y1=7; 
    !!VRz9:S^crd_glne.def^;              [...]
    !!VRz10:S^crd_glne.def^;
    !!VR(leftFrameId:y):S0;
    !!VR(rightFrameId:y):Sy2;

    *!VRz10&y2=1:S^card_c196.pcx^;      [...]
    *!VRz10&y2=2:S^card_c59.pcx^;       [...]
    *!VRz10&y2=3:S^card_c159.pcx^;      [...]
    *!VRz10&y2=4:S^card_c141.pcx^;      [...]
    *!VRz10&y2=5:S^card_c172.pcx^;      [...]

    !!VRz12:Sz179811;                    [...]
    !!VRy11:Sy3;                        [...]
    !!VRz13&y2=1:Sz179839;               [...]
    !!VRz14&y2=1:Sz179840;               [...]
    !!VRzh&y2=1:Sz179875;                [подсказка]
    !!VRz13&y2=2:Sz179841;               [...]
    !!VRz14&y2=2:Sz179842;               [...]
    !!VRzh&y2=2:Sz179876;                [подсказка]
    !!VRz13&y2=3:Sz179843;               [...]
    !!VRz14&y2=3:Sz179844;               [...]
    !!VRzh&y2=3:Sz179877;                [подсказка]
    !!VRz13&y2=4:Sz179845;               [...]
    !!VRz14&y2=4:Sz179846;               [...]
    !!VRzh&y2=4:Sz179878;                [подсказка]
    !!VRz13&y2=5:Sz179883;               [...]
    !!VRz14&y2=5:Sz179840;               [...]
    !!VRzh&y2=5:Sz179884;                [подсказка]
    !!VRy11:Sy4;                        [...]
    !!VRz15:Sz179847;                    [...]
  !!en; 
  *** Помощь Гильдии Чародеев
  !!if&y1=8; 
    !!VRz9:S^crd_glen.def^;              [...]
    !!VRz10:S^crd_glen.def^;              [...]
    !!VR(leftFrameId:y):S0;
    !!VR(rightFrameId:y):Sy2;

    !!if&y2=1;                          [Карты Судьбы]
      *!VRz10:S^card_fc.pcx^;           [...]
      !!VRz13:Sz179898;                  [...]
      !!VRz14:Sz179899;                  [...]
      !!VRzh:Sz179900;                   [подсказка]
    !!en; 
    !!if&y2=2;                          [Перекачка маны]
      *!VRz10:S^card_md.pcx^;           [...]
      !!VRz13:Sz179895;                  [...]
      !!VRz14:Sz179896;                  [...]
      !!VRzh:Sz179897;                   [подсказка]
    !!en; 
    !!if&y2=3;                          [СД]
      *!VRz10:S^card_c134.pcx^;         [...]
      !!VRz13:Sz179886;                  [...]
      !!VRz14:Sz179887;                  [...]
      !!VRzh:Sz179888;                   [подсказка]
    !!en; 
    !!VRz12:Sz179810;                    [Название Гильдии]
    !!VRy11:Sy4;                        [...]
    !!VRz15:Sz179847;                    [Стоимость карты]
  !!en; 
  *** Выбор специализации
  !!if&y1=9; 
    *!VRz9:S^card_ss5%Y2.pcx^;          [...]
    !!VRz9:S^crd_ss.def^;
    !!VR(leftFrameId):S(SKILL_NAVIGATION) *4 +y2;
    !!if&y3=1;                          [Набор рекрутов]
      !!VRz10:S^crd_spec.def^;           [Иконка специализации]
      !!VR(rightFrameId):S1;
      !!VRz13:Sz179788;                  [Название специализации]
      !!VRzh:Sz179789;                   [подсказка]
    !!en; 
    !!if&y3=2;                          [Изучение заклинаний]
      !!VRz10:S^crd_ps.def^;         [Иконка специализации]
      !!VR(rightFrameId):S3;
      !!VRz13:Sz179790;                  [Название специализации]
      !!VRzh:Sz179791;                   [подсказка]
    !!en; 
    !!if&y3=3;                          [Обучение героя]
      !!VRz10:S^crd_hrs.def^;       [Иконка специализации]
      !!VR(rightFrameId):Sv126;
      !!VRz13:Sz179792;                  [Название специализации]
      !!VRzh:Sz179793;                   [подсказка]
    !!en; 
    !!if&y3=4;                          [Постройка жилищ]
      !!VRz10:S^crd_spec.def^;           [Иконка специализации]
      !!VR(rightFrameId):S4;
      !!VRz13:Sz179794;                  [Название специализации]
      !!VRzh:Sz179795;                   [подсказка]
    !!en; 
    !!if&y3=5;                          [Обучение командира]
      !!VRz10:S^crd_crt.def^;       [Иконка специализации]
      !!VR(rightFrameId):Sv127;
      !!VRz13:Sz179796;                  [Название специализации]
      !!VRzh:Sz179797;                   [подсказка]
    !!en; 
    !!if&y3=6;                          [Услуги гильдий]
      !!VRz10:S^crd_spec.def^;           [Иконка специализации]
      !!VR(rightFrameId):S6;

      !!VRz13:Sz179798;                  [Название специализации]
      !!VRzh:Sz179799;                   [подсказка]
    !!en; 
    !!VRz12:Sz179787;                    ["Специализация"]
  !!en; 
  *** Магия командиров
  !!if&y1=10; 
    !!VRz9:S^crd_crt.def^;          [...]
    !!VRz10:S^crd_sp.def^;          [...]
    !!VR(leftFrameId):Sv127;
    !!VR(rightFrameId:y):Sy2;

    !!UN:N1/11/y2;                    [...]
    !!VRz12:Sz179807;                    ["Обучение командира"]
    !!VRy11:Sy3 *10;                    [y11 - шанс произнести заклинание]
    !!VRz13:Sz179783;                    [текст обучения]
    !!VRz14:Sz179784;                    [текст обучения (2 строка)]
    !!VRzh:Sz179785;                     [...]
  !!en; 
  !!VRy5:Sx16 *10 +1;                   [y5 - индекс иконки 1 в диалоге]
  !!VRy6:Sx16 *10 +2;                   [y6 - индекс иконки 2 в диалоге]
  !!DL72:Ay5/(DLG_CMD_SET_DEF)/z9/1 Ay6/(DLG_CMD_SET_DEF)/z10/1;      [установка иконок типа/подтипа действия в диалог]
  !!DL72:Ay5/(DLG_CMD_SET_DEF_FRAME)/(leftFrameId)/1 Ay6/(DLG_CMD_SET_DEF_FRAME)/(rightFrameId)/1;      [установка иконок типа/подтипа действия в диалог]

  !!VRy5:Sx16 *10 +3;                   [y5 - индекс 1й строки описания карты в диалоге]
  !!VRy6:Sx16 *10 +4;                   [y6 - индекс 2й строки описания карты в диалоге]
  !!VRy7:Sx16 *10 +5;                   [y7 - индекс 3й строки описания карты в диалоге]
  !!VRy8:Sx16 *10 +6;                   [y8 - индекс 4й строки описания карты в диалоге]
  *!VRy9:Sx16 *10 +7;                   [y9 - индекс 5й строки описания карты в диалоге]
  *!VRy10:Sx16 *10 +8;                  [y10 - индекс 6й строки описания карты в диалоге]
  !!DL(BH_CHOSING_DLG_ID):Ay5/3/z12/1 Ay6/3/z13/1 Ay7/3/z14/1 Ay8/3/z15/1; [установка текстов в диалог]

*?DL&v998=(BH_CHOSING_DLG_ID);                         [обработка диалога 72]

!?FU(OnCustomDialogEvent)&x1=(BH_CHOSING_DLG_ID)/i^mouse_action^<>(MOUSE_LMB_RELEASED);
!!VRv1:S0;                            [клик не на карте]


!!VR(chosenCard:y):Si^mouse_item^ :10;

!!if&i^mouse_item^>=11/i^mouse_item^<=68;
  !!if&i^mouse_action^=(MOUSE_LMB_PRESSED);[выбор доступной карты]
    !!VRv1:S(chosenCard);                            [клик не на карте]

    !!DLi^dlg_id^:C(TRUE);                          [...]
    !!FU:E;                             [...]
  !!el;
    !!VRh:S(chosenCard) +20;                      [h - индекс подсказки]
    !!IF:M0/4/h;                        [вывод подсказки]
  !!en;
 
!!el|i^mouse_item^=70/i^mouse_item^=80;
  !!VR(side:y):Si^mouse_item^ -70 F0/1;
  *!IF:M^%(side)^;
  !!FU(BH_Dlg_HeroStats_Build):P(side);

!!el|i^mouse_item^=72/i^mouse_item^=82;
  !!VR(commStack:y):Si^mouse_item^-72 F0/1;

  !!FU(BH_Dlg_CommanderStats_Build):Pi^bh_commander_stack_%(commStack)^;
!!en;


*** Быстрая подсказка в окне боя

!?FU77003;                            [нажатие клавиши]
!!SN:X?y1;                            [y1 — код клавиши]
!!if&y1=112;                          [F1 - вывод информации об используемой ИИ-схеме]
  !!SN:X?y2/1;                        [отключение реакции игры на нажатие]
  !!SN:W^BH.AI.Sheme^/?z11;           [z11 - файл ИИ-схемы]
  !!IF:L^AI: %Z11^;                   [Вывод сообщения в лог экрана]
  **BA:H1/?y2;                        [y2 - номер ИИ героя-защитника]
  **FU9052:Py2/-1/1;                  [логирование состояния ИИ]
  **BU:V1; [debug]
!!en; 
!!FU&y1<>81:E;                        [выход, если нажата не "Q"]
!!SN:L^Era.dll^/?y2;                  [received description of Era library in y2]
!!SN:Ay2/^GetGameState^/?y3;          [y3 — address of function GetGameState]
!!SN:Ey3/1/?y4;                       [y4 — ID of parent dialog, y5 — ID of current dialog]
!!FU&y5<>4662240:E;                   [выход, если нажатие не в окне боя]
** отключение быстрой подсказки во время действия, либо во время хода оппонента (c)Berserker
!!BG:A?y1;                            [y1 - текущее действие]
!!FU&y1<>0:E;                         [выход, если выполняется какое-либо действие]
!!BG:Q?y1;                            [y1 - активная сторона]
!!BHy1:N?y2;                          [y2 - номер текущего героя]
!!BA:O?y3/?y4;                        [y3-y4 - хозяева героев]
!!VRy5:S3+y1 Syy5;                    [y5 - хозяин активной стороны]
!!OW:Iy5/?y6 Gy5/?y7;                 [y6 - человек ли сейчас ходит, y7 - активен локальный игрок (1) или удалённый(0)]
!!FU|y6<>0/y7<>1:E;                   [выход, если ходит ИИ или игрок за удаленным ПК]
**
!!SN:X?y1/1;                          [отключение реакции игры на нажатие]
!!BMi^bh_commander_stack_0^:H?y11 L?y12;                [y11/y12 - Максимальное и потерянное здоровье командира левого]
!!VRy12:-y11 *-1;                     [y12 - текущее здоровье командира левого]
!!HEv9998:I?y13/1 Fd/d/d/?y31;        [параметры левого героя]
!!VRy31:*10 +10;                      [y31 - макс. мана героя.]
!!FU9047:Pv9998/?y14;                 [y14 - регенерация маны левого героя]
!!BMi^bh_commander_stack_1^:H?y21 L?y22;                [y21/y22 - Максимальное и потерянное здоровье командира правого]
!!VRy22:-y21 *-1;                     [y22 - текущее здоровье командира левого]
!!HEv9999:I?y23/1 Fd/d/d/?y32;        [параметры правого героя]
!!VRy32:*10 +10;                      [y31 - макс. мана героя.]
!!FU9047:Pv9999/?y24;                 [y24 - регенерация маны правого героя]
!!VRi:Sv997 +1;                       [i - номер раунда]
!!VRy16:Sv9964;                       [y16=1, если есть способность Повышенная регенерация у командира левого]
!!VRy26:Sv9965;                       [y26=1, если есть способность Повышенная регенерация у командира правого]
!!VRy16&y16=1:Sy11 :50;               [y16 - регенерация командира левого]
!!VRy16&y16=0:Sy11 :100;              [...]
!!VRy26&y26=1:Sy21 :50;               [y26 - регенерация командира правого]
!!VRy26&y26=0:Sy21 :100;              [...]
!!VRz11:Sz179838;                     [формирование быстрой подсказки]
!!IF:Lz11;                            [вывод подсказки в лог экрана]

!?FU88000;                            [Изменение координат отображения WoG диалогов DL
** thanks igrik, gamecreator, sergroj
  !#VA(dlgId:x) (x:x) (y:x);
  ; x1 - WoG dialog id
  ; x2 - coord X (-1 center)
  ; x3 - coord Y (-1 center)

!!VRy1:Sv1;
!!SN:E7510739/1/x1;
!!VRy2:Sv1;
!!if&y2<>0; 
  !!UN:Cy2/4/?y5;
  !!if&x2<0; 
    !!VRy6:Sy5 +32;
    !!UN:Cy6/4/?y7;
    !!UN:C4199496/2/?y8;
    !!VRy10:Sy8 :2;
    !!VRy11:Sy7 :2;
    !!VRx2:Sy10 -y11;
  !!en; 
  !!if&x3<0; 
    !!VRy6:Sy5 +36;
    !!UN:Cy6/4/?y7;
    !!UN:C4199503/2/?y8;
    !!VRy10:Sy8 :2;
    !!VRy11:Sy7 :2;
    !!VRx3:Sy10 -y11;
  !!en; 
  !!VRy3:Sy5 +24;
  !!UN:Cy3/4/x2;
  !!VRy4:Sy5 +28;
  !!UN:Cy4/4/x3;
!!en; 
!!VRv1:Sy1;

** Модификация диалога повышения уровня. Теперь из него можно открыть окно героя для управления войсками/артефактами и пр.
** (с)пасибо igrik
!?PI;
!!SN:L^EraPlugins\erm_hooker.era^/?y1;
!!SN&y1<>0:Ay1/^SetHook^/?y2 Ey2/1/5218510/9065;

!?GM0;
!!SN:L^EraPlugins\erm_hooker.era^/?y1;
!!SN&y1<>0:Ay1/^SetHook^/?y2 Ey2/1/5218510/9065;

!?FU9065;
!!SN:X?y91;
!!VRy92:Sy91 +8;   !!UN:Cy92/4/?y93;
!!VRy94:Sy93 +8;   !!UN:Cy94/4/?y95;
!!VRy96:Sy95 +8;   !!UN:Cy96/4/?y97;  [= CM:I?y97]
!!VRy98:Sy95 +12;  !!UN:Cy98/4/?y99;  [= CM:F?y99]
!!FU|y97<>2001/y99=512:E; выход, если не портрет героя или ПКМ
!!SN:W^Dlg_LvlUpHeroID^/?y90 E5118576/3/y90/1/0/0;

!?HL-1;
!!OW:A-1/?y1;
!!HE-1&y1=-1:N?y1;
!!SN:W^Dlg_LvlUpHeroID^/y1;   запомнить номер героя

** Artefact helper
!?CM0;                          [клик на карте]
!!CM:F?y1 I?y2;                 [y1/y2 - параметры/место клика]
!!FU|y1<>512/y2<>37:E;          [выход, если не ПКМ на карте приключений]
!!CM:P?y1/?y2/?y3 ;             [y1-y3 - координаты клика]
!!OW:C?y4;                      [y4 - текущий игрок]
!!VRy5&y4=0:S1;  !!VRy5&y4=1:S2;  !!VRy5&y4=2:S4;  !!VRy5&y4=3:S8;   [y5 - бит.маска текущего игрока]
!!VRy5&y4=4:S16; !!VRy5&y4=5:S32; !!VRy5&y4=6:S64; !!VRy5&y4=7:S128; [...]
!!TRy1/y2/y3:V?y6;              [y6 - бит.маска видимости квадрата]
!!VRy6:&y5;                     [y6=0, если квадрат не видим для игрока]
!!FU&y6=0:E;                    [выход, елси квадрат невидим]
!!OBy1/y2/y3:T?y4 U?y5;         [y4/y5 - тип/подтип объекта]
!!FU&y4<>5:E;                   [выход, если клик не на артефакте]
!!UN:C6687592/4/?y12;           [y12 - ссылка на таблицу артефактов [спс => igrik]]
!!VRy13:Sy5 *32 +y12 +16;       [y13 - адрес указателя с описанием артефакта]
!!UN:Cy13/4/?y14;               [y14 - получить указатель на описание артефакта]
!!SN:X?y9 Xy14 X?z1 Xy9;        [z1 - хранит описание артефакта]
!!IF:Q1/8/y5/4^%Z1^;            [вывод окна с картинкой и названием артефакта]
!!CM:R0;                        [отмена стандартной реакции на клик]

** end
