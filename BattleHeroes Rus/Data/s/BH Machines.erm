ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : Battle Heroes: Battle Machines

!?BG0;                                [перед действием в битве, исправление поведения Боевых машин]
!!BG:N?y1 E?y2 A?y6;                  [y1/y2 - номер ходящего отряда / целевой отряд, y6 - действие]
!!BMy1:T?y3 I?y4 U3/?y7;              [y3/y4 - тип / принадлежность ходящего отряда, y7 - боезапас]
!!BMy1&y3=147:U1/0 U2/0;              [если палатка, обнуляем урон]
!!BMy1&y3>=145/y3<=148:S0;            [если боевая машина, обнуляем скорость]
!!BG&y6=7/y3>=145/y3<=146/y7<1:A12;   [пропуск хода, если баллиста или катапульта пытается стрелять без боеприпасов]
!!BMy2&y3=147/y2>-1:I?y5;             [y5 - принадлежность целевого отряда, если такой есть]
!!BG&y3=147/y2>-1/y4<>y5/y6<>1/y6<>4/y6<>5:A8; [действие "ждать", если палатка пытается воздействовать на врага]
!!if&y3=147/y6=11/y2>-1:;             [лечение палатки]
  !!BMy2:H?y11 L?y12;                 [y11/y12 - максимальное/потерянное здоровье исцеляемого отряда]
  !!BMy1:I?y5 N?y10;                  [y5/y10 - номер героя-владельца Палатки/количество Палаток в отряде]
  !!VRy5:+1 *-10;                     [...]
  !!HEy5:S10/?y13;                    [y13 - уровень навыка Боевые машины]
  !!VRy14:Sy13 *25 *y10;              [y14 - минимальное лечение Палатками (0/25/50/75)*кол-во Палаток]
  !!VRy15:Sy13 *25 +25 *y10;          [y15 - максимальное лечение Палатками (25/50/75/100)*кол-во Палаток]
  !!VRy15:-y14;                       [y15 - разброс между минимальным и максимальным лечением Палаток]
  !!VRy14:Ry15;                       [y14 - случайный в пределах разброса объем лечения]
  !!BMy1:G213/?y20/d G213/0/d;        [y20 - (не)удача палаток]
  !!VRy21:Sy2 *10;                    [y21 - шанс выпадения (не)удачи 10% за уровень (не)удачи отряда]
  !!VRy21&y2<0:*-1;                   [...]
  !!VRy22:S0 R99;                     [y22 - случайное число 0.99]
  !!if&y22<y21/y20<0:;                [если выпала неудача]
    !!VRy14::2;                       [сила лечения 50%]
    !!VRz1:S^MISFORT^;                [z1 - звук "исцеления"]
    !!SN:Pz1;                         [воспроизведение звука]
    !!BMy1:V48;                       [анимация неудачи]
  !!en:;
  !!if&y22<y21/y20>0:;                [если выпала удача]
    !!VRy14:*3 :2;                    [сила лечения 150%]
    !!VRz1:S^FORTUNE^;                [z1 - звук "исцеления"]
    !!SN:Pz1;                         [воспроизведение звука]
    !!BMy1:V18;                       [анимация удачи]
  !!en:;
  !!VRy14&y14<y10:Sy10;               [Сила лечения не менее количества Палаток ...]
  !!VRy14&y14>y12:Sy12;               [... и не более, чем потерянное здоровье исцеляемого отряда]
  !!VRy12:-y14;                       [y12 - обновленное "потерянное здоровье"]
  !!if&y14>0:;                        [если эффект лечения есть]
    !!VRz1:S^REGENER.wav^;            [z1 - звук "исцеления"]
    !!SN:Pz1;                         [воспроизведение звука]
    !!BMy2:Ly12 V79;                  [восполнение здоровья и анимация на отряде]
    !!VRz1:Sz179947;                  [формирование сообщения об исцелении палатками]
    !!MM:Sz1;                         [вывод сообщения в лог битвы]
  !!en:;
  !!BG:A12;                           [отмена стандартного лечения]
!!en:;

!?MF1;                                [при нанесении урона Баллистой]
!!BG:N?y1 A?y2;                       [y1/y2 - действующий отряд/тип действия]
!!BMy1&y2=7:T?y3;                     [y3 - тип стреляющего отряда]
!!if&y2=7/y3=146:;                    [если стреляет Баллиста]
  !!MF:N?y4 F?y5;                     [y4/y5 - отряд/получаемый отрядом урон]
  !!BMy4:H?y6 N?y7 L?y8;              [y6/y7/y8 - макс. здоровье/количество существ в отряде/потерянное здоровье отряда]
  !!VRy9:Sy7 *y6 -y8;                 [y9 - общее здоровье отряда]
  !!VRy10:Sy5 :y7;                    [y10 - снижающий здоровье урон]
  !!if&y5<y9/y10<y6/y10>0:;           [если атака не убивает отряд и снижает его здоровье, но не до 0]
    !!VRy11:Sy10 *y7 -y5 *-1;         [y11 - обычный урон]
    !!VRy12:Sy6 -y10;                 [y12 - новое макс. здоровье отряда]
    !!VRy13:Sy8 +y10;                 [y13 - новое потерянное здоровье]
    !!VRy14:Sy7;                      [y14 - новая численность отряда]
    !!if&y13>=y12:;                   [если потерянное здоровье превышает новое максимальное]
      !!VRy15:Sy13 :y12;              [y15 - снижение численности отряда из-за потерянного здоровья и уменьшения макс. здоровья]
      !!VRy13:Sy13 %y12;              [y13 - новое потерянное здоровье после уменьшения численности]
      !!VRy14:-y15;                   [снижаем численность отряда]
    !!en:;
    !!BMy4:Ny14 Ly13 Hy12 V64;        [уменьшение макс. здоровья цели и анимация]
    !!MF:Fy11;                        [коррекция обычного урона]
    !!BMy1:N?y16;                     [y16 - количество Баллист]
    !!UN&y16=1:N3/?z2/146/0;          [z2 - название БМ (ед.ч.)]
    !!UN&y16>1:N3/?z2/146/1;          [z2 - название БМ (мн.ч.)]
    !!VRz1&y16=1:Sz179602;            [формирование сообщения об уроне баллисты]
    !!VRz1&y16>1:Sz179603;            [формирование сообщения об уроне баллист]
    !!MM:Sz1;                         [вывод сообщения в лог битвы]
    !!en:;
!!en:;

!?BR&v997>0;                          [Тележка в начале каждого раунда начиная со 2го]
!!VRv1:C-1/-1;                        [инициализация v1/v2]
!!DO9036/0/41/1:P;                    [поиск тележек с боеприпасами в армиях героев]
!!VRy1:Sv1;                           [y1 - отряд с Тележкой атакующего]
!!VRy2:Sv2;                           [y2 - отряд с Тележкой защищающегося]
!!if&y1>=0:;                          [если у атакующего есть тележка]
  !!BMy1:P?y11 N?y16;                 [y11/y16 - позиция/численность Тележек]
  !!FU9049:Py11/?y12/?y13;            [y13>0, если рядом враг]
  !!if&y13>0:;                        [если тележка блокирована]
    !!UN&y16=1:N3/?z2/148/0;          [z2 - название БМ (ед.ч.)]
    !!UN&y16>1:N3/?z2/148/1;          [z2 - название БМ (мн.ч.)]
    !!VRz1&y16=1:Sz179780;            [z1 - текст сообщения для вывода в лог]
    !!VRz1&y16>1:Sz179781;            [...]
    !!MM:Sz1;                         [добавляем сообщение в лог битвы]
  !!el:;
    !!HE-10:S10/?y3;                  [y3 - навык владения боевыми машинами]
    !!VRv1:C-1/-1/100;                [инициализация v1/v2/v3]
    !!DO9037/0/20/1:P0;               [v1/v2 - колво стрелков/стрелок с мин. боезапасом]
    !!if&v1>-1:;                      [если есть стрелковые отряды с неполным боезапасом]
      !!VRy4&y3>0:Sv2;                [y4 - стрелок с мин. боезапасом, если есть навык владения боевыми машинами]
      !!if&y3<1:;                     [если нет навыка владения боевыми машинами]
        !!VRi:S1 Rv1;                 [... с неполным боезапасом]
        !!VRv1:C-1/-1;                [инициализация v1/v2]
        !!DO9037/0/20/1:Pi;           [v1]
        !!VRy4:Sv1;                   [y4 - случайный стрелок с неполным боезапасом]
      !!en:;
      !!BMy4:T?y5 U3/?y6;             [y6 - пополненный боезапас стрелка:]
      !!MA:Ny5/?y7;                   [...каждая тележка добавляет 1 выстрел...]
      !!BMy1:N?y8;                    [...и +1 выстрел за каждый уровень навыка...]
      !!VRy8:+y3;
      !!BMy1:G213/?y20/d G213/0/d;    [y20 - (не)удача тележек]
      !!VRy21:Sy2 *10;                [y21 - шанс выпадения (не)удачи 10% за уровень (не)удачи отряда]
      !!VRy21&y2<0:*-1;               [...]
      !!VRy22:S0 R99;                 [y22 - случайное число 0.99]
      !!if&y22<y21/y20<0:;            [если выпала неудача]
        !!VRy8::2;                    [перезарядка 50%]
        !!VRz1:S^MISFORT^;            [z1 - звук "исцеления"]
        !!SN:Pz1;                     [воспроизведение звука]
        !!BMy1:V48;                   [анимация неудачи]
      !!en:;
      !!if&y22<y21/y20>0:;            [если выпала удача]
        !!VRy8:*3 :2;                 [перезарядка 150%]
        !!VRz1:S^FORTUNE^;            [z1 - звук "исцеления"]
        !!SN:Pz1;                     [воспроизведение звука]
        !!BMy1:V18;                   [анимация удачи]
      !!en:;
      !!VRy8&y8<1:S1;                 [мин. пополнение боезапаса = 1]
      !!VRy6:+y8;                     [...пополненный боезапас не может превышать...]
      !!VRy6&y6>y7:Sy7;               [...максимальный боезапас стрелка]
      !!BMy1:V76;                     [анимация на Тележке]
      !!BMy4:U3/y6 V75;               [пополнение боезапаса и анимация на отряде стрелка]
      !!UN:N3/?z2/y5/1;               [z2 - название стрелков (мн.ч.)]
      !!VRz1:Sz179188;                [z1 - сообщение для вывода в лог битвы]
      !!MM:Sz1;                       [вывод сообщения в лог битвы]
    !!en:;
  !!en:;
!!en:;
!!if&y2>=0:;                          [если у защищающегося есть тележка]
  !!BMy2:P?y11 N?y16;                 [y11/y16 - позиция/численность Тележек]
  !!FU9049:Py11/?y12/?y13;            [y12>0, если рядом враг]
  !!if&y12>0:;                        [если тележка блокирована]
    !!UN&y16=1:N3/?z2/148/0;          [z2 - название БМ (ед.ч.)]
    !!UN&y16>1:N3/?z2/148/1;          [z2 - название БМ (мн.ч.)]
    !!VRz1&y16=1:Sz179780;            [z1 - текст сообщения для вывода в лог]
    !!VRz1&y16>1:Sz179781;            [...]
    !!MM:Sz1;                         [добавляем сообщение в лог битвы]
  !!el:;
    !!HE-20:S10/?y3;                  [y3 - навык владения боевыми машинами]
    !!VRv1:C-1/-1/100;                [инициализация v1/v2/v3]
    !!DO9037/21/41/1:P0;              [v1/v2 - колво стрелков/стрелок с мин. боезапасом]
    !!if&v1>-1:;                      [если есть стрелковые отряды с неполным боезапасом]
      !!VRy4&y3>0:Sv2;                [y4 - стрелок с мин. боезапасом, если есть навык владения боевыми машинами]
      !!if&y3<1:;                     [если нет навыка владения боевыми машинами]
        !!VRi:S1 Rv1;                 [... с неполным боезапасом]
        !!VRv1:C-1/-1;                [инициализация v1/v2]
        !!DO9037/21/41/1:Pi;          [v1]
        !!VRy4:Sv1;                   [y4 - случайный стрелок с неполным боезапасом]
      !!en:;
      !!BMy4:T?y5 U3/?y6;             [y6 - пополненный боезапас стрелка:]
      !!MA:Ny5/?y7;                   [...каждая тележка добавляет 1 выстрел...]
      !!BMy2:N?y8;                    [...и +1 выстрел за каждый уровень навыка...]
      !!VRy6:+y8 +y3;                 [...пополненный боезапас не может превышать...]
      !!VRy6&y6>y7:Sy7;               [...максимальный боезапас стрелка]
      !!BMy2:V76;                     [анимация на Тележке]
      !!BMy4:U3/y6 V75;               [пополнение боезапаса и анимация на отряде стрелка]
      !!UN:N3/?z2/y5/1;               [z2 - название стрелков (мн.ч.)]
      !!VRz1:Sz179188;                [z1 - сообщение для вывода в лог битвы]
      !!MM:Sz1;                       [вывод сообщения в лог битвы]
    !!en:;
  !!en:;
!!en:;

!?FU9036;                             [поиск тележек с боеприпасами в армиях героев в v1/v2]
!!BMx16:T?y1 N?y2;                    [y1/y2 - тип и количество существ в отряде]
!!VRv1&y1=148/y2>0/x16<21:Sx16;       [v1 - номер отряда с тележкой атакующего героя]
!!VRv2&y1=148/y2>0/x16>20:Sx16;       [v2 - номер отряда с тележкой защищающегося героя]

!?FU9037;                             [подсчет количества стрелков с неполным боезапасом в v1 возврат стрелка с мин. боезапасом в v2(x1=0), выбор в v1 x1-го стрелка с неполным боезапасом (x1=n)]
!!BMx16:F?y1 T?y2 U3/?y3 N?i;         [y1/y2/y3 - флаги отряда/тип существа/кол-во выстрелов отряда]
!!FU|y2<0/i<1:E;                      [выход, если нет отряда]
!!MA:Ny2/?y4 Xy2/?y5;                 [y4/y5 - макс. кол-во выстрелов существа/флаги существа]
!!VRy1:&4;                            [y1=4, если отряд - стрелок]
!!VRy5:&4;                            [y1=5, если существо - стрелок]
!!FU&y1=0/y5=0:E;                     [выход, если отряд/существо не стрелок]
!!FU&y3>=y4:E;                        [выход, если у стрелка полный боезапас]
!!VRv1:+1;                            [v1 - кол-во стрелков с неполным боезапасом]
!!VRv2&x1=0/y3<v3/y4>0:Sx16;          [v2 - номер отряда с мин. боезапасом]
!!VRv3&x1=0/y3<v3/y4>0:Sy3;           [v3 - мин. боезапас]
!!VRi:Sx1 -1;                         [i - порядковый номер искомого отряда]
!!if&v1=i/x1>0:;                      [если найден нужный отряд]
  !!VRv1:Sx16;                        [возвращаем его номер]
  !!VRx16:S9001;                      [и прерываем цикл]
!!en:;

!?FU77006;                            [получение хода отрядом]
!!IF:V442/0;                          [обнуление флага измененной катапульты]
!!SN:X?y1/?y2;                        [y1/y2 - сторона/номер отряда]
!!VRy1:*21 +y2;                       [y1 - ходящий отряд]
!!BMy1:T?y2 U3/?y3;                   [y2/y3 - тип/кол-во выстрелов]
!!BMy1&y2=145:T146;                   [смена типа катапульты на баллисту]
!!IF&y2=145:V442/1;                   [установка флага измененной катапульты]

!?BG0&442;                            [перед действием измененной катапульты]
!!BG:N?y1 A?y2;                       [y1 - номер отряда]
!!BMy1&y2<>1/y2<>4/y2<>5:T145;        [смена типа баллисты на катапульту, если не действие героя]

!?MF1;                                [при нанесении урона Катапультой]
!!BG:N?y1 A?y2;                       [y1/y2 - действующий отряд/тип действия]
!!if|y2=6/y2=7:;                      [если атака или стрельба]
  !!BMy1:T?y3 N?y4;                   [y3/y4 - тип стреляющего отряда/количество существ]
  !!FU&y3<>145:E;                     [выход, если стреляет не Катапульта]
  !!MF:N?i;                           [i - отряд получающий урон]
  !!BMi:T?i;                          [i - тип отряда получающего урон]
  !!FU|i=145/i=146/i=147/i=148/i=116/i=117/i=32/i=33/i=159:E; [выход, если урон получают механизмы (БМ и големы) или привидения]
  !!HE-10&y1<=20:S10/?y5;             [y5 - Навык БМ героя]
  !!HE-20&y1>=21:S10/?y5;             [y5 - Навык БМ героя]
  !!VRy6:Sy5 *7 +20;                  [y6 - базовая вероятность оглушения каждой катапульты 20% + 7% за каждый уровень навыка БМ]
  !!VRv1:S0;                          [инициализация v1]
  !!DO9042/1/y4/1:Py6;                [v1=1 если хотя бы одна из катапульт смогла оглушить противника]
  !!if&v1=1:;                         [если сработало оглушение]
    !!MF:N?y8;                        [y8 - целевой отряд]
    !!BMy8:F?y9;                      [y9 - флаги отряда]
    !!VRy9:|67108864;                 [добавляем флаг "уже ходил"]
    !!BMy8:Fy9 R0 V82;                [обновление флагов, обнуление контрударов, анимация на цели]
    !!VRz1:Sz179189;                  [получаем текст сообщения для вывода в лог]
    !!BU:Mz1;                         [вывод сообщения]
  !!en:;
!!en:;

!?FU9042;                             [с вероятностью x1% возвращает v1=1]
!!VRy1:S1 R99;                        [y1 - случайное число 1-100]
!!VRv1&x1>=y1:S1;                     [v1=1 если вероятность выпала]

!?FU77007;                            [фаза регенерации (перед получением хода)]
!!SN:X?y1;                            [y1 - Отряд]
!!BMy1:T?y2 P?y3 N?y6;                [y2/y3/y6 - тип/позиция/численность отряда]
!!VRy7&y1<21:Sy3 +1;                  [y7 - позиция 2го гекса]
!!VRy7&y1>20:Sy3 -1;                  [...]
!!FU|y2<145/y2>148:E;                 [выход, если не боевая машина]
!!FU9049:Py3/?y4/?y5;                 [y4/y5 - количество атакующих/защищающихся отрядов рядом с первым гексом]
!!FU9049:Py7/?y8/?y9;                 [y8/y9 - количество атакующих/защищающихся отрядов рядом со вторым гексом]
!!FU&y1>20/y4=0/y8=0:E;               [выход, если БМ не блокирована]
!!FU&y1<21/y5=0/y9=0:E;               [...]
!!BMy1:M74/1/3;                       [накладываем паралич на 1 ход]
!!UN&y6=1:N3/?z2/y2/0;                [z2 - название БМ (ед.ч.)]
!!UN&y6>1:N3/?z2/y2/1;                [z2 - название БМ (мн.ч.)]
!!VRz1&y6=1:Sz179780;                 [z1 - текст сообщения для вывода в лог]
!!VRz1&y6>1:Sz179781;                 [...]
!!MM:Sz1;                             [добавляем сообщение в лог битвы]

!?FU77007;                            [фаза регенерации (перед получением хода)]
!!SN:X?y1;                            [y1 - Отряд]
!!BMy1:T?y2 U3/?y3;                   [y2/y3- тип/боезапас отряда]
!!BMy1&y2=145/y3<1:M74/1/3;           [накладываем паралич на 1 ход, если закончился боезапас (катапульта)]
!!BMy1&y2=146/y3<1:M74/1/3;           [накладываем паралич на 1 ход, если закончился боезапас (баллиста)]

!?FU77007;                            [фаза регенерации (перед получением хода)]
!!SN:X?y1;                            [y1 - Отряд]
!!BMy1:T?y2 F?y3;                     [y2/y3 - тип/флаги отряда]
!!FU&y2<>116/y2<>117:E;               [выход, если не мех/штурм големы]
!!HE-10&y1<21:S10/?y4;                [y4 - уровень навыка "Боевые машины" героя]
!!HE-20&y1>20:S10/?y4;                [...]
!!VRy4&y2=116:*15 +15;                [y4 - % срабатывания способности мех.голема (15/30/45/60)]
!!VRy4&y2=117:*25 +25;                [y4 - % срабатывания способности штурм.голема (25/50/75/100)]
!!VRy5:S0 R99;                        [y5 - случайное число 0..99]
!!if&y4>y5:;                          [если способность сработала]
  !!VRy3:|32768 |65536;               [добавляем двойной безответный удар]
!!el:;                                [если способность не сработала]
  !!VRy3:|32768 |65536 -32768 -65536; [убираем двойной безответный удар]
!!en:;                                [...]
!!BMy1:Fy3;                           [обновляем флаги отряда]

** end
