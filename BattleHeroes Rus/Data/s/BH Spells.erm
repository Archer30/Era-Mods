ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

*** Фикс бага Забывчивости, оригинальная действует на все отряды уже на Продвинутом уровне.
!#SS61:F5221;

** Длительность бафов и дебафов равна 3 + Уровень Школы
!?BG0;                        [Перед действием в бою]
!!BG:A?y1 S?y2 Q?y3;          [y1/y2/y3 - действие/заклинание/сторона]
!!FU|y1<>1:E;                 [выход, если не колдовство заклинания героем]
!!SSy2:F?y4; !!VRy4:&4; !!FU&y4=0:E; [выход, если колдуется не баф/дебаф]
!!VRy5:Sy3 +1 *-10;           [y5 - герой]
!!FU9064:Py2/0/y5/?y6;        [y6 - уровень Школы]
!!VRy7:S3 +y6;                [y7 - длительность заклинания = 3 + Уровень Школы]
!!UN:C6919200/4/?y8;          [изменение Силы магии героя, используемой при наложени заклинания...]
!!VRy9:Sy3 *4 +21460 +y8;     [...отдельное большое спасибо Sav'у за пример...]
!!UN:Cy9/4/?y10 Cy9/4/y7;     [Установка Силы героя равной длительности заклиания]
!!SN:W^RestoreHeroPower1^/y9; [сохранение оригинального значения Силы героя для восстановления]
!!SN:W^RestoreHeroPower2^/y10;[...]

!?BG1;                        [После действия в бою]
!!SN:W^RestoreHeroPower1^/?y1;[восстановление оригинального значения Силы героя после каста (де)бафа]
!!SN:W^RestoreHeroPower2^/?y2;[...]
!!UN&y1>0/y2>0:Cy1/4/y2;      [...]
!!SN:W^RestoreHeroPower1^/0;  [обнуление сохраненных значения]
!!SN:W^RestoreHeroPower2^/0;  [...]

*** Силовое поле имеет длительность A+SP/B
!?BG0;                        [Перед действием в бою]
!!BG:A?y1 S?y2 D?y3 Q?y4;     [y1/y2/y3/y4 - действие/заклинание/место/сторона]
!!VRy5:Sy4 +1 *-10;           [y5 - герой]
!!FU|y1<>1/y2<>12:E;          [выход, если не колдовство "Силового поля"]
!!FU9064:P12/0/y5/?y6;        [y6 - уровень Школы]
!!HEy5:Fd/d/?y7/d;            [y7 - Сила героя]
!!VRy10:S3 +y6;               [длительность Силового поля = 3 + Уровень Школы]
!!UN:C5901022/4/y10;          [устанавливаем длительность (спс. gamecreator'у)]

!?FU9064;                     [возврат в x4 действующего уровня Школы заклинания x1 в бою (x2=0) или на карте (x2=1) для героя x3]
!!VRy10:S0;                   [обнуляем искомое значение]
!!HE&x2=1:P?y1/?y2/?y3;       [y1/y2/y3 - координаты места колдовства]
!!BA&x2=0:P?y1/?y2/?y3;       [...]
!!TRy1/y2/y3:G?y4;            [y4 - тип бонусной земли]
!!SSx1:S?y5;                  [y5 - биты школ заклинания]
!!VRy6:Sy5 &1;                [y6>0 - заклинание Воздуха]
!!VRy7:Sy5 &4;                [y7>0 - заклинание Воды]
!!VRy8:Sy5 &2;                [y8>0 - заклинание Огня]
!!VRy9:Sy5 &8;                [y9>0 - заклинание Земли]
!!HEx3:S15/?y11 S16/?y12 S14/?y13 S17/?y14; [y11-y14 Уровни навыков Школ героя]
!!VRy10&y6>0/y10<y11:Sy11;    [y10 - макс. уровень по Школам героя и бонусам террирорий]
!!VRy10&y7>0/y10<y12:Sy12;    [...]
!!VRy10&y8>0/y10<y13:Sy13;    [...]
!!VRy10&y9>0/y10<y14:Sy14;    [...]
!!VRy10&y4=46:S3;             [маг. земля]
!!VRy10&y6>0/y4=229:S3;       [маг. облака]
!!VRy10&y7>0/y4=228:S3;       [прозрачные пруды]
!!VRy10&y8>0/y4=226:S3;       [огненные поля]
!!VRy10&y9>0/y4=231:S3;       [скалистая земля]
!!VRx4:Sy10;                  [Возвращаем макс. значение Школы]

*** Защита от стихий (вместо "Защиты от воды")
!#UN:J0/30/1 J0/31/1 J0/32/1; [Запрет заклинаний Защита от Воздуха/Воды/Огня]

!?BG0;                        [Перед действием в бою]
!!BG:A?y1 S?y2 D?y3 Q?y8;     [y1/y2/y3/y8 - действие/заклинание/место/сторона]
!!VRy15:Sy8 +1 *-10;          [y15 - герой]
!!FU|y1<>1/y2<>32:E;          [выход, если не колдовство "Защиты от воды"]
!!HEy15:S16/?y4;              [y4 - уровень Магии Воды]
!!SS32:Cy4/?y13 Cy4/1;        [y13 - стоимость заклинания Защиты от стихий, сброс стоимости на 1]
!!VRy14:S3 +y4;               [y14 - длительность заклинаний 3 + Уровень Школы]
!!UN:C6919200/4/?y10;         [изменение Силы магии героя, используемой при наложени заклинания...]
!!VRy11:Sy8 *4 +21460 +y10;   [...отдельное большое спасибо Sav'у за пример...]
!!UN:Cy11/4/?y12 Cy11/4/y14;  [Установка Силы героя равной длительности заклиания]
!!BHy8:C30/y3/y4/0 C31/y3/y4/0 C32/y3/y4/0 C33/y3/y4/0 M1; [Колдовство "защит", запрет повторного колдовства в раунде]
!!UN:Cy11/4/y12;              [восстановление Силы героя]
!!VRy9:S4 -y13;               [y9 - кол-во маны для возврата]
!!HEy15:Idy9/1;               [Корректировка маны героя]
!!SS32:Cy4/y13;               [Восстанавливаем стоимости заклинаний защиты]
!!BG:A0;                      [отмена штатного колдовства]
!!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [перерисовываем тень перемещения]

*** Перзарядка (вместо "Устранения преград")
!#SS64:F278561 O1;            [флаги заклинания: 1+32+16384+262144]

!?BG0;                        [Перед действием в бою]
!!BG:A?y1 S?y2 E?y3 Q?y8;     [y1/y2/y3/y8 - действие/заклинание/отряд/сторона]
!!FU|y1<>1/y2<>64:E;          [выход, если не колдовство "устранения преград"]
!!BMy3:T?y6 U3/?y7;           [y6/y7 - тип существа/текущий боезапас]
!!UN:N3/z3/y6/1;              [z3 - название целевого отряда существ]
!!MA:Ny6/?y6;                 [y6 - максимальный боезапас]
!!if&y6<=y7:;                 [если у отряда полный боезапас]
  !!BG:A0;                    [отмена действия]
  !!BHy8:M0;                  [разрешение посторного колдовства в раунде]
  !!FU:E;                     [выход]
!!en:;
!!VRy6:-y7;                   [y6 - недостающий боезапас]
!!FU9064:P64/0/-1/?y4;        [y4 - уровень Школы]
!!SS64:Cy4/?y5 Ey4/?y7;       [y5/y7 - стоимость/эффект заклинания]
!!VRy5:*-1;                   [...]
!!VRy7&y7>y6:Sy6;             [если эффект сильнее чем недост. боезапас, восстанавливаем недост. боезапас]
!!HE-1:Idy5/1;                [уменьшаем ману героя]
!!VRy8:*16;                   [y8 - позиция героя]
!!UN:C6919200/4/?i;           [анимация на герое]
!!SN:E4810128/2/i/76/y8/1/0;  [...]
!!BMy3:U3/dy7 V75;            [восстановление боезапаса и анимация на отряде]
!!HE-1:B0/?z1;                [z1 - имя героя]
!!UN:N1/z2/64;                [z2 - название заклинания]
!!VRz4:Sz179600;              [z4 - строка для вывода в лог]
!!MM:Sz4;                     [вывод сообщения в лог битвы]
!!BHy8:M1;                    [запрет повторного колдовства в раунде]
!!BG:A0;                      [отмена штатного колдовства]
!!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [перерисовываем тень перемещения]

!?BG0;                        [ИИ]
!!HE-1:O?y1 M64/?y2 I?y4/1 S15/?y5; [y1/y2/y4/y5 - игрок/наличие заклинания "Перезарядка"/мана/уровень Магии Воздуха]
!!OW:Iy1/?y3;                 [y2 - ИИ-признак]
!!SS64:Cy5/?y6;               [y6 - стоимость заклинания]
!!BG:Q?y7;                    [y7 - текущая сторона]
!!BHy7:M?y8;                  [y8 - признак колдовства в раунде]
!!FU|y2=0/y3=0/y4<y6/y8>0:E;  [выход если не ИИ/нет заклинания/недостаточно маны/уже колдовал в раунде]
!!VRy8:Sy7 *21;               [y8..y9 - диапазон отрядов для поиска]
!!VRy9:Sy8 +20;               [...]
!!DO9026/y8/y9/1:P1/?y10;     [y10 - номер отряда стрелков с боезапасом < 4]
!!if&y10>-1:;                 [если подходящий целевой отряд найден]
  !!BG:A0;                    [отмена действия]
  !!BMy10:P?y11;              [позиция существа]
  !!BHy7:C64/y11/y5/1 M1;     [колдовство "Перезарядки" и запрет повторного колдовства в раунде]
!!en:;

*** Призыв элементалей в один отряд, замена элементалей на посланников
!?BG0;                                [действие в бою]
!!BG:A?y1;                            [y1 - тип действия]
!!FU&y1<>1:E;                         [выход, если не колдовство героя]
!!BG:S?y1 Q?y2;                       [y1 - номер колдующегося заклинания, y2 - текущая сторона]
!!FU|y1<66/y1>69:E;                   [выход, если не призыв элементалей]
!!VRy4&y1=66:S164;                    [y4 - номер призываемого существа]
!!VRy4&y1=67:S165;                    [y4 - номер призываемого существа]
!!VRy4&y1=68:S167;                    [y4 - номер призываемого существа]
!!VRy4&y1=69:S166;                    [y4 - номер призываемого существа]
!!BA:Hy2/?y5;                         [y5 - колдующий герой]
!!FU9064:Py1/0/y5/?y7;                [y7 - уровень Школы для заклианния]
!!SSy1:Cy7/?y9 Ey7/?y10 P?y11;        [y9/y10/y11 цена/бонус/эффект заклинания]
!!VRy9:*-1;                           [...]
!!HEy5:Fd/d/?y12/d Idy9/1;            [y12 - Сила героя, уменьшаем ману]
!!BHy2:M1;                            [запрещаем колдовство в этом раунде]
!!VRy13:Sy12 *y11 +y10;               [y13 - количество призванных существ (SP*P+E)]
!!HEy5:A2/79/?i/?y14 A2/80/?i/?y15 A2/81/?i/?y16 A2/82/?i/?y17; [y14-y17 Сферы стихий у героя]
!!VRy13&y1=66/y16>0:*3 :2;            [+50% к числу призываемых посланников, если на герое надета Сфера соотв. стихии]
!!VRy13&y1=67/y17>0:*3 :2;            [...]
!!VRy13&y1=68/y15>0:*3 :2;            [...]
!!VRy13&y1=69/y14>0:*3 :2;            [...]
!!FU9010:Py4/y2/y13/0/3;              [призываем посланников]
!!HEy5:B0/?z1;                        [z1 - имя героя]
!!UN:N1/2/y1;                         [z2 - Название заклинания]
!!VRz3:Sz179601;                      [z3 сообщение для вывода в лог боя]
!!MM:Sz3;                             [вывод сообщения в лог боя]
!!BG:A0;                              [отменяем штатный призыв]
!!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [перерисовываем тень перемещения]

** Призыв посланников командиром
!?BG0;                                [действие в бою]
!!BG:A?y1;                            [y1 - тип действия]
!!FU&y1<>10:E;                        [выход, если не колдовство существа]
!!BG:Q?y2 N?y3;                       [y2 - текущая сторона, y3 - активный отряд]
!!BMy3:T?y4 U4/?y1 E?y30;             [y4 - тип существ активного отряда, y1 - номер колдующегося заклинания, y30 - кол-во заклинаний]
!!FU|y1<66/y1>69/y4<174/y4>191/y30<1:E;[выход, если не призыв элементалей командиром]
!!VRy4&y1=66:S164;                    [y4 - номер призываемого существа]
!!VRy4&y1=67:S165;                    [y4 - номер призываемого существа]
!!VRy4&y1=68:S167;                    [y4 - номер призываемого существа]
!!VRy4&y1=69:S166;                    [y4 - номер призываемого существа]
!!BA:Hy2/?y5;                         [y5 - герой]
!!COy5:P4/?y6;                        [y6 - сила магии командира]
!!VRy7:S2;                            [y7 - множитель призыва элементалей]
!!BA:P?y11/?y12/?y13;                 [y11-y13 - координаты битвы]
!!TRy11/y12/y13:G?y8;                 [y8 - тип бонусной земли]
!!VRy7&y8=46:S3;                      [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=69/y8=229:S3;               [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=66/y8=226:S3;               [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=68/y8=228:S3;               [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=67/y8=231:S3;               [множитель призыва на соотв. бонусной земле]
!!HEy5:A2/79/?i/?y14 A2/80/?i/?y15 A2/81/?i/?y16 A2/82/?i/?y17; [y14-y17 Сферы стихий у героя]
!!SSy1:Ey7/?y21 P?y22;                [y21/y22 - бонус/эффект заклинания]
!!VRy9:Sy6 *y22 +y21;                 [y9 - количество призызываемых элементалей]
!!VRy9&y1=66/y16>0:*3 :2;             [+50% к количеству призываемых, если на герое надета Сфера соотв. стихии]
!!VRy9&y1=67/y17>0:*3 :2;             [...]
!!VRy9&y1=68/y15>0:*3 :2;             [...]
!!VRy9&y1=69/y14>0:*3 :2;             [...]
!!BMy3:G-87/12/d G-86/0/d;            [анимация каста командира]
!!BU:R;                               [...]
!!FU9010:Py4/y2/y9/0/3;               [призываем посланников]
!!COy5:N?z1;                          [z1 - имя командира]
!!UN:N1/z2/y1;                        [z2 - название заклинания]
!!VRz4:Sz179601;                      [z4 - строка для вывода в лог]
!!MM:Sz4;                             [вывод сообщения в лог битвы]
!!BG:A12;                             [тратим ход командира]
!!BMy3:E0;                            [обнуляем кол-во заклинаний командира]
!!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [перерисовываем тень перемещения]

*** Лечение. Снижение ценности, если командир не сильно ранен.
!?PI;                                 [перед началом игры]
!!SS37:I0/?y1 I1/?y2 I2/?y3 I3/?y4;   [y1-y4 - базовые значения ценностей Лечения]
!!SN:W^cure_0^/y1 W^cure_1^/y2 W^cure_2^/y3 W^cure_3^/y4; [сохраняем значения]

!?BR&v997>0;                          [начало раунда]
!!BA:O?y1/?y2;                        [y1/y2 атакующий/защищающийся игроки]
!!OW:Iy1/?y11 Iy2/?y12;               [y11/y12 - ИИ статусы игроков]
!!FU&y11=0/y12=0:E;                   [выход, если Человек VS Человек]
!!VRi:S9996 +y12;                     [i - номер отряда ИИ-командира]
!!BMvi:H?y3 L?y4;                     [y3/y4 м.здоровье/ранения ИИ командира]
!!VRy5:Sy4 *100 :y3;                  [y5 - %раненния (0 - здоров)]
!!SN:W^cure_0^/?y1 W^cure_1^/?y2 W^cure_2^/?y3 W^cure_3^/?y4; [y1-y4 оригинальные ценности Лечения]
!!SS37&y5>=50:I0/y1 I1/y2 I2/y3 I3/y4;[оригинальные ценности Лечения, если ИИ-командир ранен на 50+%]
!!SS37&y5<50:I0/1 I1/1 I2/1 I3/1;     [минимальные ценности Лечения, если ИИ-командир ранен менее чем на 50%]

*** Невосприимчивость к магии
!?MR2;                                [Невосприимчивость к магии]
!!MR:S?y1;                            [y1 - заклинание]
!!MR:M?y2;                            [y2 - тип существа]
!!SSy1:L?y3 S?y4 F?y5;                [y3/y4/y5 - уровень/школы/флаги заклинания]
!!VRy10:Sy5 &512;                     [y10>0 - заклианние урона]
!!VRy11:Sy4 &1;                       [y11>0 - заклинание воздуха]
!!VRy12:Sy4 &4;                       [y12>0 - заклинание воды]
!!VRy13:Sy4 &2;                       [y13>0 - заклинание огня]
!!VRy14:Sy4 &8;                       [y14>0 - заклинание земли]
!!VRy15:Sy5 &1024;                    [y15>0 - заклианние разума]
!!VRy16:Sy5 &4096;                    [y16>0 - заклианние не действующее на БМ]

!_!MR&y1=39/y2=172:F0;
!_!MR&y1=39/y2=159:F0;

** Посланники
!!if&y2>=164/y2<=167:;
  !!MR:F0;                            [по умолчанию на Посланников действуют все заклинания]
  !!MR&y11>0/y2=166:F100;             [Заклинания не действуют на посланников соответствующей стихии]
  !!MR&y12>0/y2=167:F100;             [...]
  !!MR&y13>0/y2=164:F100;             [...]
  !!MR&y14>0/y2=165:F100;             [...]
  !!MR&y15>0:F100;                    [Заклинания Разума не действуют на Посланников]
!!en:;
** Волна смерти
!!if&y1=24:;                          [Волна смерти не действует на]
  !!MR|y2=32/y2=33/y2=116/y2=117/y2=164/y2=165/y2=166/y2=167:F100;[Големов и посланников]
  !!MR|y2=120/y2=121/y2=123/y2=125/y2=127/y2=129/y2=112/y2=113/y2=114/y2=115:F100;[Элементалей]
  !!MR|y2=182/y2=191/y2=52/y2=53/y2=36/y2=37/y2=30/y2=31:F100; [Астрального духа, джиннов, ифритов, гаргулий]
!!en:;
** Перезарядка
!!if&y1=64:;                          [Перезарядка не действует на]
  !!MA:Xy2/?y3;                       [нестреляющих существ]
  !!VRy3:&4;                          [...]
  !!MR&y3=0:F100;                     [...]
  !!BG:Q?y3;                          [за исключением командиров с навыком "Стрельба"]
  !!VRy4:S0;                          [...]
  !!BMv9996&y3=0:F?y4;                [...]
  !!BMv9997&y3=1:F?y4;                [...]
  !!VRy4:&4;                          [...]
  !!MR&y4>0:F0;                       [...]
!!en:;

*** Уязвимость к магии
!?MR1;                                [Уязвимость к магии]
!!MR:S?y1;                            [y1 - заклинание]
!!MR:M?y2;                            [y2 - тип существа]
!!SSy1:L?y3 S?y4 F?y5;                [y3/y4/y5 - уровень/школы/флаги заклинания]
!!VRy10:Sy5 &512;                     [y10>0 - заклианние урона]
!!VRy11:Sy4 &1;                       [y11>0 - заклинание воздуха]
!!VRy12:Sy4 &4;                       [y12>0 - заклинание воды]
!!VRy13:Sy4 &2;                       [y13>0 - заклинание огня]
!!VRy14:Sy4 &8;                       [y14>0 - заклинание земли]
!!VRy15:Sy5 &1024;                    [y15>0 - заклианние разума]
!!VRy16:Sy5 &4096;                    [y16>0 - заклианние не действующее на БМ]
** Посланники
!!if&y10>0/y2>=164/y2<=167:;          [Урон противоположной стихии удваивается для посланников]
  !!MR:D?y8;                          [y8 - базовый урон]
  !!MR&y11>0/y2=166:F0;               [Заклинания не действуют на посланников соответствующей стихии]
  !!MR&y12>0/y2=167:F0;               [...]
  !!MR&y13>0/y2=164:F0;               [...]
  !!MR&y14>0/y2=165:F0;               [...]
  !!MR&y15>0:F0;                      [Заклинания Разума не действуют на Посланников]
  !!VRy9:Sy8 *2;                      [y9 - удвоенный урон]
  !!MR&y11>0/y2=165:Fy9;              [установка удвоенного урона посланникам противоположной стихии]
  !!MR&y12>0/y2=164:Fy9;              [...]
  !!MR&y13>0/y2=167:Fy9;              [...]
  !!MR&y14>0/y2=166:Fy9;              [...]
!!en:;

***
