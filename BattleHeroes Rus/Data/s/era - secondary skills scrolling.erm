ZVSE
; [Function: GetNumSkills]
; Description:
;   Calculate number of learnt secondary skills
; Arguments:
;   Hero ID (x1)
;   ?Result (x2)
!?FU77997;
!!VRx2:S0;      #0 zero skills counter
!!VRy1:S0;      #1 y1 is skill ID
!!HEx1:Sy1/?y2; #2 y2 is skill value
!!VRx2&y2>0:+1; #3 increase skills counter
!!VRy1:+1;      #4 go to next skill
!!SN&y1<28:G2;  #5 cycle to command 2 if not all skills were processed

; [Function: Update skills screen]
; Description:
;   Updates secondary skills screen according to variable "CurrPage"
; Arguments:
;   Hero ID (x1)
!?FU77996;
!!SN:W^SecSkills.CurrPage^/?y1;   #0  y1 - current page of skills
!!VRy2:Sy1*8;                     #1  y2 - number of hidden skills to skip or
;                                     negative number of displayed skills on this page
!!VRy3:S1;                        #2  y3 - skill slot number
!!VRy4:S0;                        #3  y4 - currently checked skill ID
!!HEx1:Sy4/?y5;                   #4  y5 - skill value
!!if&y5>0:;                       #5  if skill is learnt
  !!VRy2:-1;                      #6  decrease hiddent skills number
  !!if&y2<0:;                     #7  if all hidden skills were skipped
    !!HEx1:Sy3/y4/1;              #8  display this skill
    !!VRy3:+1;                    #9  increase skill slot ID
  !!en:;                          #10
!!en:;                            #11
!!VRy4:+1;                        #12 go to next skill ID
!!SN&y4<28/y2<>-8:G4;             #13 loop until 8 skills are displayed or end of skills is reached
!!VRy2:*-1;                       #14 y2 - number of displayed skills on this page
!!HEx1:Sy2;                       #15 show y2 number of skills

; [On enter Hero Screen]
!?FU77004;
!!SN:W^SecSkills.CurrPage^/0; set current skills page to 0 (8 skills per page)
!!FU77996:P-1;                update skills screen

; [On close Hero Screen]
!?FU77005;
!!SN:W^SecSkills.CurrPage^/0; set current skills page to 0 (8 skills per page)
!!FU77996:P-1;                update skills screen

; [On mouse click at Hero Screen]
!?CM2;
!!CM:I?y1;                                  y1 - click ID
!!CM:S=13; !!FU&-1:E; !!CM:F=0; !!FU&-1:E;  exit if not LMB released
!!SN:L^Era.dll^/?y2 Ay2/^GetButtonID^/?y3;  y2 - era.dll, y3 - function GetButtonID
!!SN:Ey3/0/^SecSkills.ScrollUp^;            y4 - button "SecSkills.ScrollUp" ID
!!VRy4:Sv1;
!!SN:Ey3/0/^SecSkills.ScrollDown^;          y5 - button "SecSkills.ScrollDown" ID
!!VRy5:Sv1;

!!if&y1=y4:;                                click on UP-arrow
  !!CM:R0;                                  disable default reaction
  !!SN:W^SecSkills.CurrPage^/?y6;           y6 - current skills page
  !!if&y6>0:;                               if it's not the first page
    !!VRy6:-1;                              go to the previous page
    !!SN:W^SecSkills.CurrPage^/y6;          save page number
    !!FU77996:P-1;                          update skills screen
    !!UN:R3/-1;                             update hero screen
  !!en:;
!!en:;

!!if&y1=y5:;                                click on DOWN-arrow
  !!CM:R0;                                  disable default reaction
  !!SN:W^SecSkills.CurrPage^/?y6;           y6 - current skills page
  !!FU77997:P-1/?v1;                        get learnt skills number
  !!VRy7:Sv1;                               y7 - skills number
  !!VRy8:Sy6+1*8;                           y8 - max number of skills for this page
  !!if&y7>y8:;                              if we can proceed to the next page
    !!VRy6:+1;                              increase page number
    !!SN:W^SecSkills.CurrPage^/y6;          save it
    !!FU77996:P-1;                          update skills screen
    !!UN:R3/-1;                             update hero screen
  !!en:;
!!en:;
