ZVSE2

*** Файл настроек, таймер и отключение автосохранения
!#UN:J3/^BattleHeroes.dat^ J5/0;
!#TM20:S1/3/1/8;                   [Триггер в первый/второй день для зеленого игрока]

!?TM20;                            [в первый день для зеленого игрока]
!!VRi:Sc;                          [i - номер дня]
!!if&i<3:;                         [в первый день]
  !!UN:C6919480/4/?y1;             [передаем ход следующему игроку (с)пасибо feanor'у]
  !!SN:E5008544/2/y1;              [...]
  !!FU:E;
!!en:;
!!HE20/27/0:N?y1;                  [y1 - основной герой]
!!HE0/0/0:N?y2;                    [y2 - 1й противник]
!!HE0/1/0:N?y3;                    [y3 - 2й противник]
!!HE0/2/0:N?y4;                    [y4 - 3й противник]
!!DO29001/4/170/1:Py1 Py2 Py3 Py4; [удаление войск, артефактов и заклинаний у героев]
!!UN:J6/2/?y11;                    [y11 - артефакт 1-го противника (treasure)]
!!UN:J6/2/?y12; !!UN&y12=y11:J6/2/?y12; !!UN:J6/4/?y13; [y12,y13 - артефакы 2-го противника (treasure, minor)]
!!UN:J6/4/?y14; !!UN:J6/4/?y15; !!UN&y14=y15:J6/4/?y15; [y14,y15 - артефакы 3-го противника (minor, minor)]
!!HEy2:A4/y11;                     [1й противник получает артефакт]
!!HEy3:A4/y12 A4/y13;              [2й противник получает артефакты]
!!HEy4:A4/y14 A4/y15;              [3й противник получает артефакты]
!!FU29002:Py1/0/0/0;               [вызов битвы с 1м противником]
!!HEy1:P?i/j/k; !!FU&i<0:E;        [выход, если герой проиграл]
!!FU29002:Py1/0/1/0;               [вызов битвы со 2м противником]
!!HEy1:P?i/j/k; !!FU&i<0:E;        [выход, если герой проиграл]
!!FU29002:Py1/0/2/0;               [вызов битвы с 3м противником]

!?FU29001;                         [удаление войск, артефактов и заклинаний у героя]
!!HEx1&x16=4:C0/0/-1/0 C0/1/-1/0 C0/2/-1/0 C0/3/-1/0 C0/4/-1/0 C0/5/-1/0 C0/6/-1/0; [удаление войск]
!!HEx1:A3/x16/3/1;                 [удаление артефактов]
!!HEx1&x16>90/x16<70:Mx16/0;       [удаление заклианний]

!?BA0;                             [перед началом боя]
!!VRy11:S0 R15; !!VRy12:S0 R15;    [y11/y12/0 - координаты поля боя]
!!BA:Py11/y12/0;                   [установка координат поля боя]

!?FU29002;                         [посещение объекта героем (с)пасибо igrik'у]
*** х1     - номер героя, x2..x4 - координаты объекта на карте
!!UN:C6918840/4/?y10;               // int pAdvManager
!!SN:E7411341/1/x1; !!VRy2:Sv1;     // получить структуру героя
!!UN:C10784129/1/0 C10784128/1/x2;  // записать все данные побайтно в адрес y1
!!UN:C10784130/1/x3 C10784131/1/x4; // записать все данные побайтно в адрес y1
!!SN:E7410371/1/y1; !!VRy3:Sv1;     // получить адрес структуры клетки в y3
!!SN:E4882784/2/y10/y2/y3/y1;       // посещение героем объекта

*** end