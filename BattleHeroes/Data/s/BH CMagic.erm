ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

*** Повтор хода отрядом (с)пасибо igrik'у
!?FU9066;
!!SN:W^RepeatRun^/?y1 W^RepeatStack^/?y3; !!FU&y1<>1:E; [выход, если нет повторного хода]
!!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [обновляем сетку]
!!SN:X?y1/0; !!VRy1:+32; !!UN:Cy1/4/4607736;            [пропускам передачу хода]
!!SN:W^RepeatRun^/0;                                    [обнуляем переменную повторного хода]
!!BMy3:F?y4; !!VRy4:|67108864 -67108864; !!BMy3:Fy4;    [сбрасываем флаг "уже ходил" для отряда]
!!SN:W^RestoreCmndrPower1^/?y5;                         [восстановление оригинального значения Силы командира после каста (де)бафа]
!!SN:W^RestoreCmndrPower2^/?y6;                         [...]
!!COy5&y6>0:P4/y6;                                      [сила магии командира = 5 (длительность (де)бафов на продвинутом уровне)]    
!!SN:W^RestoreCmndrPower1^/0;                           [сброс сохраненных значений]
!!SN:W^RestoreCmndrPower2^/0;                           [...]
!!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [перерисовываем тень перемещения]

!?PI;
!!SN:L^EraPlugins\erm_hooker.era^/?y1; !!FU&y1=0:E;     [выход если нет erm_hooker'а]
!!SN:Ay1/^SetHook^/?y2; !!SN:Ey2/1/4607082/9066;        [установка хука]

!?GM0
!!SN:L^EraPlugins\erm_hooker.era^/?y1; !!FU&y1=0:E;     [выход если нет erm_hooker'а]
!!SN:Ay1/^SetHook^/?y2; !!SN:Ey2/1/4607082/9066;        [установка хука]
***

!?BG1;
!!BG:N?y3;                            [y3 - отряд, который должен ходить]
!!VRy1:S0;                            [y1 - сторона]
!!VRy1&y3>20:S1;                      [...]
!!FU&y3<>v9996/y3<>v9997:E;           [выход, если ходит не командир]
!!BMy3:E0;                            [обнуление кол-ва заклинаний командира]
!!SN:W^Cmndr%Y1R^/?y2 W^Cmndr%Y1R^/v997;[получаем и перезаписываем раунд в котором командир колдовал/пытался колдовать]
!!FU&y2=v997:E;                       [выход, если командир уже колдовал в этом раунде]
!!VRy2:S0 R9;                         [y2 - слот магии командира]
!!SN:W^Cmndr%Y1Sp%Y2^/?y4;            [y4 - заклинание в слоте]
!!FU&y4<0:E;                          [выход, если в слоте нет заклинания]
!!HE-10&y1=0:N?y5;                    [y5 - номер героя/командира]
!!HE-20&y1=1:N?y5;                    [...]
!!HE-10&y1=1:N?y10;                   [y10 - номер вражеского героя/командира]
!!HE-20&y1=0:N?y10;                   [...]
!!COy5:P4/?y6;                        [y6 - сила магии командира]
!!VRy7:S-1;                           [инициализация y7]
!!FU9050:P?y7/y4/y1/y3/y5/y10;        [y7 - целевой отряда для выбранного заклинания]
!!FU&y7<0:E;                          [выход, если подходящая цель для заклинания не выбрана]
!!VRv9963:Sy4;                        [v9963 - номер колдующегося заклинания для Глаза Орла]
!!BMy3:E1 U4/y4;                      [смена заклинания командира]
!!BMy7:P?y8;                          [y8 - позиция целевого отряда]
!!if&y4=64:;                          [каст заклианния "Перезарядка"]
  !!BMy7:T?y16 U3/?y17;               [y16/y17 - тип существа/текущий боезапас]
  !!UN:N3/z3/y16/1;                   [z3 - название целевого отряда существ]
  !!MA:Ny16/?y16;                     [y16 - максимальный боезапас]
  !!VRy16:-y17;                       [y16 - недостающий боезапас]
  !!SS64:E2/?y17;                     [y17 - эффект заклинания (продвинутый уровень)]
  !!VRy17&y17>y16:Sy16;               [если эффект сильнее чем недост. боезапас, восстанавливаем недост. боезапас]
  !!BMy3:G-87/12/d G-86/0/d;          [анимация каста командира]
  !!BU:R;                             [...]
  !!BMy7:U3/dy17 V75;                 [восстановление боезапаса и анимация на отряде]
  !!COy5:N?z1;                        [z1 - имя командира]
  !!UN:N1/z2/64;                      [z2 - название заклинания]
  !!VRz4:Sz179600;                    [z4 - строка для вывода в лог]
  !!MM:Sz4;                           [вывод сообщения в лог битвы]
  !!BMy3:E0;                          [обнуление кол-ва заклинаний командира]
  !!UN:C6919200/4/?y2; !!SN:E4797616/2/y2/0/1; !!BU:R;    [перерисовываем тень перемещения]
!!el:;                                [каст другого заклинания]
  !!SN:W^RepeatRun^/1 W^RepeatStack^/y3; [включение повторного хода для отряда]
  !!SSy4:F?y19; !!VRy19:&4;           [y4 - флаг баф/дебаф активного заклинания]
  !!if&y19>0:;                        [для (де)бафов]
    !!COy5:P4/5;                      [сила магии командира = 5 (длительность (де)бафов на продвинутом уровне)]    
    !!SN:W^RestoreCmndrPower1^/y5;    [сохранение оригинального значения Силы командира для восстановления]
    !!SN:W^RestoreCmndrPower2^/y6;    [...]
  !!en;
  !!BG:A10 S-1 Dy8;                   [действие - каст заклинания]
!!en:;

!?FU9050;                             [возврат в x1 целевого отряда для выбранного заклинания]
** x2 - заклинание, x3 - сторона (0/1), x4 - отряд командира, x5 - номер командира, x6 - номер вражеского командира
!!if|x2=66/x2=67/x2=68/x2=69:;        [для заклинаний призыва]
  !!VRx1:Sx4;                         [цель - сам командир]
  !!FU:E;                             [...]
!!en:;
** параметры заклинания
!!TRy11/y12/y13:G?y1;                 [y1 - тип бонусной земли]
!!SSx2:S?y2;                          [y2 - маска школы]
!!VRy3:Sy2 &1;                        [y3>0 для заклинания Воздуха]
!!VRy4:Sy2 &4;                        [y4>0 для заклинания Воды]
!!VRy5:Sy2 &2;                        [y5>0 для заклинания Огня]
!!VRy6:Sy2 &8;                        [y6>0 для заклинания Земли]
!!VRy7:S3;                            [y7 - уровень школы с учетом магической почвы]
!!VRy7&y1=46:S4;                      [...]
!!VRy7&y3>0/y1=229:S4;                [...]
!!VRy7&y4>0/y1=228:S4;                [...]
!!VRy7&y5>0/y1=226:S4;                [...]
!!VRy7&y6>0/y1=231:S4;                [...]
!!SSx2:P?y8 Ey7/?y9 L?y10 F?y11;      [y8/y9/y10/y11 - эффект/бонус/уровень/флаги]
!!VRy13:Sy11 &1024;                   [y13 > 0 для заклинаний разума]
!!VRy14:Sy11 &2048;                   [y14 > 0 для дружественных, 0 для враждебных заклинаний]
!!VRy15:Sy11 &16384;                  [y15 > 0 для защищающих заклинаний]
!!VRy22:Sy11 &512;                    [y22 > 0 для заклинаний урона]
!!VRy14|y14>0/y15>0/x2=38/x2=39:S1;   [y14=0/1 - враждебное/дружественное заклинание]
!!VRy14&x2=65:S1; !!VRy13&x2=65:S0;   [клон - дружественное заклинание и не относится проверяет резист цели к заклианниям разума]
** почва и артефакты
!!HE-10:A2/126/d/?y15 A2/83/d/?y17;   [y15/y16 - Сферы запрещения]
!!HE-20:A2/126/d/?y16 A2/83/d/?y18;   [y17/y18 - Плащи отречения]
!!FU|y15>0/y16>0:E;                   [выход, если действует Сфера Запрещения]
!!FU&y17>0/y10>2:E;                   [выход, если действует Плащ отречения а заклинание выше 2го уровня]
!!FU&y18>0/y10>2:E;                   [выход, если действует Плащ отречения а заклинание выше 2го уровня]
!!FU&y1=21/y10>1:E;                   [выход, если бой на проклятой земле а заклинание выше 1го уровня]
!!HE-10:A2/93/d/?y19;                 [y19/y20 - Сфора уязвимости]
!!HE-20:A2/93/d/?y20;                 [...]
!!VRy19|y19>0/y20>0:S1;               [y19=1, если действует Сфера уязвимости]
** поиск цели
!!COx5:P4/?y21;                       [y21 - сила командира]
!!COx6:P6/?y23;                       [y23 - сопротивление магии вражеского командира]
!!VRv1:S-1;                           [v1 - количество подходящих целей]
!!DO9051/0/20/1&x3=0/y14>0:P-1/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22; [ поиск в v1 количества подходящих целей]
!!DO9051/0/20/1&x3=1/y14=0:P-1/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22; [...]
!!DO9051/21/41/1&x3=1/y14>0:P-1/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22;[...]
!!DO9051/21/41/1&x3=0/y14=0:P-1/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22;[...]
!!if&v1<0:;                           [если подходящих целей не найдено]
  !!VRx1:S-1;                         [возврвщвем -1]
  !!FU:E;                             [и выходим]
!!en:;
!!VRy20:S0 Rv1;                       [y21 - случайный номер отряда из подходящих]
!!VRv1:S-1;                           [инициализация v1]
!!DO9051/0/20/1&x3=0/y14>0:Py20/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22; [Выбор в v1 номера отряда подходящей цели]
!!DO9051/0/20/1&x3=1/y14=0:Py20/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22; [...]
!!DO9051/21/41/1&x3=1/y14>0:Py20/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22;[...]
!!DO9051/21/41/1&x3=0/y14=0:Py20/x2/y10/y3/y4/y5/y6/y7/y19/y13/y21/x5/y23/y22;[...]
!!VRx1:Sv1;                           [возврат номера отряда выбранного из подходящих]

!?FU9051;                             [подсчет в v1 количества (x1=-1) или x1-го подходящего отряда]
** x1 -1 для подсчета полдходящих отрядов, иначе порядковый номер подходящего отряда для возврата
** x2 - заклинание
** x3 - уровень заклинания
** x4 >0 для школы Воздуха
** x5 >0 для школы Воды
** x6 >0 для школы Огня
** x7 >0 для школы Земли
** x8 - уровень Школы
** x9 = начилие Сферы уязвимости (0/1)
** x10 >0 для заклинаний Разума
** x11 - Сила командира
** x12 - номер командира
** x13 - сопротивление вражеского командира
** x14 >0 для заклинаний урона
!!BMx16:T?y1 N?y2;                    [y1/y2 - тип/кол-во существ в отряде]
!!FU|y1<0/y2<1:E;                     [выход, если отряда нет]
!!BMx16:G34/?y2/?y3;                  [y2/y3 - продолжительность/сила антимагии]
!!VRy2&y2>0:Sy3 +3;                   [y2 = 3..6 - минимальный уровень заклинания преодолеющий Антимагию]
!!FU&x3<y2:E;                         [выход, если заклиание не преодолеет антимагию]
** невосприимчивость Палатки и Подводы
!!if|y1=147/y1=148:;
  !!FU|x2=43/x2=56/x2=53/x2=55/x2=41/x2=42/x2=62/x2=65/x2=60/x2=48:E;
!!en:;
** Лечение действует только на раненные отряды или отряды со снимаемыми дебафами
!!if&x2=37:;
  !!BMx16:L?y20 G42/?y21/d G45/?y22/d G50/?y23/d G52/?y24/d G54/?y25/d G59/?y26/d G60/?y27/d G61/?y28/d G62/?y29/d G70/?y30/d G71/?y31/d G72/?y32/d G73/?y33/d G74/?y34/d;
  !!FU&y20=0/y21=0/y22=0/y23=0/y24=0/y25=0/y26=0/y27=0/y28=0/y29=0/y30=0/y31=0/y32=0/y33=0/y34=0:E; [пропуск не раненного отряда без снимаемых дебафов для заклинания Лечение]
!!en:;
** иммунитеты, не зависящие от Сферы уязвимости
!!BMx16:F?y2;                         [y2 - флаги отряда]
!!VRy3:Sy2 &4;                        [y3 - стрелок]
!!VRy4:Sy2 &16;                       [y4 - живое существо]
!!VRy5:Sy2 &1024;                     [y5 - иммун к Разуму]
!!VRy6:Sy2 &262144;                   [y6 - нежить]
!!FU&y3=0/x2=64:E;                    [перезарядка действует только на стрелков]
!!FU&y3=0/x2=44:E;                    [точность действует только на стрелков]
!!FU&y3=0/x2=61:E;                    [забывчивость действует только на стрелков]
!!FU&y4=0/x2=41:E;                    [благословение действует только на живых]
!!FU&y4=0/x2=38:E;                    [воскрешение действует только на живых]
!!FU&y6=0/x2=39:E;                    [оживление мертвецов действует только на нежить]
!!SSx2:F?y7;                          [y7 - флаги заклинания]
!!VRy8:Sy7 &4096;                     [y8>0 если заклинание не действует на БМ]
!!FU&y1>=145/y1<=148/y8>0:E;          [выход, если БМ иммунна к заклинанию]
!!VRy8:Sy7 &32;                       [y8>0 если заклинание действует только на стрелков]
!!FU&y8>0/y3=0:E;                     [выход, если заклинание стрелка накладывается не на стрелка]
!!if|x16=v9996/x16=v9997:;            [если цель вражеский командир...]
  !!FU&x13>=100/x14>0:E;              [... и у него 100%, то на него не действует магия урона]
!!en:;
** иммунитет к уровням и школам заклинаний, если нет Сферы уязвимости
!!if&x9=0:;
  !!FU&y5>0/x10>0:E;                  [выход, если заклинание разума применяется к иммунному отряду]
  !!FU&y1=26/x3<4:E;                  [Зеленый дракон, иммун 1-3 ур.]
  !!FU&y1=27/x3<5:E;                  [Золотой дракон, иммун 1-4 ур.]
  !!FU&y1>=70/y1<=71/x2=62:E;         [Троглодиты, иммун к Слепоте.]
  !!FU&y1=82/x3<4:E;                  [Красный дракон, иммун 1-3 ур.]
  !!FU|y1=83/y1=155/y1=151/y1=121:E;  [Черный, Темный, Алмазный драконы, Магический элем, иммун ко всей магии]
  !!FU&y1>=52/y1<=53/x6>0:E;          [Эфриты, иммун к Огню]
  !!FU&y1=112/x2=23:E;                [Воздушный элем, иммун Шквал метеоритов]
  !!FU&y1=127/x2=23:E;                [Штормовой элем, иммун Шквал метеоритов]
  !!FU&y1=115/x2=16:E;                [Водяной элем, иммун лед.молния]
  !!FU&y1=123/x2=16:E;                [Ледяной элем, иммун лед.молния]
  !!FU&y1=115/x2=20:E;                [Водяной элем, иммун кольцо льда]
  !!FU&y1=123/x2=20:E;                [Ледяной элем, иммун кольцо льда]
  !!FU&y1=114/x6>0:E;                 [Огненный элем, иммун к Огню]
  !!FU&y1=129/x6>0:E;                 [Энертетический элем, иммун к Огню]
  !!FU&y1=113/x2=17:E;                [Земляной элем, иммун Молния]
  !!FU&y1=125/x2=17:E;                [Магматический элем, иммун Молния]
  !!FU&y1=113/x2=19:E;                [Земляной элем, иммун  Цепь молний]
  !!FU&y1=125/x2=19:E;                [Магматический элем, иммун Цепь молний]
  !!FU&y1=113/x2=26:E;                [Земляной элем, иммун Армагеддон]
  !!FU&y1=125/x2=26:E;                [Магматический элем, Армагеддон]
  !!FU&y1>=130/y1<=131/x6>0:E;        [Огн. птицы,Фениксы, иммун к Огню]
  !!FU&y1=158/x6>0:E;                 [Духи Погрубального Костра, иммун к Огню]
  !!FU&y1=132/x3<4:E;                 [Лазурный дракон, иммун 1-3 ур.]
  !!FU&y1=166/x4>0:E;                 [Возд. посланник, иммун к Воздуху]
  !!FU&y1=167/x5>0:E;                 [Водн. посланник, иммун к Воде]
  !!FU&y1=164/x6>0:E;                 [Огн. посланник, иммун к Огню]
  !!FU&y1=165/x7>0:E;                 [Земл. посланник, иммун к Земле]
!!en:;
**
!!if&x2=60:;                          [анализ здоровья цели для заклинания Гипноз]
  !!BMx16:N?y11 H?y12 L?y13;          [y13 - здоровье цели]
  !!VRy11:*y12 -y13;                  [...]
  !!SS60:P?y12 Ex8/?y13;              [y12 - сила Гипноза]
  !!VRy12:*x11 +y13;                  [...]
  !!FU&y13>y12:E;                     [выход, если сила Гипноза недостаточна]
!!en:;
!!if|x2=38/x2=39:;                    [поиск мертвых существ для Воскрешения/Поднятия нежити]
  !!BMx16:B?y11 N?y12;                [y11/y12 - макс/текущая численность отряда]
  !!FU&y12>=y11:E;                    [выход, если в отряде нет убитых существ]
!!en:;
!!if&x2=65:;                          [проверка возможности клонирования]
  !!BMx16:U5/?y11 L?y12 T?y13 F?y15;  [y11/y12/y13/y15 - наличие клона/уровень/тип/флаги отряда]
  !!VRy15:&8388608;                   [y15>0 для клона]
  !!SS65:Ex8/?y14;                    [y14 - макс. уровень для клонирования]
  !!FU|y12>y14/y11>=0/y15>0:E;        [выход, если у отряда слишком большой уровень, уже есть клон или он сам является клоном]
  !!FU&y13>=174/y13<=191:E;           [нельзя клонировать командира]
!!en:;
!!if&x2=64:;                          [проверка боезапаса у цели Перезарядки]
  !!BMx16:U3/?y11 T?y12;              [y11/y12 - боезапас/тип существа]
  !!MA:Ny12/?y13;                     [y13 - макс.боезапас]
  !!if&y12>=174/y12<=191:;            [если цель - командир]
    !!COx12:B1/4/?y14;                [y14 - навык Стрельба]
    !!VRy13:S0;                       [по умолчанию макс. боезапас командира нулевой]
    !!VRy13&y14=1:S24;                [но при наличии навыка стрельбы - 24]
  !!en:;
  !!FU&y11>=y13:E;                    [выход, если отряд имеет полный боезапас]
!!en:;
**
!!VRv1:+1;                            [увеличение счетчика подходящих целей]
!!if&x1>=0/x1=v1:;                    [если искомый отряд найден]
  !!VRv1:Sx16;                        [возврат в v1 номер отряда]
  !!VRx16:S100500;                    [прерывание цикла]
!!en:;

!?BA52;                               [перед боем]
!!SN:W^Cmndr0AR^/-1 W^Cmndr1AR^/-1;   [сброс раундов, в которых командиры уже совершали действие]

!?BG0;                                [перед действием]
!!BG:N?y1 A?y2;                       [y1/y2 - отряд/действие]
!!FU&y1<>v9996/y1<>v9997:E;           [выход, если ходит не командир]
!!VRy3:S0; !!VRy3&y1>20:S1;           [y3 - сторона]
!!SN:W^Cmndr%Y3AR^/?y4;               [y4 - раунд, в котором командир уже совершил действие]
!!BG&y4=v997:A12;                     [отмена действия, если командир уже ходил в этом раунде]
!!SN|y2=2/y2=3/y2=6/y2=7:W^Cmndr%Y3AR^/v997; [фиксация действия командира ([ходьба/защита/атака/стрельба)]

** end
