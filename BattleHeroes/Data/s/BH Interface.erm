ZVSE
ERMS_ScriptDate=2.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : Battle Heroes: dialogs


*** Показ диалога-подсказки

!?FU9030;                             [Показ диалога x1, пока зажата клавиша мыши (левая или правая)]
!!VRy1:Sv1;
!!SN:E7510739/1/x1;                   [find HDlg -> v1]
!!VRy2:Sv1;                           [v2 = HDlg]
!!if&y2<>0:;
  !!SN:E5864096/1;                    [stop animation]
  !!UN:Cy2/4/?v1;                     [v1 = dialog]
  !!SN:E6245264/2/v1;                 [show while holding mouse button]
  !!SN:E5864272/1;                    [continue animation]
  !!SN:E7518240/2/y2;                 [destroy HDlg]
!!en:;
!!VRv1:Sy1;
!_!BU:R;                               [обновление поля боя (вылет?)]

*** Подсказка командира DL70

!?CM4;                                [клик на поле боя]
!!CM:D?y1 F?y2;                       [y1 - позиция поля, y2 - параметры клика]
!!FU|y1<0/y1>186/y2<>512:E;           [выход, если не правый клик не на поле]
!!BU:Ey1/?y2;                         [y2 - номер живого отряда в позиции]
!!FU&y2=-1:E;                         [выход, если нет отряда]
!!FU&y2<>v9996/y2<>v9997:E;           [выход, если клик не на командира]
!!CM:R0;                              [отмена стандартной реакции на клик]
!!FU9096:Py2;                         [отображение диалога командира]

!?FU9096;                             [отображение диалога командира (x1 - номер отряда командира)]
!!VRy2:Sx1;                           [...]
!!DL70:N^cstat.txt^;                  [подгрузка 70 диалога (статус командира)]
!!if&-1:;                             [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                             [и выходим]
!!en:;
!!VRy1&y2=v9996:Sv9998;               [y1 - Номер командира]
!!VRy1&y2=v9997:Sv9999;               [...]
!!COy1:N?z1 ;                         [z1 - имя командира]
!!DL70:A1/3/z1/1;                     [установка имени в диалог]
!!DO9029/0/9/1:Py1/y2;                [установка иконок навыков в диалог]
!!BMy2:T?y3 A?y11 D?y12 U3/?y13 U1/?y14 U2/?y15 H?y16 L?y17 S?y18 G213/?y20/d F?y4; [параметры отряда командира]
!!COy1:P6/?y21 P4/?y22;               [y21/y22 - сопротивление/сила командира]
!!VRy17:-y16 *-1;                     [y17 - текущее здоровье командира]
!!MA:Ny3/?y23;                        [y23 - макс. боезапас]
!!VRy4:&4;                            [y4=0, если командир не может стрелять]
!!VRz11:S^%Y11^;                      [z10-z19 - строки параметров для диалога]
!!VRz12:S^%Y12^;                      [...]
!!VRz13:S^%Y13 / %Y23^;               [...]
!!VRz13&y4=0:S^-^;                    [...]
!!VRz14:S^%Y14 - %Y15^;               [...]
!!VRz14&y14=y15:S^%Y14^;              [...]
!!VRz15:S^%Y16^;                      [...]
!!VRz16:S^%Y18^;                      [...]
!!VRz17:S^%Y21%%^;                    [...]
!!VRy30&y2=v9996:Sv9964;              [y30=1, если есть способность Повышенная регенерация]
!!VRy30&y2=v9997:Sv9965;              [...]
!!VRy30&y30=1:Sy16 :50;               [y36 - регенерация командира]
!!VRy30&y30=0:Sy16 :100;              [...]
!!VRz18:S^%Y17  (+%Y30)^;             [z1 - строка регенерации для диалога]
!!VRz19:S^%Y22^;                      [...]
!!VRz10:S^%Y20^;                      [...]
!!DL70:A30/3/z11/1 A31/3/z12/1 A32/3/z13/1 A33/3/z14/1 A34/3/z15/1 A35/3/z16/1 A36/3/z17/1 A2/3/z18/1 A38/3/z19/1 A39/3/z10/1; [установка параметров в диалог]
!!VRz11:S^csp00.pcx^;                 [иконка эффекта по умолчанию]
!!DL70:A40/11/z11/1 A41/11/z11/1 A42/11/z11/1 A43/11/z11/1 A44/11/z11/1; A45/11/z11/1 A46/11/z11/1 A47/11/z11/1; [сброс картинок эффектов]
!!VRz11:S^^;                          [длительность эффекта по умолчанию]
!!DL70:A50/3/z11/1 A51/3/z11/1 A52/3/z11/1 A53/3/z11/1 A54/3/z11/1; A55/3/z11/1 A56/3/z11/1 A57/3/z11/1; [сброс длительностей эффектов]
!!VRv1:S40;                           [инизиализация v1 - место для отображения эффекта]
!!DO9031/0/75/1:Py2;                  [установка иконок и длительностей эффектов в диалог]
!!VRy1&y2=v9996:S0;                   [y1 - сторона]
!!VRy1&y2=v9997:S1;                   [...]
!!SN:W^Cmndr%Y1Sp0^/?y10 W^Cmndr%Y1Sp1^/?y11 W^Cmndr%Y1Sp2^/?y12 W^Cmndr%Y1Sp3^/?y13 W^Cmndr%Y1Sp4^/?y14; [получение магии командира]
!!SN:W^Cmndr%Y1Sp5^/?y15 W^Cmndr%Y1Sp6^/?y16 W^Cmndr%Y1Sp7^/?y17 W^Cmndr%Y1Sp8^/?y18 W^Cmndr%Y1Sp9^/?y19; [...]
!!VRz11:S^cm%Y10.pcx^;                [формирование имен файлов]
!!VRz12:S^cm%Y11.pcx^;                [...]
!!VRz13:S^cm%Y12.pcx^;                [...]
!!VRz14:S^cm%Y13.pcx^;                [...]
!!VRz15:S^cm%Y14.pcx^;                [...]
!!VRz16:S^cm%Y15.pcx^;                [...]
!!VRz17:S^cm%Y16.pcx^;                [...]
!!VRz18:S^cm%Y17.pcx^;                [...]
!!VRz19:S^cm%Y18.pcx^;                [...]
!!VRz10:S^cm%Y19.pcx^;                [...]
!!DL70:A60/11/z11/1 A61/11/z12/1 A62/11/z13/1 A63/11/z14/1 A64/11/z15/1 A65/11/z16/1 A66/11/z17/1 A67/11/z18/1 A68/11/z19/1 A69/11/z10/1; [установка картинок в диалог]
!!BMy2:T?y11;                         [y11 - тип командира]
!!VRz11:S^card_c%Y11.pcx^;            [...]
!!DL70:A9/11/z11/1;                   [установка портрета командира в диалог]
!!UN:C4199496/2/?i C4199503/2/?j;     [i,j - размер экрана]
!!VRk:S-179 +i;                       [...]
!!VRi&x1<=20:-806 :2 +2;              [i,j - смещения диалога]
!!VRi&x1<=20/i<0:S0;                  [...]
!!VRi&x1>=21:-806 :2 +806 -180 -2;    [...]
!!VRi&x1>=21/i>k:Sk;                  [...]
!!VRj:-630 :2 +3;                     [...]
!!VRj&j<0:S0;                         [...]
!!FU88000:P70/i/j;                    [изменение координат диалога]
!!FU9030:P70;                         [отображение диалога]

!?FU9029;                             [x1 - номер командира, x2 - номер отряда командира, x16 - способность]
!!VRy1:Sx16;                          [y1 - способность]
!!VRy1&x16=1:S11;                     [...]
!!COx1:B1/y1/?y2;                     [y2 = 0/1 нет/есть способность]
!!VRy2&y1=11/x2=v9996:Sv9964;         [...]
!!VRy2&y1=11/x2=v9997:Sv9965;         [...]
!!if&y1=8:;                           [проверка способности «Выносливость»]
  !!VRy98:Sx2 :21;                    [y98 - сторона командира]
  !!SN:W^BH.Commander%Y98.Hardiness^/?y2; [y2 - есть ли способность]
!!en:;
!!VRz11:S^css0%X16%Y2.pcx^;           [z1 - название файла с иконкой навыка]
!!VRy3:Sx16 +10;                      [y3 - номер элемента в диалоге]
!!DL70:Ay3/11/z11/1;                  [установка иконки в диалог]

!?FU9031;                             [x1 - отряд, x2 - номер диалога, x16 - эффект]
!!BMx1:Gx16/?y1/d;                    [y1 - продолжительность эффекта x16]
!!FU|y1<1/y1>99:E;                    [выход, если эффекта нет или эффект постоянный (огн. щит)]
!!BMx1:G32/?y3/d;                     [y1 - продолжительность эффекта защиты от стихий]
!!FU&x16=30/y1=y3:E;                  [не отображаем компоненты защиты от стихий]
!!FU&x16=31/y1=y3:E;                  [не отображаем компоненты защиты от стихий]
!!FU&x16=33/y1=y3:E;                  [не отображаем компоненты защиты от стихий]
!!VRy2:Sv1 +10;                       [y2 - номер элемента "длительность эффекта"]
!!VRz11:S^csp%X16.pcx^;               [z1 - иконка эффекта]
!!VRz12:S^х %Y1^;                     [z2 - текст эффекта]
!!DL70:Av1/11/z11/1 Ay2/3/z12/1;      [установка иконки и длительности эффекта в диалог]
!!VRv1:+1;                            [инкремент v1]
!!VRx16&v1>44:S100;                   [прерываем цикл, если места для отображения эффектов закончились]

*** Подсказка героя DL71

!?CM4;                                [клик на поле боя]
!!CM:D?y1 F?y2;                       [y1 - позиция поля, y2 - параметры клика]
!!FU|y1<252/y1>253/y2<>512:E;         [Выход, если не ПКМ на герое]
!!VRy1&y1=252:S-10;                   [y1 - Номер героя]
!!VRy1&y1=253:S-20;                   [...]
!!FU9095:Py1;                         [отображение диалога героя]

!?FU9095;                             [отображение диалога героя (x1 - номер героя -10/-20)]
!!VRy1:Sx1;                           [...]
!!HEy1:B0/?z11 N?i O?j;               [z11/i/j - имя/номер/владелец героя]
!!OW:Ij/?y2;                          [y2=1 для ИИ]
!!FU&v7909<>0/v7909<>2/y2=1:E;        [выход, если просмотр состояния ИИ героя запрещен]
!!CM:R0;                              [отмена стандартной реакции на клик]
!!DL71:N^hstat.txt^;                  [подгрузка 71 диалога (статус героя)]
!!if&-1:;                             [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                             [и выходим]
!!en:;
!!DL71:A30/3/z11/1;                   [установка имени в диалог]
!!VRz11:S^card_h%Vi.pcx^;             [названеи портрета героя]
!!DL71:A80/11/z11/1;                  [установка портрета в диалог]
!!DO9028/1/27/1:Py1;                  [установка иконок навыков в диалог]
!!HEy1:F?y11/?y12/?y13/?y14 I?y18/1;  [параметры героя]
!!FU9047:Py1/?y19;                    [y19 - регенерация маны героя]
!!VRy20:Sy14 *10 +10;                 [y20 - макс. мана героя.]
!!VRz11:S^%Y11^;                      [z1-z5 - строки параметров для диалога]
!!VRz12:S^%Y12^;                      [...]
!!VRz13:S^%Y13^;                      [...]
!!VRz14:S^%Y14^;                      [...]
!!VRz15:S^%Y18 / %Y20 (+%Y19)^;       [...]
!!DL71:A41/3/z11/1 A42/3/z12/1 A43/3/z13/1 A44/3/z14/1 A36/3/z15/1; [установка параметров в диалог]
** информация об отстройке зданий в городах
!!HEy1:B2/?y2;                        [y2 - класс героя]
!!VRy2::2;                            [y2 - родной город героя]
!!VRy3|y2=0/y2=1/y2=2:S0;             [y3 - 1-й город для класса]
!!VRy3|y2=3/y2=4/y2=5:S3;             [y3 - 1-й город для класса]
!!VRy3|y2=6/y2=7/y2=8:S6;             [y3 - 1-й город для класса]
!!VRy4&y1=-10:Sv9980 -1 :2;           [y4 - уровень отстроенного здания в 1-м городе]
!!VRy5&y1=-10:Sv9980 -1 %2;           [y5 - грейд отстроенного здания в 1-м городе]
!!VRy4&y1=-20:Sv9983 -1 :2;           [...]
!!VRy5&y1=-20:Sv9983 -1 %2;           [...]
!!UN:Ty3/y4/y5/?y6;                   [y6 - тип существа]
!!VRz12:S^card_c%Y6.pcx^;             [z12 - картинка 1-го существа]
!!DL71:A82/11/z12/1;                  [установка картинок 1-го города]
!!VRy3|y2=0/y2=1/y2=2:S1;             [y3 - 2-й город для класса]
!!VRy3|y2=3/y2=4/y2=5:S4;             [y3 - 2-й город для класса]
!!VRy3|y2=6/y2=7/y2=8:S7;             [y3 - 2-й город для класса]
!!VRy4&y1=-10:Sv9981 -1 :2;           [y4 - уровень отстроенного здания в 2-м городе]
!!VRy5&y1=-10:Sv9981 -1 %2;           [y5 - грейд отстроенного здания в 2-м городе]
!!VRy4&y1=-20:Sv9984 -1 :2;           [...]
!!VRy5&y1=-20:Sv9984 -1 %2;           [...]
!!UN:Ty3/y4/y5/?y6;                   [y6 - тип существа]
!!VRz12:S^card_c%Y6.pcx^;             [z12 - картинка 2-го существа]
!!DL71:A84/11/z12/1;                  [установка картинок 2-го города]
!!VRy3|y2=0/y2=1/y2=2:S2;             [y3 - 3-й город для класса]
!!VRy3|y2=3/y2=4/y2=5:S5;             [y3 - 3-й город для класса]
!!VRy3|y2=6/y2=7/y2=8:S8;             [y3 - 3-й город для класса]
!!VRy4&y1=-10:Sv9982 -1 :2;           [y4 - уровень отстроенного здания в 3-м городе]
!!VRy5&y1=-10:Sv9982 -1 %2;           [y5 - грейд отстроенного здания в 3-м городе]
!!VRy4&y1=-20:Sv9985 -1 :2;           [...]
!!VRy5&y1=-20:Sv9985 -1 %2;           [...]
!!UN:Ty3/y4/y5/?y6;                   [y6 - тип существа]
!!VRz12:S^card_c%Y6.pcx^;             [z12 - картинка 3-го существа]
!!DL71:A86/11/z12/1;                  [установка картинок 3-го города]
** Информация о городах и строениях
** y3 - 3й город, x1 - герой -10/-20
!!VRx2:Sx1 :-20;                      [x2 - сторона 0/1]
!!VRy3|y2=0/y2=1/y2=2:S0;             [y3 - 1-й город для класса]
!!VRy3|y2=3/y2=4/y2=5:S3;             [y3 - 1-й город для класса]
!!VRy3|y2=6/y2=7/y2=8:S6;             [y3 - 1-й город для класса]
!_!VRz12:S^card_b%Y3.pcx^;             [z12 - картинка города]
!_!DL71:A132/11/z12/1;                 [установка картинки 3-го города]
!!SN:W^b%X2.%Y3.0^/?y8 W^b%X2.%Y3.1^/?y9 W^b%X2.%Y3.2^/?y11; [y8/y9/y11 - 1/2/3 строения]
!!VRy10:S0;                           [1 здание]
!!VRz12&y8=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
!!VRz12&y8=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
!!DL71:A142/11/z12/1;                 [установка картинки 1го здания 1-го города]
!!VRy10:S1;                           [2 здание]
!!VRz12&y9=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
!!VRz12&y9=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
!!DL71:A152/11/z12/1;                 [установка картини 2го здания 1-го города]
!!VRy10:S2;                           [2 здание]
!!VRz12&y11=0:S^bn%Y3%Y10.pcx^;       [z12 - картинка города]
!!VRz12&y11>0:S^b%Y3%Y10.pcx^;        [z12 - картинка города]
!!DL71:A162/11/z12/1;                 [установка картини 3го здания 1-го города]
!!VRy11:-1;                           [перезарядка Грааля]
!!VRz11:S^^;                          [...]
!!VRz11&y11>0:S^%Y11^;                [...]
!!DL71:A167/3/z11/1;                  [установка времени перезарядки Грааля в диалог]
**
!!VRy3|y2=0/y2=1/y2=2:S1;             [y3 - 2-й город для класса]
!!VRy3|y2=3/y2=4/y2=5:S4;             [y3 - 2-й город для класса]
!!VRy3|y2=6/y2=7/y2=8:S7;             [y3 - 2-й город для класса]
!_!VRz12:S^card_b%Y3.pcx^;             [z12 - картинка города]
!_!DL71:A134/11/z12/1;                 [установка картинки 3-го города]
!!SN:W^b%X2.%Y3.0^/?y8 W^b%X2.%Y3.1^/?y9 W^b%X2.%Y3.2^/?y11; [y8/y9/y11 - 1/2/3 строения]
!!VRy10:S0;                           [1 здание]
!!VRz12&y8=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
!!VRz12&y8=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
!!DL71:A144/11/z12/1;                 [установка картинки 1го здания 2-го города]
!!VRy10:S1;                           [2 здание]
!!VRz12&y9=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
!!VRz12&y9=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
!!DL71:A154/11/z12/1;                 [установка картини 2го здания 2-го города]
!!VRy10:S2;                           [2 здание]
!!VRz12&y11=0:S^bn%Y3%Y10.pcx^;       [z12 - картинка города]
!!VRz12&y11>0:S^b%Y3%Y10.pcx^;        [z12 - картинка города]
!!DL71:A164/11/z12/1;                 [установка картини 3го здания 2-го города]
!!VRy11:-1;                           [перезарядка Грааля]
!!VRz11:S^^;                          [...]
!!VRz11&y11>0:S^%Y11^;                [...]
!!DL71:A168/3/z11/1;                  [установка времени перезарядки Грааля в диалог]
**
!!VRy3|y2=0/y2=1/y2=2:S2;             [y3 - 3-й город для класса]
!!VRy3|y2=3/y2=4/y2=5:S5;             [y3 - 3-й город для класса]
!!VRy3|y2=6/y2=7/y2=8:S8;             [y3 - 3-й город для класса]
!_!VRz12:S^card_b%Y3.pcx^;             [z12 - картинка города]
!_!DL71:A136/11/z12/1;                 [установка картинки 1-го города]
!!SN:W^b%X2.%Y3.0^/?y8 W^b%X2.%Y3.1^/?y9 W^b%X2.%Y3.2^/?y11; [y8/y9/y11 - 1/2/3 строения]
!!VRy10:S0;                           [1 здание]
!!VRz12&y8=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
!!VRz12&y8=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
!!DL71:A146/11/z12/1;                 [установка картинки 1го здания 3-го города]
!!VRy10:S1;                           [2 здание]
!!VRz12&y9=0:S^bn%Y3%Y10.pcx^;        [z12 - картинка города]
!!VRz12&y9=1:S^b%Y3%Y10.pcx^;         [z12 - картинка города]
!!DL71:A156/11/z12/1;                 [установка картини 2го здания 3-го города]
!!VRy10:S2;                           [2 здание]
!!VRz12&y11=0:S^bn%Y3%Y10.pcx^;       [z12 - картинка города]
!!VRz12&y11>0:S^b%Y3%Y10.pcx^;        [z12 - картинка города]
!!DL71:A166/11/z12/1;                 [установка картини 3го здания 3-го города]
!!VRy11:-1;                           [перезарядка Грааля]
!!VRz11:S^^;                          [...]
!!VRz11&y11>0:S^%Y11^;                [...]
!!DL71:A169/3/z11/1;                  [установка времени перезарядки Грааля в диалог]
** Расположение диалога
!!UN:C4199496/2/?i C4199503/2/?j;     [i,j - размер экрана]
!!VRk:S-184 +i;                       [...]
!!VRi&x1=-10:-806 :2 +2;              [i,j - смещения диалога]
!!VRi&x1=-10/i<0:S0;                  [...]
!!VRi&x1=-20:-806 :2 +806 -185 -2;    [...]
!!VRi&x1=-20/i>k:Sk;                  [...]
!!VRj:-630 :2 +3;                     [...]
!!VRj&j<0:S0;                         [...]
!!FU88000:P71/i/j;                    [изменение координат диалога]
!!FU9030:P71;                         [отображение диалога]

!?FU9028;                             [x1 - герой, x16 - втор.навык]
!!FU|x16=0/x16=20/x16=3/x16=27:E;     [выход, если навык не используется]
!!HEx1:Sx16/?y1;                      [y1  - уровень навыка]
!!VRy1&x1=-10/x16=12:Sv9970;          [для Некромантии актуальный уровень навыка в v-переменной]
!!VRy1&x1=-20/x16=12:Sv9971;          [...]
!!VRz11:S^hss%X16%Y1.pcx^;            [имя картинки]
!!VRy2:Sx1 :-10 -1;                   [y2 - сторона]
!!SN:W^spec_%Y2^/?j;                  [j - специализация]
!!VRz11&x16=5/j=1:S^card_hm.pcx^;     [Иконка специализации]
!!VRz11&x16=5/j=2:S^card_ps34.pcx^;   [Иконка специализации]
!!HEx1&x16=5/j=3:N?i;                 [i - номер героя]
!!VRz11&x16=5/j=3:S^card_h%Vi.pcx^;   [Иконка специализации]
!!VRz11&x16=5/j=4:S^card_bs.pcx^;     [Иконка специализации]
!!HEx1&x16=5/j=5:B2/?i;               [i - номер существа командира]
!!VRi&x16=5/j=5::6;                   [...]
!!VRi&i=1/x16=5/j=5:S4;               [...]
!!VRi&i=2/x16=5/j=5:S8;               [...]
!!VRi&x16=5/j=5:+174;                 [...]
!!VRz11&x16=5/j=5:S^card_c%Vi.pcx^;   [Иконка специализации]
!!VRz11&x16=5/j=6:S^card_ga.pcx^;     [Иконка специализации]
!!DL71:Ax16/11/z11/1;                 [установка картинки]
!!VRy2:Sx16 +50;                      [y2 - номер текста]
!!VRz12&y1=0:S^^;                     [z2 - обозначение уровня навыка]
!!VRz12&y1=1:Sz179908;                [...]
!!VRz12&y1=2:Sz179909;                [...]
!!VRz12&y1=3:Sz179910;                [...]
!!DL71:Ay2/3/z12/1;                   [установка обозначения уровня навыка]

*** отображение диалога выбора карты

!?FU9032;                             [v101-v124 - параметры 1-6 карты, z1-z6 - описания действий 1-6 карты, v125 - количество карт, 126 - номер героя, 127 - тип командира. Возврат выбранной карты в v1.]
!!DL72:N^cards_%V125.txt^;            [подгрузка 72 диалога (выбор карты)]
!!if&-1:;                             [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                             [и выходим]
!!en:;
!!VRy1:Sv997 +1;                      [y1 - номер раунда]
!!VRz10:Sz179911;                     [z10 - текст "номер раунда"]
!!DL72:A100/3/z10/1;                  [установка номера раунда]
!!HE-10:N?y2;                         [y2 - номер атакующего героя]
!!VRz9:S^card_h%Y2.pcx^;              [z9 - иконка героя]
!!HEv126:O?y1;                        [y1 - номер игрока]
!!VRz10:S^flag_%Y1.pcx^;              [z10 - иконка флага]
!!BMv9996:T?y3;                       [y3 - тип атакующего командира]
!!VRz11:S^card_c%Y3.pcx^;             [z11 - иконка командира]
!!DL72:A70/11/z9/1 A71/11/z10/1 A72/11/z11/1; [установка иконок героя, флага и командира в диалог]
!!HE-20:N?y2;                         [y2 - номер защищающегося героя]
!!VRz9:S^card_h%Y2.pcx^;              [z9 - иконка героя]
!!BMv9997:T?y3;                       [y3 - тип защищающегося командира]
!!VRz11:S^card_c%Y3.pcx^;             [z11 - иконка командира]
!!DL72:A80/11/z9/1 A82/11/z11/1; [установка иконок героя, флага и командира в диалог]
**установка информации о мане и здоровье в диалог
!!HEv9998:I?y11/1;                    [...]
!!VRz11:S^{~a}%Y11{~}^;               [...]
!!BMv9996:H?y12 L?y13;                [...]
!!VRy12:-y13;                         [...]
!!VRz12:S^{~r}%Y12{~}^;               [...]
!!BMv9997:H?y14 L?y15;                [...]
!!VRy14:-y15;                         [...]
!!VRz13:S^{~r}%Y14{~}^;               [...]
!!HEv9999:I?y16/1;                    [...]
!!VRz14:S^{~a}%Y16{~}^;               [...]
!!DL72:A87/3/z11/1 A88/3/z12/1 A89/3/z13/1 A90/3/z14/1; [установка информации о мане и здоровье]
**
!!DO9033/1/v125/1:P;                  ["отрисовка" карт]
!!VRv1:C0/0;                          [инициализация v1/v2]
!!UN:C4199496/2/?i C4199503/2/?j;     [i,j - размер экрана]
!!VRk:S-600 +j;                       [...]
!!VRj:-630 :2 +26;                    [...]
!!VRj&j>k:Sk;                         [...]
!!VRj&j<0:S0;                         [...]
!!FU88000:P72/-1/j;                   [изменение координат диалога]
!!DL72:S;                             [отображение диалога 72]

!?FU9033;                             ["отрисовка" карты x16]
!!VRy1:Sx16 -1 *4 +101 Svy1;          [y1-y4 - параметры карты]
!!VRy2:Sx16 -1 *4 +102 Svy2;          [...]
!!VRy3:Sx16 -1 *4 +103 Svy3;          [...]
!!VRy4:Sx16 -1 *4 +104 Svy4;          [...]
!!VRy5:Sx16 +2;                       [y5 - индекс подложки карты]
!!VRz9:S^crd_bg_0.pcx^;               [z9 - подложка карты]
!!DL72:Ay5/11/z9/1;                   [установка подложки карты]
!!VRy5:Sx16 +90;                      [y5 - индекс бордюра карты]
!!HEv126:O?y6;                        [y6 - цвет героя]
!!VRz9:S^crd_brd_%Y6.pcx^;            [z9 - бордюр активной карты]
!!DL72:Ay5/11/z9/1;                   [установка бордюра карты]
!!VRz12:S^^;                          [обнуление строк описания карты]
!!VRz13:S^^;                          [...]
!!VRz14:S^^;                          [...]
!!VRz15:S^^;                          [...]
!!VRh:Sx16 +20;                       [h - индекс подсказки]
!!VRzh:Szx16;                         [подсказка по умолчанию - описание действия]
*** призыв и призывные действия гильдий
!!if&y1=1:;                           [...]
  !!VRz9:S^card_hm.pcx^;              [z9/z10,z12-z17 - иконка типа/подтипа, тексты действия]
  !!VRz10:S^card_c%Y2.pcx^;           [...]
  !!UN&y3=1:N3/?z11/y2/0;             [...]
  !!UN&y3>1:N3/?z11/y2/1;             [...]
  !!VRy11:Sy3;                        [...]
  !!VRz12:S179801;                    [...]
  !!VRz78:M2/z11/0;                   [...]
  !!VRz79:M2/z11/1;                   [...]
  !!VRz80:M2/z11/2;                   [...]
  !!VRz13:S^{~Magenta}%Y11 %Z78{~}^;  [...]
  !!VRz14:S^{~Magenta}%Z79 %Z80{~}^;  [...]
  !!VRzh:S179871;                     [подсказка]
  !!if|y2=140/y2=142/y2=143/y2=144/y2=168/y2=194/y2=195:; [Для Гильдии Головорезов]
    !!VRz9:S^card_gh.pcx^;            [...]
    !!VRz12:S179809;                  [...]
    !!VRy11:Sy4;                      [...]
    !!VRz15:S179847;                  [...]
    !!VRzh:S179872;                   [подсказка]
  !!en:;
  !!if|y2=137/y2=138/y2=170/y2=171/y2=192:; [Для Гильдии Стрелков]
    !!VRz9:S^card_gs.pcx^;            [...]
    !!VRz12:S179894;                  [...]
    !!VRy11:Sy4;                      [...]
    !!VRz15:S179847;                  [...]
    !!VRzh:S179872;                   [подсказка]
  !!en:;
  !!if|y2=136/y2=193/y2=169:;         [Для Гильдии Чародеев]
    !!VRz9:S^card_ge.pcx^;            [...]
    !!VRz12:S179810;                  [...]
    !!VRz13:S179889;                  [...]
    !!VRz14:S179890;                  [...]
    !!VRy11:Sy4;                      [...]
    !!VRz15:S179847;                  [...]
    !!VRzh&y2=169:S179891;            [подсказка Фанатики Войны]
    !!VRzh&y2=193:S179892;            [подсказка Волшебницы]
    !!VRzh&y2=136:S179893;            [подсказка Чародеи]
  !!en:;
  !!if|y2=145/y2=146/y2=147/y2=148/y2=116/y2=117:; [Для Гильдии Кузнецов]
    !!VRz9:S^card_gb.pcx^;            [...]
    !!VRz12:S179808;                  [...]
    !!VRy11:Sy4;                      [...]
    !!VRz15:S179847;                  [...]
    !!VRzh|y2=116/y2=117:S179873;     [подсказка големов]
    !!VRzh|y2=145:S179882;            [подсказка Катапульты]
    !!VRzh|y2=146:S179879;            [подсказка Баллисты]
    !!VRzh|y2=147:S179880;            [подсказка Палатки]
    !!VRzh|y2=148:S179881;            [подсказка Тележки]
  !!en:;
!!en:;
*** изучение заклинаний
!!if&y1=2:;
  !!VRz9:S^card_h%V126.pcx^;          [...]
  !!VRz10:S^card_sp%Y2.pcx^;          [...]
  !!UN:N1/?z11/y2;                    [...]
  !!VRz12:S179802;                    [...]
  !!VRz13:S179836;                    [...]
  !!SSy2:S?y20;                       [y20 - биты школ магии]
  !!HEv126:S15/?y31 S14/?y32 S16/?y33 S17/?y34; [y31-y34 - уровни навыков школ героя]
  !!VRy21:Sy20 &1;                    [y30 - уровень максимаьно-развитой школы магии, которой принадлежит заклинание]
  !!VRy22:Sy20 &2;                    [...]
  !!VRy23:Sy20 &4;                    [...]
  !!VRy24:Sy20 &8;                    [...]
  !!VRy30:S0;                         [...]
  !!VRy30&y21>0/y30<y31:Sy31;         [...]
  !!VRy30&y22>0/y30<y32:Sy32;         [...]
  !!VRy30&y23>0/y30<y33:Sy33;         [...]
  !!VRy30&y24>0/y30<y34:Sy34;         [...]
  !!VRy30:*4;                         [х4]
  !!VRi:Sy2 *136 +8114880 +120 +y30;  [получение подсказки по заклинанию - описание текущего уровня Школы]
  !!UN:Ci/4/?j;                       [спасибо Berserker'у]
  !!SN:X?k Xj X?z77 Xk;               [...]
  !!VRzh:S^{%Z11}

%Z77^;                                [Формирование подсказки по заклинанию]
!!en:;
*** тренировка героя
!!if&y1=3:;
  !!VRz9:S^card_h%V126.pcx^;          [...]
  !!VRz10:S^card_ps%Y2.pcx^;          [...]
  !!VRz12:S179803;                    [...]
  !!VRy11:Sy3;                        [...]
  !!VRz13&y2=31:S179814;              [...]
  !!VRzh&y2=31:S179867;               [подсказка]
  !!VRz13&y2=32:S179815;              [...]
  !!VRzh&y2=32:S179868;               [подсказка]
  !!VRz13&y2=33:S179816;              [...]
  !!VRzh&y2=33:S179869;               [подсказка]
  !!VRz13&y2=34:S179817;              [...]
  !!VRzh&y2=34:S179870;               [подсказка]
!!en:;
*** обучение героя
!!if&y1=4:;
  !!VRz9:S^card_h%V126.pcx^;          [...]
  !!VRz10:S^card_ss%Y2%Y3.pcx^;       [...]
  !!UN:N4/?z11/y2;                    [...]
  !!VRz12:S179804;                    [...]
  !!VRz13:S^{~Lime}%Z11{~}^;
  !!VRz14&y3=1:S179818;               [...]
  !!VRz14&y3=2:S179819;               [...]
  !!VRz14&y3=3:S179820;               [...]
  !!VRi:Sy2 *2 +901;                  [i - индекс z-переменной с описание втор.навыка]
  !!VRz77:Szi;                        [z77 - описание втор.навыка]
  !!VRzh:S^{%Z11}

%Z77^;                                [подсказка для изучения втор.навыка - описание втор.навыка]
!!en:;
*** Постройка жилищ
!!if&y1=5:;
  !!if&y2=9:;                         [Спецстроения]
    !!VRy5:Sy3 :3;                    [y5 - город]
    !!VRy6:Sy3 %3;                    [y6 - строение]
    !!VRz9:S^card_b%Y5.pcx^;          [1я иконка карты]
    !!VRz10:S^b%Y5%Y6.pcx^;           [2я иконка карты]
    !!VRy7&y6<2:S179226 +y5 +y5 +y6;  [y7 - индекс ert-переменной с названием строения]
    !!VRy7&y6=2:S179298 +y5;          [y7 - индекс ert-переменной с названием строения]
    !!VRz11:Szy7;                     [z11 - название спецстроения]
    !!VRz12:S179244;                  [Тип карты "Постройка Здания"]
    !!VRz13:S^{~w}%Z11{~}^;           [1я строка карты]
    !!VRz14:S^^;                      [2я строка карты]
    !!VRy7&y6<2:S179245 +y5 +y5 +y6;  [y7 - индекс ert-переменной с описанием строения]
    !!VRy7&y6=2:S179307 +y5;          [y7 - индекс ert-переменной с описанием строения]
    !!VRzh:Szy7;                      [Подсказка карты]
  !!el:;
    !!VRz9:S^card_b%Y2.pcx^;          [...]
    !!VRy5:Sy3 -30 %7;                [y7 - тип существа в отстраиваемом жилище]
    !!VRy6&y3<37:S0;                  [...]
    !!VRy6&y3>36:S1;                  [...]
    !!UN:Ty2/y5/y6/?y7;               [...]
    !!VRz10:S^card_c%Y7.pcx^;         [...]
    !!UN:N2/?z11/y2/y3;               [...]
    !!VRz12:S179805;                  [...]
    !!VRz78:M2/z11/0;                 [...]
    !!VRz79:M2/z11/1;                 [...]
    !!VRz80:M2/z11/2;                 [...]
    !!VRz81:M2/z11/3;                 [...]
    !!VRz13:S^{~w}%Z78 %Z79{~}^;      [...]
    !!VRz14:S^{~w}%Z80 %Z81{~}^;      [...]
    !!UN:N2/?z31/y2/y3 N3/?z32/y7/1;  [формирование подсказки]
    !!VRzh:Sz179866;                  [...]
  !!en:;
!!en:;
*** Тренировка/обучение командира
!!if&y1=6:;
  !!VRz9:S^card_c%V127.pcx^;          [...]
  !!VRz10&y2<7:S^card_cp%Y2.pcx^;     [...]
  !!VRz12&y2<7:S179806;               [...]
  !!VRy11:Sy3;                        [...]
  !!VRz13&y2=0:S179814;               [...]
  !!VRzh&y2=0:S179859;                [...]
  !!VRz13&y2=1:S179815;               [...]
  !!VRzh&y2=1:S179860;                [...]
  !!VRz13&y2=2:S179821;               [...]
  !!VRzh&y2=2:S179861;                [...]
  !!VRz13&y2=3:S179823;               [...]
  !!VRzh&y2=3:S179863;                [...]
  !!VRz13&y2=4:S179824;               [...]
  !!VRzh&y2=4:S179864;                [...]
  !!VRz13&y2=5:S179822;               [...]
  !!VRzh&y2=5:S179862;                [...]
  !!VRz13&y2=6:S179825;               [...]
  !!VRzh&y2=6:S179865;                [...]
  !!if&y2=7:;
    !!VRz10:S^card_cs%Y3.pcx^;        [...]
    !!VRz12:S179807;                  [...]
    !!VRz13&y3=0:S179826;             [...]
    !!VRzh&y3=0:S179849;              [...]
    !!VRz13&y3=2:S179827;             [...]
    !!VRzh&y3=2:S179850;              [...]
    !!VRz13&y3=3:S179828;             [...]
    !!VRzh&y3=3:S179851;              [...]
    !!VRz13&y3=4:S179829;             [...]
    !!VRzh&y3=4:S179852;              [...]
    !!VRz13&y3=5:S179830;             [...]
    !!VRzh&y3=5:S179853;              [...]
    !!VRz13&y3=6:S179831;             [...]
    !!VRzh&y3=6:S179854;              [...]
    !!VRz13&y3=7:S179832;             [...]
    !!VRzh&y3=7:S179855;              [...]
    !!VRz13&y3=8:S179833;             [...]
    !!VRzh&y3=8:S179856;              [...]
    !!VRz13&y3=9:S179834;             [...]
    !!VRzh&y3=9:S179857;              [...]
    !!VRz13&y3=11:S179835;            [...]
    !!VRzh&y3=11:S179858;             [...]
  !!en:;
!!en:;
*** Помощь Гильдии Некромантов
!!if&y1=7:;
  !!VRz9:S^card_gn.pcx^;              [...]
  !!VRz10&y2=1:S^card_c196.pcx^;      [...]
  !!VRz10&y2=2:S^card_c59.pcx^;       [...]
  !!VRz10&y2=3:S^card_c159.pcx^;      [...]
  !!VRz10&y2=4:S^card_c141.pcx^;      [...]
  !!VRz10&y2=5:S^card_c172.pcx^;      [...]
  !!VRz12:S179811;                    [...]
  !!VRy11:Sy3;                        [...]
  !!VRz13&y2=1:S179839;               [...]
  !!VRz14&y2=1:S179840;               [...]
  !!VRzh&y2=1:S179875;                [подсказка]
  !!VRz13&y2=2:S179841;               [...]
  !!VRz14&y2=2:S179842;               [...]
  !!VRzh&y2=2:S179876;                [подсказка]
  !!VRz13&y2=3:S179843;               [...]
  !!VRz14&y2=3:S179844;               [...]
  !!VRzh&y2=3:S179877;                [подсказка]
  !!VRz13&y2=4:S179845;               [...]
  !!VRz14&y2=4:S179846;               [...]
  !!VRzh&y2=4:S179878;                [подсказка]
  !!VRz13&y2=5:S179883;               [...]
  !!VRz14&y2=5:S179840;               [...]
  !!VRzh&y2=5:S179884;                [подсказка]
  !!VRy11:Sy4;                        [...]
  !!VRz15:S179847;                    [...]
!!en:;
*** Помощь Гильдии Чародеев
!!if&y1=8:;
  !!VRz9:S^card_ge.pcx^;              [...]
  !!if&y2=1:;                         [Карты Судьбы]
    !!VRz10:S^card_fc.pcx^;           [...]
    !!VRz13:S179898;                  [...]
    !!VRz14:S179899;                  [...]
    !!VRzh:S179900;                   [подсказка]
  !!en:;
  !!if&y2=2:;                         [Перекачка маны]
    !!VRz10:S^card_md.pcx^;           [...]
    !!VRz13:S179895;                  [...]
    !!VRz14:S179896;                  [...]
    !!VRzh:S179897;                   [подсказка]
  !!en:;
  !!if&y2=3:;                         [СД]
    !!VRz10:S^card_c134.pcx^;         [...]
    !!VRz13:S179886;                  [...]
    !!VRz14:S179887;                  [...]
    !!VRzh:S179888;                   [подсказка]
  !!en:;
  !!VRz12:S179810;                    [Название Гильдии]
  !!VRy11:Sy4;                        [...]
  !!VRz15:S179847;                    [Стоимость карты]
!!en:;
*** Выбор специализации
!!if&y1=9:;
  !!VRz9:S^card_ss5%Y2.pcx^;          [...]
  !!if&y3=1:;                         [Набор рекрутов]
    !!VRz10:S^card_hm.pcx^;           [Иконка специализации]
    !!VRz13:S179788;                  [Название специализации]
    !!VRzh:S179789;                   [подсказка]
  !!en:;
  !!if&y3=2:;                         [Изучение заклинаний]
    !!VRz10:S^card_ps34.pcx^;         [Иконка специализации]
    !!VRz13:S179790;                  [Название специализации]
    !!VRzh:S179791;                   [подсказка]
  !!en:;
  !!if&y3=3:;                         [Обучение героя]
    !!VRz10:S^card_h%V126.pcx^;       [Иконка специализации]
    !!VRz13:S179792;                  [Название специализации]
    !!VRzh:S179793;                   [подсказка]
  !!en:;
  !!if&y3=4:;                         [Постройка жилищ]
    !!VRz10:S^card_bs.pcx^;           [Иконка специализации]
    !!VRz13:S179794;                  [Название специализации]
    !!VRzh:S179795;                   [подсказка]
  !!en:;
  !!if&y3=5:;                         [Обучение командира]
    !!VRz10:S^card_c%V127.pcx^;       [Иконка специализации]
    !!VRz13:S179796;                  [Название специализации]
    !!VRzh:S179797;                   [подсказка]
  !!en:;
  !!if&y3=6:;                         [Услуги гильдий]
    !!VRz10:S^card_ga.pcx^;           [Иконка специализации]
    !!VRz13:S179798;                  [Название специализации]
    !!VRzh:S179799;                   [подсказка]
  !!en:;
  !!VRz12:S179787;                    ["Специализация"]
!!en:;
*** Магия командиров
!!if&y1=10:;
  !!VRz9:S^card_c%V127.pcx^;          [...]
  !!VRz10:S^card_sp%Y2.pcx^;          [...]
  !!UN:N1/?z11/y2;                    [...]
  !!VRz12:S179807;                    ["Обучение командира"]
  !!VRy11:Sy3 *10;                    [y11 - шанс произнести заклинание]
  !!VRz13:S179783;                    [текст обучения]
  !!VRz14:S179784;                    [текст обучения (2 строка)]
  !!VRzh:S179785;                     [...]
!!en:;
!!VRy5:Sx16 *10 +1;                   [y5 - индекс иконки 1 в диалоге]
!!VRy6:Sx16 *10 +2;                   [y6 - индекс иконки 2 в диалоге]
!!DL72:Ay5/11/z9/1 Ay6/11/z10/1;      [установка иконок типа/подтипа действия в диалог]
!!VRy5:Sx16 *10 +3;                   [y5 - индекс 1й строки описания карты в диалоге]
!!VRy6:Sx16 *10 +4;                   [y6 - индекс 2й строки описания карты в диалоге]
!!VRy7:Sx16 *10 +5;                   [y7 - индекс 3й строки описания карты в диалоге]
!!VRy8:Sx16 *10 +6;                   [y8 - индекс 4й строки описания карты в диалоге]
!!VRy9:Sx16 *10 +7;                   [y9 - индекс 5й строки описания карты в диалоге]
!!VRy10:Sx16 *10 +8;                  [y10 - индекс 6й строки описания карты в диалоге]
!!DL72:Ay5/3/z12/1 Ay6/3/z13/1 Ay7/3/z14/1 Ay8/3/z15/1; [установка текстов в диалог]

!?DL&v998=72;                         [обработка диалога 72]
!!VRv1|v999=11/v999=12/v999=13/v999=14/v999=15/v999=16/v999=3/v999=91:S1; [v1=1, если клик на 1й карте]
!!VRv1|v999=21/v999=22/v999=23/v999=24/v999=25/v999=26/v999=4/v999=92:S2; [v1=2, если клик на 2й карте]
!!VRv1|v999=31/v999=32/v999=33/v999=34/v999=35/v999=36/v999=5/v999=93:S3; [v1=3, если клик на 3й карте]
!!VRv1|v999=41/v999=42/v999=43/v999=44/v999=45/v999=46/v999=6/v999=94:S4; [v1=4, если клик на 4й карте]
!!VRv1|v999=51/v999=52/v999=53/v999=54/v999=55/v999=56/v999=7/v999=95:S5; [v1=5, если клик на 5й карте]
!!VRv1|v999=61/v999=62/v999=63/v999=64/v999=65/v999=66/v999=8/v999=96:S6; [v1=6, если клик на 6й карте]
!!if&v1>0/v1<=v125/v1000=12:;         [выбор доступной карты]
  !!DL72:C1;                          [...]
  !!FU:E;                             [...]
!!en:;
!!if&v1>0/v1<=v125/v1000=14:;         [вывод подсказки по доступной карте]
  !!VRh:Sv1 +20;                      [h - индекс подсказки]
  !!IF:M0/4/h;                        [вывод подсказки]
!!en:;
!!VRv1:S0;                            [клик не на карте]
!!FU9095&v999=70/v1000=14:P-10;       [вывод диалога атакующего героя]
!!FU9096&v999=72/v1000=14:Pv9996;     [вывод диалога атакующего командира]
!!FU9095&v999=80/v1000=14:P-20;       [вывод диалога защищающегося героя]
!!FU9096&v999=82/v1000=14:Pv9997;     [вывод диалога защищающегося командира]

*** Быстрая подсказка в окне боя

!?FU77003;                            [нажатие клавиши]
!!SN:X?y1;                            [y1 — код клавиши]
!!if&y1=112:;                         [F1 - вывод информации об используемой ИИ-схеме]
  !!SN:X?y2/1;                        [отключение реакции игры на нажатие]
  !!SN:W^BH.AI.Sheme^/?z11;           [z11 - файл ИИ-схемы]
  !!IF:L^AI: %Z11^;                   [Вывод сообщения в лог экрана]
  **BA:H1/?y2;                        [y2 - номер ИИ героя-защитника]
  **FU9052:Py2/-1/1;                  [логирование состояния ИИ]
  **BU:V1; [debug]
!!en:;
!!FU&y1<>81:E;                        [выход, если нажата не "Q"]
!!SN:L^Era.dll^/?y2;                  [received description of Era library in y2]
!!SN:Ay2/^GetGameState^/?y3;          [y3 — address of function GetGameState]
!!SN:Ey3/1/?y4;                       [y4 — ID of parent dialog, y5 — ID of current dialog]
!!FU&y5<>4662240:E;                   [выход, если нажатие не в окне боя]
** отключение быстрой подсказки во время действия, либо во время хода оппонента (c)Berserker
!!BG:A?y1;                            [y1 - текущее действие]
!!FU&y1<>0:E;                         [выход, если выполняется какое-либо действие]
!!BG:Q?y1;                            [y1 - активная сторона]
!!BHy1:N?y2;                          [y2 - номер текущего героя]
!!BA:O?y3/?y4;                        [y3-y4 - хозяева героев]
!!VRy5:S3+y1 Syy5;                    [y5 - хозяин активной стороны]
!!OW:Iy5/?y6 Gy5/?y7;                 [y6 - человек ли сейчас ходит, y7 - активен локальный игрок (1) или удалённый(0)]
!!FU|y6<>0/y7<>1:E;                   [выход, если ходит ИИ или игрок за удаленным ПК]
**
!!SN:X?y1/1;                          [отключение реакции игры на нажатие]
!!BMv9996:H?y11 L?y12;                [y11/y12 - Максимальное и потерянное здоровье командира левого]
!!VRy12:-y11 *-1;                     [y12 - текущее здоровье командира левого]
!!HEv9998:I?y13/1 Fd/d/d/?y31;        [параметры левого героя]
!!VRy31:*10 +10;                      [y31 - макс. мана героя.]
!!FU9047:Pv9998/?y14;                 [y14 - регенерация маны левого героя]
!!BMv9997:H?y21 L?y22;                [y21/y22 - Максимальное и потерянное здоровье командира правого]
!!VRy22:-y21 *-1;                     [y22 - текущее здоровье командира левого]
!!HEv9999:I?y23/1 Fd/d/d/?y32;        [параметры правого героя]
!!VRy32:*10 +10;                      [y31 - макс. мана героя.]
!!FU9047:Pv9999/?y24;                 [y24 - регенерация маны правого героя]
!!VRi:Sv997 +1;                       [i - номер раунда]
!!VRy16:Sv9964;                       [y16=1, если есть способность Повышенная регенерация у командира левого]
!!VRy26:Sv9965;                       [y26=1, если есть способность Повышенная регенерация у командира правого]
!!VRy16&y16=1:Sy11 :50;               [y16 - регенерация командира левого]
!!VRy16&y16=0:Sy11 :100;              [...]
!!VRy26&y26=1:Sy21 :50;               [y26 - регенерация командира правого]
!!VRy26&y26=0:Sy21 :100;              [...]
!!VRz11:Sz179838;                     [формирование быстрой подсказки]
!!IF:Lz11;                            [вывод подсказки в лог экрана]

!?FU88000;                            [Изменение координат отображения WoG диалогов DL
** thanks igrik, gamecreator, sergroj
; x1 - номер WoG диалога
; x2 - координата по X (-1 или 0 по центру)
; x3 - координата по Y (-1 или 0 по центру)
!!VRy1:Sv1;
!!SN:E7510739/1/x1;
!!VRy2:Sv1;
!!if&y2<>0:;
  !!UN:Cy2/4/?y5;
  !!if&x2<0:;
    !!VRy6:Sy5 +32;
    !!UN:Cy6/4/?y7;
    !!UN:C4199496/2/?y8;
    !!VRy10:Sy8 :2;
    !!VRy11:Sy7 :2;
    !!VRx2:Sy10 -y11;
  !!en:;
  !!if&x3<0:;
    !!VRy6:Sy5 +36;
    !!UN:Cy6/4/?y7;
    !!UN:C4199503/2/?y8;
    !!VRy10:Sy8 :2;
    !!VRy11:Sy7 :2;
    !!VRx3:Sy10 -y11;
  !!en:;
  !!VRy3:Sy5 +24;
  !!UN:Cy3/4/x2;
  !!VRy4:Sy5 +28;
  !!UN:Cy4/4/x3;
!!en:;
!!VRv1:Sy1;

** Модификация диалога повышения уровня. Теперь из него можно открыть окно героя для управления войсками/артефактами и пр.
** (с)пасибо igrik
!?PI;
!!SN:L^EraPlugins\erm_hooker.era^/?y1;
!!SN&y1<>0:Ay1/^SetHook^/?y2 Ey2/1/5218510/9065;

!?GM0;
!!SN:L^EraPlugins\erm_hooker.era^/?y1;
!!SN&y1<>0:Ay1/^SetHook^/?y2 Ey2/1/5218510/9065;

!?FU9065;
!!SN:X?y91;
!!VRy92:Sy91 +8;   !!UN:Cy92/4/?y93;
!!VRy94:Sy93 +8;   !!UN:Cy94/4/?y95;
!!VRy96:Sy95 +8;   !!UN:Cy96/4/?y97;  [= CM:I?y97]
!!VRy98:Sy95 +12;  !!UN:Cy98/4/?y99;  [= CM:F?y99]
!!FU|y97<>2001/y99=512:E; выход, если не портрет героя или ПКМ
!!SN:W^Dlg_LvlUpHeroID^/?y90 E5118576/3/y90/1/0/0;

!?HL-1;
!!OW:A-1/?y1;
!!HE-1&y1=-1:N?y1;
!!SN:W^Dlg_LvlUpHeroID^/y1;   запомнить номер героя

** Artefact helper
!?CM0;                          [клик на карте]
!!CM:F?y1 I?y2;                 [y1/y2 - параметры/место клика]
!!FU|y1<>512/y2<>37:E;          [выход, если не ПКМ на карте приключений]
!!CM:P?y1/?y2/?y3 ;             [y1-y3 - координаты клика]
!!OW:C?y4;                      [y4 - текущий игрок]
!!VRy5&y4=0:S1;  !!VRy5&y4=1:S2;  !!VRy5&y4=2:S4;  !!VRy5&y4=3:S8;   [y5 - бит.маска текущего игрока]
!!VRy5&y4=4:S16; !!VRy5&y4=5:S32; !!VRy5&y4=6:S64; !!VRy5&y4=7:S128; [...]
!!TRy1/y2/y3:V?y6;              [y6 - бит.маска видимости квадрата]
!!VRy6:&y5;                     [y6=0, если квадрат не видим для игрока]
!!FU&y6=0:E;                    [выход, елси квадрат невидим]
!!OBy1/y2/y3:T?y4 U?y5;         [y4/y5 - тип/подтип объекта]
!!FU&y4<>5:E;                   [выход, если клик не на артефакте]
!!UN:C6687592/4/?y12;           [y12 - ссылка на таблицу артефактов [спс => igrik]]
!!VRy13:Sy5 *32 +y12 +16;       [y13 - адрес указателя с описанием артефакта]
!!UN:Cy13/4/?y14;               [y14 - получить указатель на описание артефакта]
!!SN:X?y9 Xy14 X?z1 Xy9;        [z1 - хранит описание артефакта]
!!IF:Q1/8/y5/4^%Z1^;            [вывод окна с картинкой и названием артефакта]
!!CM:R0;                        [отмена стандартной реакции на клик]

** end
