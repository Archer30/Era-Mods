ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
ERMS_ScriptDate=21.12(December).2012
** Author orig.  : Algor
** Name          : Battle Heroes: starting conditions

*** Отключение всех опций.
*** Стартовые установки:
* Переопределение параметров существ
* "Чистка" героев:
* Установка специализации "Battle Heroes"
* Раздача книг
* Удаление заклинаний из книг
* Замена неиспользуемых втор. навыков в хижинах ведьм, у ученых и в университетах.
* Переопределение ценностей артефактов

!?FU9098;                             [Отключении опции x16]
!!UN:Px16/0;                          [по умолчанию значения опций равны 0 - выключены(командиры включены и присутствуют изначально)]
!!UN&x16=5:Px16/2;                    [вогификация всех карт]
!!UN&x16=101:Px16/5;                  [не использовать "правила карты"]
!!IF&x16>1/x16<996:Vx16/0;            [сброс флага]
!!VRy1:Sx16 *10 +1;                   [y1 - индекс первой из десятка v-переменных для очистки]
!!VRvy1&y1<9991:C0/0/0/0/0/0/0/0/0/0; [сброс v-переменных]
!!VRvy1&y1=9991:C0/0/0/0/0/0/0/0/0;   [...]

!#UN:J3/^:clear:^;                    [отключаем опции]
!#UN:J11/1;                           [SoD-style ПКМ]
!#DO9098/0/999/1:P;                   [отключаем опции]

!?GM0;                                [загрузка игры]
!!FU9021:P;                           [выполнение действий требуемых при старте и загрузке]

!?OB17;                               [при посещении жилища]
!!DWv998/v999/v1000:G0/-1/0 G1/-1/0 G2/-1/0 G3/-1/0 G4/-1/0 G5/-1/0 G6/-1/0; [убираем охрану]

!?OB20;                               [при посещении жилища]
!!DWv998/v999/v1000:G0/-1/0 G1/-1/0 G2/-1/0 G3/-1/0 G4/-1/0 G5/-1/0 G6/-1/0; [убираем охрану]

!?OB20/1;                             [при посещении фабрики големов]
!!DWv998/v999/v1000:M2/-1/0 M3/-1/0;  [убираем золотых и алмазных големов]

!?PI;                                 [Стартовые установки]
** проверка и выход, если карта рандомная (thanks to feanor)
!!UN:C6919480/4/?y10;
!!VRy11:Sy10 +128980;
!!UN:Cy11/1/?y11;
!!VRz1:Sz179907;
!!if&y11=114:;
  !!IF&y11=114:Mz1;
  !!UN&y11=114:Q0;
  !!FU:E;
!!en:;
**
!!UN:K1/1 M1;                         [запрет недель и месяцев монстров]
!!OW:D0/128 D1/128 D2/128 D3/128 D4/128 D5/128 D6/128 D7/128; [бесконечная жизнь без городов для всех игроков]
!!DO9000/0/196/1:P;                   [переопределение параметров существ]
!!VRz959:Sz179756;                    [z951 - Описание общей специализации]
!!DO9001/0/155/1:P;                   [переопределение параметров героев и командиров]
!!FU9021:P;                           [выполнение действий требуемых при старте и загрузке]
!!DO9097/0/196/1:P;                   [Установка всем существам 7 уровня флага Король1, для действия базового Палача]
!!DO9035/0/27/1:P;                    [Установка описаний вторичных навыков]
!!VRz955:Sz179757;                    [получение новых описаний артефактов Некромантии из ert-файла]
!!VRz956:Sz179758;                    [...]
!!VRz957:Sz179759;                    [...]
!!VRz958:Sz179760;                    [...]
!!UN:A54/10/955 A55/10/956 A56/10/957 A130/10/958; [Установка новых описаний артефактов Некромантии]
!!VRz962:Sz179763;                    [получение новых описаний артефактов Глаза орла из ert-файла]
!!VRz963:Sz179764;                    [...]
!!VRz964:Sz179765;                    [...]
!!UN:A63/10/962 A64/10/963 A65/10/964;[Установка новых описаний артефактов Глаза орла]
!!VRz965:Sz179766;                    [получение новых описаний артефактов Легиона из ert-файла]
!!VRz966:Sz179767;                    [...]
!!VRz967:Sz179768;                    [...]
!!VRz968:Sz179769;                    [...]
!!VRz969:Sz179770;                    [...]
!!VRz970:Sz179771;                    [...]
!!UN:A118/10/965 A119/10/966 A120/10/967 A121/10/968 A122/10/969 A133/10/970; [Установка новых описаний артефактов Легиона]
!!VRz971:Sz179772;                    [получение новых описаний артефактов Дипломата из ert-файла]
!!VRz972:Sz179773;                    [...]
!!VRz973:Sz179774;                    [...]
!!UN:A66/10/971 A67/10/972 A68/10/973;[Установка новых описаний артефактов Дипломата]
!!VRz974:Sz179775;                    [получение новых описаний артефактов восстановления маны из ert-файла]
!!VRz975:Sz179776;                    [...]
!!VRz976:Sz179777;                    [...]
!!VRz985:Sz179782;                    [...]
!!UN:A73/10/974 A74/10/975 A75/10/976 A138/10/985;[Установка новых описаний артефактов восстановления маны]
!!VRz979:Sz179901;                    [получение новых описаний артефактов морали из ert-файла]
!!VRz980:Sz179902;                    [...]
!!VRz981:Sz179903;                    [...]
!!VRz982:Sz179904;                    [...]
!!VRz983:Sz179905;                    [...]
!!VRz984:Sz179906;                    [...]
!!UN:A49/10/979 A50/10/980 A51/10/981 A45/10/982 A108/10/983 A84/10/984;[Установка новых описаний артефактов морали]
!!VRz880:Sz179912;                    [получение новых описаний подвесок из ert-файла]
!!VRz881:Sz179913;                    [...]
!!VRz882:Sz179914;                    [...]
!!VRz883:Sz179915;                    [...]
!!VRz884:Sz179916;                    [...]
!!VRz885:Sz179917;                    [...]
!!VRz886:Sz179918;                    [...]
!!VRz887:Sz179919;                    [...]
!!UN:A100/10/880 A101/10/881 A102/10/882 A103/10/883 A104/10/884 A105/10/885 A106/10/886 A107/10/887;[Установка новых описаний подвесок]
!!VRz849:Sz179328;                    [получение новых описаний артефактов Сопротивления из ert-файла]
!!VRz850:Sz179329;                    [...]
!!VRz851:Sz179330;                    [...]
!!UN:A57/10/849 A58/10/850 A59/10/851;[Установка новых описаний артефактов Сопротивления]
!!VRz977:S179778;                     [Название "Дракон маны"]
!!VRz978:S179779;                     [Название "Драконы маны"]
!!UN:G1/134/0/977 G1/134/1/978;       [установка названий ДМ]
** Замена неиспользуемых втор. навыков в хижинах ведьм, у ученых и в университетах.
!!UN:U113/-1/?y1;                     [y1 - количество хижин ведьм]
!!VRv1:S-1;                           [инициализация v1 для быстрого поиска]
!!DO9053/1/y1/1:P113;                 [замена навыков]
!!UN:U81/-1/?y1;                      [y1 - количество ученых]
!!VRv1:S-1;                           [инициализация v1 для быстрого поиска]
!!DO9053/1/y1/1:P81;                  [замена навыков]
!!UN:U104/-1/?y1;                     [y1 - количество университетов]
!!VRv1:S-1;                           [инициализация v1 для быстрого поиска]
!!DO9053/1/y1/1:P104;                 [замена навыков]
** Названия существ
!!VRz867:Sz179934; !!VRz868:Sz179935; [Привидения -> Духи]
!!UN:G1/159/0/867 G1/159/1/868;       [устанавливаем названия Духов]
!!VRz865:Sz179936; !!VRz866:Sz179937; [Воры -> Головорезы]
!!UN:G1/143/0/865 G1/143/1/866;       [устанавливаем названия Головорезов]
** Описания способностей существ
!!VRz861:Sz179316; !!UN:G1/150/2/861; [подсказка Верх. Архангела]
!!VRz860:Sz179317; !!UN:G1/151/2/860; [подсказка Алм. Дракона]
!!VRz859:Sz179318; !!UN:G1/152/2/859; [подсказка Громовержца]
!!VRz858:Sz179319; !!UN:G1/153/2/858; [подсказка Барона Ада]
!!VRz857:Sz179320; !!UN:G1/154/2/857; [подсказка Кров. Дракона]
!!VRz856:Sz179321; !!UN:G1/155/2/856; [подсказка Темн. Дракона]
!!VRz855:Sz179322; !!UN:G1/156/2/855; [подсказка Чудища-Привидения]
!!VRz854:Sz179323; !!UN:G1/157/2/854; [подсказка Кошм. Гидры]
!!VRz853:Sz179324; !!UN:G1/158/2/853; [подсказка Духа погр. костра]
!!VRz852:Sz179327; !!UN:G1/172/2/852; [подсказка Кошмара]
!!VRz845:Sz179331; !!UN:G1/166/2/845; [подсказка Посланника Воздуха]
!!VRz846:Sz179332; !!UN:G1/167/2/846; [подсказка Посланника Воды]
!!VRz847:Sz179333; !!UN:G1/164/2/847; [подсказка Посланника Огня]
!!VRz848:Sz179334; !!UN:G1/165/2/848; [подсказка Посланника Земли]

!?FU9053;                             [замена навыков]
!!UN:Ux1/-1/-1/1;                     [получаем координаты шахты в v1-v3]
!!if&x1=113:;                         [хижина ведьмы]
  !!WHv1/v2/v3:S?y1;                  [y1 - предлагаемый навык]
  !!VRy1|y1=0/y1=5:S2;                [Поиск пути, Навигация -> Логистика]
  !!VRy1&y1=3:S8;                     [Разведка - Мистицизм]
  !!VRy1|y1=20/y1=27:S10;             [Артиллерия, Первая помощь - Баллистика]
  !!WHv1/v2/v3:Sy1;                   [замена навыка]
  !!OBv1/v2/v3:Uy1;                   [...]
!!en:;
!!if&x1=81:;                          [ученый]
  !!SCv1/v2/v3:T?y1;                  [y1 - тип ученого]
  !!FU&y1<>1:E;                       [выход, если не втор. навык]
  !!SCv1/v2/v3:S?y1;                  [y1 - предлагаемый навык]
  !!VRy1|y1=0/y1=5:S2;                [Поиск пути, Навигация -> Логистика]
  !!VRy1&y1=3:S8;                     [Разведка - Мистицизм]
  !!VRy1|y1=20/y1=27:S10;             [Артиллерия, Первая помощь - Баллистика]
  !!SCv1/v2/v3:Sy1;                   [замена навыка]
!!en:;
!!if&x1=104:;                         [университет]
  !!URv1/v2/v3:S?y1/?y2/?y3/?y4;      [y1-y4 - предлагаемые навыки]
  !!VRy1&y1=0/y2<>19/y3<>19/y4<>19:S19;[Поиск пути -> Тактика]
  !!VRy1&y1=0/y2<>21/y3<>21/y4<>21:S21;[Поиск пути -> Обучение]
  !!VRy1&y1=0/y2<>18/y3<>18/y4<>18:S18;[Поиск пути -> Грамотность]
  !!VRy1&y1=0/y2<>2/y3<>2/y4<>2:S2;   [Поиск пути -> Логистика]
  !!VRy1&y1=5/y2<>2/y3<>2/y4<>2:S2;   [Навигация -> Логистика]
  !!VRy1&y1=5/y2<>18/y3<>18/y4<>18:S18;[Навигация -> Грамотность]
  !!VRy1&y1=5/y2<>21/y3<>21/y4<>21:S21;[Навигация -> Обучение]
  !!VRy1&y1=5/y2<>19/y3<>19/y4<>19:S19;[Навигация -> Тактика]
  !!VRy1&y1=3/y2<>11/y3<>11/y4<>11:S11;[Разведка -> Глаз Орла]
  !!VRy1&y1=3/y2<>24/y3<>24/y4<>24:S24;[Разведка -> Интеллект]
  !!VRy1&y1=3/y2<>4/y3<>4/y4<>4:S4;   [Разведка -> Дипломатия]
  !!VRy1&y1=3/y2<>8/y3<>8/y4<>8:S8;   [Разведка -> Мистицизм]
  !!VRy1&y1=20/y2<>10/y3<>10/y4<>10:S10;[Артиллерия -> Боевые машины]
  !!VRy1&y1=20/y2<>1/y3<>1/y4<>1:S1;  [Артиллерия -> Стрельба]
  !!VRy1&y1=20/y2<>22/y3<>22/y4<>22:S22;[Артиллерия -> Нападение]
  !!VRy1&y1=20/y2<>9/y3<>9/y4<>9:S9;  [Артиллерия -> Удача]
  !!VRy1&y1=27/y2<>10/y3<>10/y4<>10:S10;[Первая помощь -> Боевые машины]
  !!VRy1&y1=27/y2<>23/y3<>23/y4<>23:S23;[Первая помощь -> Защита]
  !!VRy1&y1=27/y2<>26/y3<>26/y4<>26:S26;[Первая помощь -> Сопротивление]
  !!VRy1&y1=27/y2<>12/y3<>12/y4<>12:S12;[Первая помощь -> Некромантия]
  !!VRy2&y2=0/y1<>19/y3<>19/y4<>19:S19;[Поиск пути -> Тактика]
  !!VRy2&y2=0/y1<>21/y3<>21/y4<>21:S21;[Поиск пути -> Обучение]
  !!VRy2&y2=0/y1<>18/y3<>18/y4<>18:S18;[Поиск пути -> Грамотность]
  !!VRy2&y2=0/y1<>2/y3<>2/y4<>2:S2;   [Поиск пути -> Логистика]
  !!VRy2&y2=5/y1<>2/y3<>2/y4<>2:S2;   [Навигация -> Логистика]
  !!VRy2&y2=5/y1<>18/y3<>18/y4<>18:S18;[Навигация -> Грамотность]
  !!VRy2&y2=5/y1<>21/y3<>21/y4<>21:S21;[Навигация -> Обучение]
  !!VRy2&y2=5/y1<>19/y3<>19/y4<>19:S19;[Навигация -> Тактика]
  !!VRy2&y2=3/y1<>11/y3<>11/y4<>11:S11;[Разведка -> Глаз Орла]
  !!VRy2&y2=3/y1<>24/y3<>24/y4<>24:S24;[Разведка -> Интеллект]
  !!VRy2&y2=3/y1<>4/y3<>4/y4<>4:S4;   [Разведка -> Дипломатия]
  !!VRy2&y2=3/y1<>8/y3<>8/y4<>8:S8;   [Разведка -> Мистицизм]
  !!VRy2&y2=20/y1<>10/y3<>10/y4<>10:S10;[Артиллерия -> Боевые машины]
  !!VRy2&y2=20/y1<>1/y3<>1/y4<>1:S1;  [Артиллерия -> Стрельба]
  !!VRy2&y2=20/y1<>22/y3<>22/y4<>22:S22;[Артиллерия -> Нападение]
  !!VRy2&y2=20/y1<>9/y3<>9/y4<>9:S9;  [Артиллерия -> Удача]
  !!VRy2&y2=27/y1<>10/y3<>10/y4<>10:S10;[Первая помощь -> Боевые машины]
  !!VRy2&y2=27/y1<>23/y3<>23/y4<>23:S23;[Первая помощь -> Защита]
  !!VRy2&y2=27/y1<>26/y3<>26/y4<>26:S26;[Первая помощь -> Сопротивление]
  !!VRy2&y2=27/y1<>12/y3<>12/y4<>12:S12;[Первая помощь -> Некромантия]
  !!VRy3&y3=0/y2<>19/y1<>19/y4<>19:S19;[Поиск пути -> Тактика]
  !!VRy3&y3=0/y2<>21/y1<>21/y4<>21:S21;[Поиск пути -> Обучение]
  !!VRy3&y3=0/y2<>18/y1<>18/y4<>18:S18;[Поиск пути -> Грамотность]
  !!VRy3&y3=0/y2<>2/y1<>2/y4<>2:S2;   [Поиск пути -> Логистика]
  !!VRy3&y3=5/y2<>2/y1<>2/y4<>2:S2;   [Навигация -> Логистика]
  !!VRy3&y3=5/y2<>18/y1<>18/y4<>18:S18;[Навигация -> Грамотность]
  !!VRy3&y3=5/y2<>21/y1<>21/y4<>21:S21;[Навигация -> Обучение]
  !!VRy3&y3=5/y2<>19/y1<>19/y4<>19:S19;[Навигация -> Тактика]
  !!VRy3&y3=3/y2<>11/y1<>11/y4<>11:S11;[Разведка -> Глаз Орла]
  !!VRy3&y3=3/y2<>24/y1<>24/y4<>24:S24;[Разведка -> Интеллект]
  !!VRy3&y3=3/y2<>4/y1<>4/y4<>4:S4;   [Разведка -> Дипломатия]
  !!VRy3&y3=3/y2<>8/y1<>8/y4<>8:S8;   [Разведка -> Мистицизм]
  !!VRy3&y3=20/y2<>10/y1<>10/y4<>10:S10;[Артиллерия -> Боевые машины]
  !!VRy3&y3=20/y2<>1/y1<>1/y4<>1:S1;  [Артиллерия -> Стрельба]
  !!VRy3&y3=20/y2<>22/y1<>22/y4<>22:S22;[Артиллерия -> Нападение]
  !!VRy3&y3=20/y2<>9/y1<>9/y4<>9:S9;  [Артиллерия -> Удача]
  !!VRy3&y3=27/y2<>10/y1<>10/y4<>10:S10;[Первая помощь -> Боевые машины]
  !!VRy3&y3=27/y2<>23/y1<>23/y4<>23:S23;[Первая помощь -> Защита]
  !!VRy3&y3=27/y2<>26/y1<>26/y4<>26:S26;[Первая помощь -> Сопротивление]
  !!VRy3&y3=27/y2<>12/y1<>12/y4<>12:S12;[Первая помощь -> Некромантия]
  !!VRy4&y4=0/y2<>19/y3<>19/y1<>19:S19;[Поиск пути -> Тактика]
  !!VRy4&y4=0/y2<>21/y3<>21/y1<>21:S21;[Поиск пути -> Обучение]
  !!VRy4&y4=0/y2<>18/y3<>18/y1<>18:S18;[Поиск пути -> Грамотность]
  !!VRy4&y4=0/y2<>2/y3<>2/y1<>2:S2;   [Поиск пути -> Логистика]
  !!VRy4&y4=5/y2<>2/y3<>2/y1<>2:S2;   [Навигация -> Логистика]
  !!VRy4&y4=5/y2<>18/y3<>18/y1<>18:S18;[Навигация -> Грамотность]
  !!VRy4&y4=5/y2<>21/y3<>21/y1<>21:S21;[Навигация -> Обучение]
  !!VRy4&y4=5/y2<>19/y3<>19/y1<>19:S19;[Навигация -> Тактика]
  !!VRy4&y4=3/y2<>11/y3<>11/y1<>11:S11;[Разведка -> Глаз Орла]
  !!VRy4&y4=3/y2<>24/y3<>24/y1<>24:S24;[Разведка -> Интеллект]
  !!VRy4&y4=3/y2<>4/y3<>4/y1<>4:S4;   [Разведка -> Дипломатия]
  !!VRy4&y4=3/y2<>8/y3<>8/y1<>8:S8;   [Разведка -> Мистицизм]
  !!VRy4&y4=20/y2<>10/y3<>10/y1<>10:S10;[Артиллерия -> Боевые машины]
  !!VRy4&y4=20/y2<>1/y3<>1/y1<>1:S1;  [Артиллерия -> Стрельба]
  !!VRy4&y4=20/y2<>22/y3<>22/y1<>22:S22;[Артиллерия -> Нападение]
  !!VRy4&y4=20/y2<>9/y3<>9/y1<>9:S9;  [Артиллерия -> Удача]
  !!VRy4&y4=27/y2<>10/y3<>10/y1<>10:S10;[Первая помощь -> Боевые машины]
  !!VRy4&y4=27/y2<>23/y3<>23/y1<>23:S23;[Первая помощь -> Защита]
  !!VRy4&y4=27/y2<>26/y3<>26/y1<>26:S26;[Первая помощь -> Сопротивление]
  !!VRy4&y4=27/y2<>12/y3<>12/y1<>12:S12;[Первая помощь -> Некромантия]
  !!URv1/v2/v3:Sy1/y2/y3/y4;          [обновление набора навыков]
!!en:;

!?FU9000;                             [переопределение параметров существ]
!!MA:Lx16/?y1 Gx16/?y2 Xx16/?y3 Fx16/?y4; [y1/y2/y3 - уровень(0..6)/прирост/флаги/ценость существа]
** скорость
!!VRy1:+2 :2 +2;                      [y1 скорость существа (3/3/4/4/5/5/6)]
!!VRy1&x16>=145/x16<=148:S0;          [нулевая скорость для боевых машин]
!!VRy1|x16=140/x16=142/x16=143/x16=144/x16=168/x16=194/x16=195:+1; [Скорость +1 для существ Гильдии Разбойников]
!!VRy1|x16=137/x16=138/x16=170/x16=171/x16=192:+3; [Скорость +1 для существ Гильдии Стрелков]
** прирост
!!VRy2&x16>=145/x16<=148:S1;          [прирост 1 для боевых машин]
!!VRy2|x16=116/x16=117:S1;            [прирост 1 для Механических/Штурмовых големов]
** флаги
!!VRy3|x16<174/x16>191:|4194304 |131072;[y3: добавляем флаг призванного существа и "нет пенальти морали" всем, кроме командиров]
!!VRy3&x16>173/x16<192:|131072 |4096 |1024 |128; [добавляем командирам флаги "нет пенальти морали", "нет штрафа в рукопашной", "иммунитет к заклинаниям разума", "Король 1"]
!!VRy3|x16=182/x16=191:|16 -16 |262144 -262144; [снимаем флаги "живое существо" и "нежить" у Астрального духа]
!!VRy3&x16=14:|4 -4;                  [Кентавры не имеют флага "Стрелок"]
!!VRy3&x16=159:|262144;               [Привидения имеют флаг "Нежить"]
!!VRy3&x16=168:|16;                   [Горынычи имеют флаг "Живое существо"]
!!VRy3|x16=116/x16=117:|1;            [Механический и Штурмовой голем имеют флаг "широкий"]
!!VRy3|x16=30/x16=31/x16=36/x16=37/x16=52/x16=53:|16 -16; [Джинны, Ифриты, Гаргульи не имеют флага "Живое существо"]
!!VRy3&x16=146:|65536 |4096 |32 -32;  [Катапульта имеет "нет ответа","нет штрафа в ближнем бою", не имееет "катапульта"]
** Боевая ценность для ИИ
!!VRy4&x16>=174/x16<=191:S50000;      [50k боевая ценность командиров]
!!VRy4&x16>=145/x16<=148:S5000;       [5k боевая ценность БМ]
** Обновление
!!MA:Sx16/y1 Gx16/y2 Xx16/y3 Fx16/y4; [обновляем скорость, прирост, флаги, ценость]
** разное
!!MA&x16>=174/x16<=191:Ox16/-1;       [для командиров убираем принадлежность городам (нет бонусов территории)]
!!MA&x16>=145/x16<=148:Px16/100 Dx16/20;[Боевые машины имеют 100 "здоровья" и 20 Защиты]
!!MA&x16>=145/x16<=146:Ax16/20;       [Баллиста  и Катапульта имеют 20 Атаки]
!!MA&x16=145:Mx16/10 Ex16/30 Nx16/30; [Катапульта имеет 30 выстрелов, урон 10-30]
!!MA&x16=146:Mx16/1 Ex16/9 Nx16/30;   [Баллиста имеет 30 выстрелов, урон (1-9)*Атк]
!!MA&x16=138:Nx16/12;                 [Хоббиты имеют 12 выстрелов вместо 24]
!!MA|x16=136/x16=169/x16=193:Nx16/0;  [Фанатики/Волшебницы/Чародеи - нулевой боезапас]
!!VRz960:Sz179761;                    [получение описания баллисты]
!!VRz961:Sz179762;                    [получение описания катапульты]
!!UN:G1/146/2/960 G1/145/2/961;       [Установка описаний баллисты и катапульты]
!!MA&x16=172:X172/4588553;            [Кошмары имеют флаги "нежить", "иммунитет к разуму", "Широкий", "Призванный" и "расширенный радиус атаки"]
!!MA&x16=159:Lx16/2 Ax16/9 Dx16/9 Px16/9 Sx16/6 Mx16/3 Ex16/3; [Параметры привидений]
** параметры посланников стихий (2-5 уровнь)
** A/D:4+2xL H:10+5xL S:5 DMG:3+2xL..7+2xL
!!MA&x16=166:Lx16/2 Ax16/8  Dx16/8  Px16/20 Sx16/5 Mx16/7  Ex16/9;   [Воздух]
!!MA&x16=167:Lx16/3 Ax16/10 Dx16/10 Px16/25 Sx16/5 Mx16/9  Ex16/11;  [Вода]
!!MA&x16=164:Lx16/4 Ax16/12 Dx16/12 Px16/30 Sx16/5 Mx16/11  Ex16/13; [Огонь]
!!MA&x16=165:Lx16/5 Ax16/14 Dx16/14 Px16/35 Sx16/5 Mx16/13 Ex16/15;  [Земля]
** Механический и Штурмовой големы
!!if&x16=116:;
  !!MA:Ax16/25 Dx16/25 Px16/250 Sx16/2 Mx16/25 Ex16/25 Lx16/6; [A:25 D:25 H:250 S:2 DMG:25-25 Lvl:7]
  !!VRz889:Sz179940;                  [получение названий из ert-файла]
  !!VRz890:Sz179939;                  [...]
  !!VRz891:Sz179938;                  [...]
  !!UN:G1/116/0/889 G1/116/1/890 G1/116/2/891; [устанавливаем название и подсказку Механического голема]
!!en:;
!!if&x16=117:;
  !!MA:Ax16/30 Dx16/30 Px16/300 Sx16/3 Mx16/30 Ex16/30 Lx16/6; [A:30 D:30 H:300 S:3 DMG:30-30 Lvl:7]
  !!VRz986:Sz179942;                 [получение названий из ert-файла]
  !!VRz987:Sz179943;                 [...]
  !!VRz988:Sz179941;                 [...]
  !!UN:G1/117/0/986 G1/117/1/987 G1/117/2/988; [устанавливаем название и подсказку Штурмового голема]
!!en:;

!?FU9001;                             [переопределение параметров героев и командиров]
!!HEx16:O?y1;                         [y1 - хозяин героя]
!!OW:Iy1/?y2;                         [y2=0 для игрока-человека]
!!HEx16:A1/0/17 X0/19;                [есть книжка, установка общей специализации]
!!HEx16:B2/?y3;                       [y3 - класс героя]
!!VRy4&y3>=0/y3<=5:S0;                [y4 - тип командира. Для "добрых" - Паладин]
!!VRy4&y3>=6/y3<=11:S4;               [y4 - тип командира. Для "злых" - Пожиратель душ]
!!VRy4&y3>=12/y3<=17:S8;              [y4 - тип командира. Для "нейтральных" - Астральный дух]
!!COx16:E0 Ty4 D0;                    [установка командира и деактивация его до начала боя]
!!VRy4:+274;                          [Картинка героя]
!!VRz956:Sz179756;
!!UN:G2/x16/2/959 G2/x16/3/y4;        [Установка картинки и текста специализации]
!!HEx16:M0/0 M1/0 M2/0 M3/0 M4/0 M5/0 M6/0 M7/0 M8/0 M9/0; [удаление заклинаний из книжки]
!!HEx16:M10/0 M11/0 M12/0 M13/0 M14/0 M15/0 M16/0 M17/0 M18/0 M19/0; [...]
!!HEx16:M20/0 M21/0 M22/0 M23/0 M24/0 M25/0 M26/0 M27/0 M28/0 M29/0; [...]
!!HEx16:M30/0 M31/0 M32/0 M33/0 M34/0 M35/0 M36/0 M37/0 M38/0 M39/0; [...]
!!HEx16:M40/0 M41/0 M42/0 M43/0 M44/0 M45/0 M46/0 M47/0 M48/0 M49/0; [...]
!!HEx16:M50/0 M51/0 M52/0 M53/0 M54/0 M55/0 M56/0 M57/0 M58/0 M59/0; [...]
!!HEx16:M60/0 M61/0 M62/0 M63/0 M64/0 M65/0 M66/0 M67/0 M68/0 M69/0; [...]
!!DO9034/0/27/1:Px16;                 [замена неиспользуемых вторичных навыков]

!?FU9097;                             [Палач действует на всех существ 7 уровня. Установка флага Король 1. x16 - номер существа]
!!MA:Lx16/?y1;                        [y1 - уровень существа]
!!FU&y1<6:E;                          [выход, если не существо 7го уровня]
!!MA:Xx16/?y1;                        [y1 - флаги существа]
!!VRy2:Sy1 &128;                      [y2=128, если есть флаг Король1]
!!VRy3:Sy1 &256;                      [y3=256, если есть флаг Король2]
!!VRy4:Sy1 &512;                      [y4=512, если есть флаг Король3]
!!VRy1&y2=0:+128;                     [если нет, добавляем флаг Король1]
!!VRy1&y3=256:-256;                   [если есть, убираем флаг Король2]
!!VRy1&y4=512:-512;                   [если есть, убираем флаг Король3]
!!MA:Xx16/y1;                         [Обновляем флаги]

!?FU9034;                             [замена неиспользуемых вторичных навыков герою x1. X16 - проверяемый навык]
!!FU&x16<>0/x16<>5/x16<>20/x16<>3/x16<>27:E; [выход, если навык заменять не нужно]
!!HEx1:S2/?y1 S8/?y2 S10/?y3 Sx16/?y4;[y1-y4 - уровни Лигистики, Мистицизма, Баллистики и заменяемого навыка]
!!VRy1|x16=0/x16=5:+y4;               [Поиск пути, Навигация -> Логистика]
!!VRy2&x16=3:+y4;                     [Разведка - Мистицизм]
!!VRy3|x16=20/x16=27:+y4;             [Артиллерия, Первая помощь - Баллистика]
!!VRy1&y1>3:S3;                       [макс. уровень - Эксперт]
!!VRy2&y2>3:S3;                       [...]
!!VRy3&y3>3:S3;                       [...]
!!HEx1:S2/y1 S8/y2 S10/y3 Sx16/0;     [замена навыков]

*** Изменение прокачки героев (ОТКЛЮЧЕНО, ИСПОЛЬЗУЮТСЯ ДАННЫЕ ИЗ hctraits.txt)
* При повышении уровня не предлагаются неиспользуемые навыки
* Нет классовых ограничений на изучение навыков
* Первый навык по возможности из числа недоизученных, второй - из неизученных.

!?HL-1;                               [получение героем уровня. Шансы выпадения первичных и вторичных навыков]
!!FU:E;                               [переопределение отключено, вероятности выпадения теперь задаются отдельно для классов]
!!VRy2:S0 R9 :3;                      [y2 - первичный навый (Атака/Защита/Сила по 30%, Знание - 10%)]
!!HE-1:N?y1;                          [y1 - номер Героя]
** выбор первого навыка (по возможности недоизученного)
!!DO9027/0/27/1:Py1/0/-1/1;           [v1 - количество недоизученных навыков]
!!if&v1=0:;                           [если недоизученных навыков нет]
  !!DO9027/0/27/1:Py1/0/-1/0;         [v1 - количество неизученных навыков]
  !!if&v1=0:;                         [если неизученных навыков также нет]
    !!VRy3:S-1;
  !!el:;
    !!VRv1:-1;                        [декремент v1]
    !!VRy3:S1 Rv1;                    [y3 - случайный номер навыка]
    !!DO9027/0/27/1:Py1/y3/-1/0;      [v1 - случайный неизученный навык]
    !!VRy3:Sv1;                       [y3 - первый предлагаемый навык]
  !!en:;
!!el;
  !!VRv1:-1;                          [декремент v1]
  !!VRy3:S1 Rv1;                      [y3 - случайный номер навыка]
  !!DO9027/0/27/1:Py1/y3/-1/1;        [v1 - случайный недоизученный навык]
  !!VRy3:Sv1;                         [y3 - первый предлагаемый навык]
!!en:;
** выбор второго навыка (по возможности неизученного)
!!DO9027/0/27/1:Py1/0/y3/0;           [v1 - количество неизученных навыков]
!!if&v1=0:;                           [если неизученных навыков нет]
  !!DO9027/0/27/1:Py1/0/y3/1;         [v1 - количество недоизученных навыков]
  !!if&v1=0:;                         [если недоизученных навыков также нет]
    !!VRy4:S-1;
  !!el:;
    !!VRv1:-1;                        [декремент v1]
    !!VRy4:S1 Rv1;                    [y4 - случайный номер навыка]
    !!DO9027/0/27/1:Py1/y4/y4/1;      [v1 - случайный недоизученный навык]
    !!VRy4:Sv1;                       [y4 - первый предлагаемый навык]
  !!en:;
!!el;
  !!VRv1:-1;                          [декремент v1]
  !!VRy4:S1 Rv1;                      [y4 - случайный номер навыка]
  !!DO9027/0/27/1:Py1/y4/y3/0;        [v1 - случайный неизученный навык]
  !!VRy4:Sv1;                         [y4 - первый предлагаемый навык]
!!en:;
!!HL:Sy2/y3/y4;                       [переопределение первичных и вторичных навыков]

!?FU9027;                             [возврат в v1 количества навыков (x2=0) или номера x2-го навыка, исключая навык x3]
** x4=0 - только неизвестные герою навыки, x4=1 - только известные но изученные не полностью навыки
!!VRv1&x16=0:S0;                      [обнуление v1 перед первым циклом]
!!FU&x16=x3:E;                        [исключение навыка x2]
!!FU|x16=0/x16=3/x16=5/x16=20/x16=27:E; [исключение навыков Поиск Пути, Разведка, Навигация, Артиллерия, Первая Помощь]
!!HEx1:Sx16/?y1;                      [y1 - текущий уровень навыка]
!!VRv1&x4=0/y1=0:+1;                  [инкремент v1 если навык соответствует требованиям (неизученный/недоизученный)]
!!VRv1&x4=1/y1>0/y1<3:+1;             [...]
!!if&x2>0/x2=v1:;                     [если нужно вернуть навык с определенным номером]
  !!VRv1:Sx16;                        [возвращаем его]
  !!VRx16:S30;                        [и прерываем цикл]
!!en:;

!?FU9035;                             [Установка описания вторичного навыка x1]
!!VRy1:Sx16 *2 +900;                  [y1 - индекс z-переменной названия навыка]
!!VRy2:Sx16 *2 +179700;               [y2 - индекс z-переменной названия навыка в ert-файле]
!!VRy3:Sx16 *2 +901;                  [y3 - индекс z-переменной описания навыка]
!!VRy4:Sx16 *2 +179701;               [y4 - индекс z-переменной описания навыка в ert-файле]
!!VRzy1:Szy2;                         [zy1 - название навыка]
!!VRzy3:Szy4;                         [zy3 - описание навыка]
!!UN:G0/x16/0/y1 G0/x16/1/y3 G0/x16/2/y3 G0/x16/3/y3; [установка названия и описания навыка]

!?FU9021;                             [действия, выполнение которых требуется при старте и загрузке игры]
!!UN:C7505216/1/2;                    [отключение тени DL-диалогов, спасибо MoP'у]
!!UN:C5896327/1/235;                  [Отключение проверки типа призываемых элементалей...]
!!UN:C5936848/1/235;                  [...отдельное большое спасибо Sav'у за пример...]
!!UN:C5129872/1/233;                  [отключаем стандартное действие навыка Зоркость (спс. feanor'у)]
!!UN:C5129873/4/340;                  [отключаем стандартное действие артефактов Зоркости (спс. feanor'у)]
!!UN:C5125902/4/34281 C5125907/1/144; [отключение сообщений о достижении максимального уровня на карте (спс. Berserker'у)]
** Навыки Стрельба, Нападение, Волшебство имеют действующие значения 10/20/30% (спс. Berserker'у)
!!FU9045:P1/1/10 P1/2/20 P1/3/30;     [...]
!!FU9045:P22/1/10 P22/2/20 P22/3/30;  [...]
!!FU9045:P25/1/10 P25/2/20 P25/3/30;  [...]
** Навык Защита имеет действующие значения 10/15/20% (спс. Berserker'у)
!!FU9045:P23/1/10 P23/2/15 P23/3/20;  [...]
** Навыки Сопротивление имеет действующие значения 10/15/20% (спс. Berserker'у)
!!FU9045:P26/1/10 P26/2/15 P26/3/20;  [...]
** Навык Некромантия имеет действующие значения 0/0/0% (спс. Berserker'у)
!!FU9045:P12/1/0 P12/2/0 P12/3/0;  [...]

!?FU9045;                             [Автор: Демиург, Берсеркер. Устанавливает вещественное значение для вторичного навыка]
;x1 - навык, x2 - уровень (0..3), x3 - значение в прцоентах, например 8
!!FU|x1<1/x1=5/x1=7/x1=10/x1>26:E;//skills aren't supported
!!FU&x1>13/x1<21:E;//skills aren't supported
!!FU|x2<0/x2>3:E;//wrong mastery (must be 0..3)
!!VRy1&x1=1:S6547944;//archery
!!VRy1&x1=2:S6548072;//logistics
!!VRy1&x1=3:S6547928;//scouting
!!VRy1&x1=4:S6548024;//dyplomacy
!!VRy1&x1=6:S6547880;//leadership
!!VRy1&x1=8:S6547912;//mysticism
!!VRy1&x1=9:S6547864;//luck
!!VRy1&x1=11:S6548008;//eagle eye
!!VRy1&x1=12:S6547896;//necromancy
!!VRy1&x1=13:S6547992;//estate
!!VRy1&x1=21:S6548056;//learning
!!VRy1&x1=22:S6547960;//attack
!!VRy1&x1=23:S6547976;//armorer
!!VRy1&x1=24:S6548104;//intelligence
!!VRy1&x1=25:S6548088;//sorcery
!!VRy1&x1=26:S6548040;//resistance
!!VRx2:*4;
!!VRy1:+x2;
!!VRe1:Sx3:100;
!!SN:X?y2 Xe1 X?y3 Xy2;
!!UN:Cy1/4/y3;

** Переопределение ценностей артефактов
** Запрет (0/1)  Класс (2/4/8/16)  Цена
** 0+-3000 - Treasure (2)
** 3000+-6000 - Minor (4)
** 6000+-9000 - Major (8)
** 9000+ - Relict (16)
** A,D,S = 1000, K = 1250
!#UN:A7/0 A7/3/2 A7/1/2000;          [+ Секира Кентавра (A+2)]
!#UN:A8/0 A8/3/4 A8/1/3000;          [+ Черный Клинок Мертвого Рыцаря (A+3)]
!#UN:A9/0 A9/3/4 A9/1/4000;          [+ Великий Гноллий Кистень (A+4)]
!#UN:A10/0 A10/3/4 A10/1/5000;       [+ Карающая Дубина Огра (A+5)]
!#UN:A11/0 A11/3/8 A11/1/6000;       [+ Адский Меч (A+6)]
!#UN:A12/0 A12/3/16 A12/1/9000;      [+ Гладиус Титана (A+12 D-3)]
!#UN:A13/0 A13/3/2 A13/1/2000;       [+ Щит Гномьих Героев (D+2)]
!#UN:A14/0 A14/3/4 A14/1/3000;       [+ Щит Тоскующих Мертвецов (D+3)]
!#UN:A15/0 A15/3/4 A15/1/4000;       [+ Щит Короля Гноллов (D+3)]
!#UN:A16/0 A16/3/4 A16/1/5000;       [+ Щит Яростного Огра (D+5)]
!#UN:A17/0 A17/3/8 A17/1/6000;       [+ Щит Проклятых (D+6)]
!#UN:A18/0 A18/3/16 A18/1/9000;      [+ Щит Стража (D+12 A-3)]
!#UN:A19/0 A19/3/2 A19/1/1250;       [+ Шлем Белого Единорога (K+1)]
!#UN:A20/0 A20/3/2 A20/1/2500;       [+ Шлем-Череп (K+2)]
!#UN:A21/0 A21/3/4 A21/1/3750;       [+ Шлем Хаоса (K+3)]
!#UN:A22/0 A22/3/4 A22/1/5000;       [+ Корона Верховного Волхва (K+4)]
!#UN:A23/0 A23/3/8 A23/1/6250;       [+ Шлем Сатанинской Ярости (K+5)]
!#UN:A24/0 A24/3/16 A24/1/10500;     [+ Шлем Небесного Грома (K+10 S-2)]
!#UN:A25/0 A25/3/2 A25/1/1000;       [+ Нагрудник из Окаменелого Дерева (S+1)]
!#UN:A26/0 A26/3/2 A26/1/2000;       [+ Доспехи из Ребер (S+2)]
!#UN:A27/0 A27/3/4 A27/1/3000;       [+ Кольчуга Великого Василиска (S+3)]
!#UN:A28/0 A28/3/4 A28/1/4000;       [+ Туника Короля Циклопов (S+4)]
!#UN:A29/0 A29/3/4 A29/1/5000;       [+ Нагрудник из Серного Камня (S+5)]
!#UN:A30/0 A30/3/8 A30/1/7500;       [+ Латы Титана (S+10 K-2)]
!#UN:A132/0 A132/3/16 A132/1/10500;  [+ Доспехи Проклятого]
!#UN:A135/0 A135/3/16 A135/1/36000;  [+ Грохот Титана]
!#UN:A31/0 A31/3/4 A31/1/4250;       [+ Магические Доспехи (ADSK+1)]
!#UN:A32/0 A32/3/8 A32/1/8500;       [+ Сандалии Святых (ADSK+2)]
!#UN:A33/0 A33/3/16 A33/1/12750;     [+ Ожерелье Божественной Благодати (ADSK+3)]
!#UN:A34/0 A34/3/16 A34/1/1700;      [+ Щит Львиной Храбрости (ADSK+4)]
!#UN:A35/0 A35/3/16 A35/1/21250;     [+ Меч Правосудия (ADSK+5)]
!#UN:A36/0 A36/3/16 A36/1/25500;     [+ Шлем Божественного Просвещения (ADSK+6)]
!#UN:A129/0 A129/3/16 A129/1/89250;  [+ Альянс Ангелов]
!#UN:A37/0 A37/3/2 A37/1/2000;       [+ Неподвижный Глаз Дракона (AD+1)]
!#UN:A38/0 A38/3/4 A38/1/4000;       [+ Языки Пламени Красного Дракона (AD+2)]
!#UN:A39/0 A39/3/8 A39/1/6000;       [+ Щит из Чешуи Дракона (AD+3)]
!#UN:A40/0 A40/3/8 A40/1/8000;       [+ Доспехи из Чешуи Дракона (AD+4)]
!#UN:A41/0 A41/3/2 A41/1/2250;       [+ Наколенники из Драконьей Кости (SK+1)]
!#UN:A42/0 A42/3/4 A42/1/4500;       [+ Плащ из Драконьих Крыльев (SK+2)]
!#UN:A43/0 A43/3/8 A43/1/6750;       [+ Ожерелье из Зубов Дракона (SK+3)]
!#UN:A44/0 A44/3/16 A44/1/9000;      [+ Корона из Зубов Дракона (SK+4)]
!#UN:A134/0 A134/3/16 A134/1/46500;  [+ Мощь Отца Драконов
** Morale (10% Recruit), Luck = 2000
!#UN:A45/0 A45/3/4 A45/1/4000;       [+ Застывший Глаз Дракона (ML+1)
!#UN:A46/0 A46/3/2 A46/1/2000;       [+ Клевер Удачи (L+1)]
!#UN:A47/0 A47/3/2 A47/1/2000;       [+ Карты Пророчества (L+1)]
!#UN:A48/0 A48/3/2 A48/1/2000;       [+ Голубка Удачи (L+1)]
!#UN:A49/0 A49/3/2 A49/1/2000;       [+ Значок Смелости (M+1)]
!#UN:A50/0 A50/3/2 A50/1/2000;       [+ Герб Доблести (M+1)]
!#UN:A51/0 A51/3/2 A51/1/2000;       [+ Знак Отваги (M+1)]
!#UN:A108/0 A108/3/16 A108/1/12000;  [+ Брелок Смелости (ML+3)
** +1 square Scouting = 1000 (DISABLED)
!#UN:A52/1 A52/3/2 A52/1/1000;       [- Телескоп]
!#UN:A53/1 A53/3/2 A53/1/1000;       [- Подзорная Труба]
** +1 Undead Rang = 1000, +5% Necromancy = 1000
!#UN:A54/0 A54/3/2 A54/1/2000;       [+ Амулет Гробовщика (U+1 N+1)]
!#UN:A55/0 A55/3/4 A55/1/3000;       [+ Мантия Вампира (U+1 N+2)]
!#UN:A56/0 A56/3/4 A56/1/4000;       [+ Сапоги Мертвеца (U+1 N+3)]
!#UN:A130/1 A130/3/4 A130/1/9000;    [- Плащ Короля Нежити[DISABLED]]
** 5% Resistance = 1000
!#UN:A57/0 A57/3/2 A57/1/1000;       [+ Колье Неприступности (Res+1)]
!#UN:A58/0 A58/3/2 A58/1/2000;       [+ Мантия Равновесия (Res+2)]
!#UN:A59/0 A59/3/4 A59/1/3000;       [+ Сапоги Противодействия (Res+3)]
** +5% Archery = 1000 (DISABLED)
!#UN:A60/1 A60/3/2 A60/1/1000;       [- Эльфийский Лук из Вишневого Дерева]
!#UN:A61/1 A62/3/2 A61/1/2000;       [- Тетива из Волоса Гривы Единорога]
!#UN:A62/1 A62/3/4 A62/1/3000;       [- Стрелы из Ангельских Перьев]
** +5% Eagle Eye = 1000
!#UN:A63/0 A63/3/2 A63/1/2000;       [+ Птица Познания (Ee+2)]
!#UN:A64/0 A64/3/2 A64/1/2000;       [+ Бесстрашный Хранитель (Ee+2)]
!#UN:A65/0 A65/3/4 A65/1/4000;       [+ Символ Знаний (Ee+4)]
** -20% Diplomacy cost = 1000
!#UN:A66/0 A66/3/2 A66/1/1000;       [+ Медаль Дипломата (Di+1)]
!#UN:A67/0 A67/3/2 A67/1/1000;       [+ Кольцо Дипломата (Di+1)]
!#UN:A68/0 A68/3/2 A68/1/1000;       [+ Лента Посла (Di+1)]
** Combat Speed = 1000
!#UN:A69/0 A69/3/2 A69/1/1000;       [+ Кольцо Странника (Cs+1)]
!#UN:A97/0 A97/3/2 A97/1/1000;       [+ 97 Ожерелье Стремительности (Cs+1)]
!#UN:A99/0 A99/3/2 A99/1/1000;       [+ 99 Накидка Скорости (Cs+2)]
** Map Speed = 2500 [DISABLED]
!#UN:A70/1 A70/3/2 A70/1/2500;       [- Перчатки Всадника (Ms+1)]
!#UN:A71/1 A71/3/2 A71/1/2500;       [- Ожерелье Морского Проведения (Ms+1)]
!#UN:A98/1 A98/3/4 A98/1/5000;       [- Сапоги-Скороходы (Ms+2)]
!#UN:A123/1 A123/3/4 A123/1/5000;    [- Шляпа Морского Капитана (Ms+2)]
!#UN:A136/1 A136/3/4 A136/1/7500;    [- Шляпа Адмирала]
** Mana regeneration = 1000
!#UN:A73/0 A73/3/2 A73/1/1000;       [+ Амулет Маны (Mr+1)]
!#UN:A74/0 A74/3/2 A74/1/2000;       [+ Талисман Маны (Mr+2)]
!#UN:A75/0 A75/3/4 A75/1/3000;       [+ Магическая Медаль Маны (Mr+3)]
!#UN:A138/1 A138/3/8 A138/1/6000;    [- Колодец Волшебника (Mr+10) [DISABLED]]
** Spell Duration = 1000
!#UN:A76/0 A76/3/2 A76/1/1000;       [+ Магическое Ожерелье (Sd+1)]
!#UN:A77/0 A77/3/2 A77/1/2000;       [+ Магическое Кольцо (Sd+2)]
!#UN:A78/0 A78/3/4 A78/1/3000;       [+ Магическая Накидка (Sd+3)]
!#UN:A139/1 A139/3/8 A139/1/6000;    [- Кольцо Мага [DISABLED]]
** +50% Elemental Damage = 5000
!#UN:A78/0 A78/3/4 A78/1/5000;       [+ Сфера Небесного Свода]
!#UN:A80/0 A80/3/4 A80/1/5000;       [+ Сфера Илистого Озера]
!#UN:A81/0 A81/3/4 A81/1/5000;       [+ Сфера Бушующего Огня]
!#UN:A82/0 A82/3/4 A82/1/5000;       [+ Сфера Проливного Дождя]
** Block Spells = 2000/Level, Recruit, Luck = 4000
!#UN:A83/0 A83/3/8 A83/1/6000;       [+ Плащ Отречения]
!#UN:A84/0 A84/3/4 A84/1/4000;       [+ Дух Уныния]
!#UN:A85/0 A85/3/4 A85/1/4000;       [+ Песочные Часы Недоброго Часа]
!#UN:A126/0 A126/3/16 A126/1/10000;  [+ 126 Сфера Запрещения
** Magic Book = 15000, Spell Sources = 10000
!#UN:A86/0 A86/3/16 A86/1/15000;     [+ Фолиант Магии Огня]
!#UN:A87/0 A87/3/16 A87/1/15000;     [+ Фолиант Магии Воздуха]
!#UN:A88/0 A88/3/16 A88/1/15000;     [+ Фолиант Магии Воды]
!#UN:A89/0 A89/3/16 A89/1/15000;     [+ Фолиант Магии Земли]
!#UN:A124/0 A124/3/16 A124/1/10000;  [+ Шляпа Оратора]
** +1 HP = 1000
!#UN:A94/0 A94/3/2 A94/1/1000;       [+ Кольцо Жизненной Силы (Hp+1)]
!#UN:A95/0 A95/3/2 A95/1/1000;       [+ Кольцо Жизни (Hp+1)]
!#UN:A96/0 A96/3/2 A96/1/2000;       [+ Склянка Жизненной Силы (Hp+2)]
!#UN:A131/1 A131/3/2 A131/1/4000;    [- Эликсир Жизни [DISABLED]]
** Pendants 1 SpellLevel = 1000
!#UN:A100/0 A100/3/4 A100/1/4000;    [+ 100 Брелок Бесстрастия (Берсерк)]
!#UN:A101/0 A101/3/2 A101/1/2000;    [+ 101 Брелок Ясновидения (Слепота)]
!#UN:A102/0 A102/3/4 A102/1/3000;    [+ 102 Священный Брелок (Проклятие)]
!#UN:A103/0 A103/3/4 A103/1/3000;    [+ 103 Брелок Жизни (Волна смерти)]
!#UN:A104/0 A104/3/4 A104/1/3000;    [+ 104 Брелок Смерти (Уничтожение нежити)]
!#UN:A105/0 A105/3/4 A105/1/5000;    [+ 105 Брелок Свободы (Гипноз)]
!#UN:A106/0 A106/3/8 A106/1/6000;    [+ 106 Брелок Отрицательности (Молния, Цепь молний)]
!#UN:A107/0 A107/3/2 A107/1/1000;    [+ 107 Брелок Абсолютной Памяти (Забывчивость)]
**  Resource Generator 2500/5000/7500/10000 (DISABLED)
!#UN:A109/1 A109/3/4 A109/1/5000;    [- Плащ Бесконечных Кристаллов
!#UN:A110/1 A110/3/4 A110/1/5000;    [- Кольцо Драгоценных Камней
!#UN:A111/1 A111/3/4 A111/1/5000;    [- Неиссякаемая Склянка Ртути
!#UN:A111/1 A111/3/2 A111/1/2500;    [- Неистощимая Подвода с Рудой
!#UN:A113/1 A113/3/4 A113/1/5000;    [- Вечное Кольцо Серы
!#UN:A111/1 A111/3/2 A111/1/2500;    [- Неистощимая Подвода Леса
!#UN:A115/1 A115/3/4 A115/1/10000;   [- Неиссякаемый Мешок Золота
!#UN:A116/1 A116/3/4 A116/1/7500;    [- Неиссякаемая Сума Золота
!#UN:A117/1 A117/3/4 A117/1/5000;    [- Неиссякаемая Мошна Золота
!#UN:A140/1 A140/3/4 A140/1/20000;   [- Рог изобилия]
** Recruit Level = 3000
!#UN:A118/0 A118/3/4 A118/1/3000;    [+ Ноги Легиона]
!#UN:A119/0 A119/3/4 A119/1/3000;    [+ Поясница Легиона]
!#UN:A120/0 A120/3/4 A120/1/3000;    [+ Туловище Легиона]
!#UN:A121/0 A121/3/4 A121/1/3000;    [+ Руки Легиона]
!#UN:A122/0 A122/3/4 A122/1/3000;    [+ Голова Легиона]
!#UN:A133/0 A133/3/16 A133/1/15000;  [+ Статуя Легиона]
** Other
!#UN:A91/0 A91/3/4 A91/1/5000;       [+ Золотой Лук]
!#UN:A92/0 A92/3/2 A92/1/1000;       [+ Шар Постоянства]
!#UN:A93/0 A93/3/2 A93/1/2000;       [+ Медаль Уязвимости]
!#UN:A127/0 A127/3/4 A127/1/4000;    [+ Пузырек с Кровью Дракона (AD+5)]
** Commanders (1 card) = 1000
!#UN:A146/0 A146/3/4 A146/1/3000 A146/2/0; [+ Топор Сокрушения (A+12)]
!#VRz892:Sz179920; !#VRz893:Sz179921; !#UN:A146/9/892 A146/10/893; [Установка имени и описания]
!#UN:A154/0 A154/3/4 A154/1/3000 A154/2/0; [+ Закаленный Щит (D+12)]
!#VRz894:Sz179922; !#VRz895:Sz179923; !#UN:A154/9/894 A154/10/895; [Установка имени и описания]
!#UN:A147/0 A147/3/4 A147/1/3000 A147/2/0; [+ Мифрильная Кольчуга (H+750)]
!#VRz896:Sz179924; !#VRz897:Sz179925; !#UN:A147/9/896 A147/10/897; [Установка имени и описания]
!#UN:A148/0 A148/3/4 A148/1/3000 A148/2/0; [+ Острейший Меч (Dmg+24)]
!#VRz898:Sz179926; !#VRz899:Sz179927; !#UN:A148/9/898 A148/10/899; [Установка имени и описания]
!#UN:A150/0 A150/3/4 A150/1/3000 A150/2/0; [+ Колдовская Подвеска (Res+30)]
!#VRz878:Sz179928; !#VRz879:Sz179929; !#UN:A150/9/878 A150/10/879; [Установка имени и описания]
!#UN:A151/0 A151/3/4 A151/1/3000 A151/2/0; [+ Сапоги Ускорения (Spd+3)]
!#VRz876:Sz179930; !#VRz877:Sz179931; !#UN:A151/9/876 A151/10/877; [Установка имени и описания]
!#UN:A155/0 A155/3/8 A155/1/6000 A155/2/0; [+ Кольцо Силы [AD+4 H+250 Dmg+8 Res+10 Spd+1]
!#VRz874:Sz179932; !#VRz875:Sz179933; !#UN:A155/9/874 A155/10/875; [Установка имени и описания]
** Other [DISABLED]
!#UN:A72/1 A72/3/8 A72/1/7500;       [- Крылья Ангела]
!#UN:A90/1 A90/3/8 A90/1/7500;       [- Сапоги Левитации]
!#UN:A125/1 A125/3/4 A125/1/5000;    [- Оковы Войны]
!#UN:A128/1 A128/3/16 A128/1/50000;  [- Клинок Армагеддона]
!#UN:A137/1 A137/3/8 A137/1/7500;    [- Лук Снайпера]
!#UN:A141/1 A141/3/16 A141/1/50000;  [- Волшебная Палочка]
!#UN:A142/1 A142/3/8 A142/1/7500;    [- Золотая Стрела]
!#UN:A143/1 A143/3/8 A143/1/7500;    [- Сила Монстра]
!#UN:A144/1 A144/7/1; A144/3/0 A144/1/0; Пустой слот
!#UN:A145/1 A145/7/1; A145/3/0 A145/1/0; Заблокированный слот
!#UN:A149/1 A149/7/1; A149/3/0 A149/1/0; Шлем Бессмертия
!#UN:A152/1 A152/7/1; A152/3/0 A152/1/0; Ищущий Лук
!#UN:A153/1 A153/7/1; A153/3/0 A153/1/0; Глаз Дракона
!#UN:A156/1 A156/7/1; A156/3/0 A156/1/0; Знамя Полководца
!#UN:A157/1 A157/7/1; A157/3/0 A157/1/0; Багровый Щит Возмездия
!#UN:A158/1 A158/7/1; A158/3/0 A158/1/0; Топор Свирепости Лорда Варваров
!#UN:A159/1 A159/7/1; A159/3/0 A159/1/0; Сердце Дракона
!#UN:A160/1 A160/7/1; A160/3/0 A160/1/0; Ключ Врат
** [NOT IMPLEMENTED YET]
!#UN:A161/0 A161/7/1; A161/3/4 A161/1/3000; Пустой Шлем
!#UN:A162/0 A162/7/1; A162/3/4 A162/1/3000; Пустой Меч
!#UN:A163/0 A163/7/1; A163/3/4 A163/1/3000; Пустой Щит
!#UN:A164/0 A164/7/1; A164/3/4 A164/1/3000; Пустое Кольцо
!#UN:A165/0 A165/7/1; A165/3/4 A165/1/3000; Пустое Кольцо 3х камней
!#UN:A166/0 A166/7/1; A166/3/4 A166/1/3000; Пустое Широкое Ожерелье
!#UN:A167/0 A167/7/1; A167/3/4 A167/1/3000; Пустая Броня
!#UN:A168/0 A168/7/1; A168/3/4 A168/1/3000; Пустая Мантия
!#UN:A169/0 A169/7/1; A169/3/4 A169/1/3000; Пустые Ботинки
!#UN:A170/0 A170/7/1; A170/3/4 A170/1/3000; Пустой Рог
** Расширение набора артефактов для случайной генерации (спасибо igrik'у)
!#UN:C5018050/4/171;                 [...]
!#UN:C5018111/4/5472;                [...]
!#UN:C5018184/4/171;                 [...]

** Golem-like составляющая Сопротивления
!?MR1;                        [перед расчетом сопротивления]
!!MR:D?y1 F?y6;               [y1/y6 - базовый/конечный урон заклинания]
!!VRy6:-y1 *-1;               [y6 - кол-во урона, поглощенное естественным и магическим сопротивлениями]
!!UN:C42231940/4/?y11;        [y2 - номер отряда-цели заклинания]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y12;             [y12 - гекс отряда, получающего урон]
!!FU|y12<0/y12>186:E;         [выход, если гекс за пределами поля]
!!BU:Ey12/?y2;                [y2 - номер отряда на позиции]
!!VRy2::21;                   [y2 - сторона отряда-цели заклинания]
!!BA:Hy2/?y2;                 [y2 - номер героя-владельца]
!!FU&y2<0:E;                  [выход если героя-владельца нет]
!!HEy2:S26/?y3;               [y3 - уровень Сопротивления (0..3) героя]
!!VRy3&y3>0:+1;               [y3 - уровень Сопротивления героя (0/2/3/4)]
!!HEy2:A2/57/d/?y4;           [y4 - кол-во надетых на героя Колье неприступности]
!!VRy3&y4>0:+1;               [y3 - уровень Сопротивления с учетом Колье]
!!HEy2:A2/58/d/?y4;           [y4 - кол-во надетых на героя Мантия равновесия]
!!VRy3&y4>0:+2;               [y3 - уровень Сопротивления с учетом Мантии]
!!HEy2:A2/59/d/?y4;           [y4 - кол-во надетых на героя Сапоги противодействия]
!!VRy3&y4>0:+3;               [y3 - уровень Сопротивления с учетом Сапог]
!!VRy3:*5 *y1 :100;           [y3 - количество урона поглощеннго голем-like сопротивлением]
!!VRy1:-y3 -y6;               [y1 - новый конечный урон заклинания]
!!VRy1&y1<0:S0;               [...]
!!MR:Fy1;                     [устанавливаем новый конечный урон заклинания]

** end
