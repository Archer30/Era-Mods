ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.8ã€‚5+, Era Erm Framework

; Set up a creature to cast a spell after shooting. 
; The shooter would try to cast the "initial spell" upon attacking if the attacked target was not processed with the initial spell buff.
; If the attacking target has applied the initial spell buff, the alternative spell would be casted by the shooter instead.


; ============== SETTINGS ==============
!#VRi^0_rangedsp_triggerMon^:S(MON_ARCHER);         [Creature that has this ability]

!#VRi^0_rangedsp_initSpell^:S(SPELL_WEAKNESS);      [Initial spell the creature would cast upon attacking]
!#VRi^0_rangedsp_initSpellLevel^:S(SKILL_ADVANCED); [Skill level of the initial spell]
!#VRi^0_rangedsp_initSpellPower^:S3;                [Power of the initial spell]
!#VRi^0_rangedsp_initSpellChance^:S100;             [Chance of casting the initial spell]

!#VRi^0_rangedsp_altSpell^:S(SPELL_DISRUPTING_RAY); [Alternative spell is only casted when the debuff of the initial spell is applied on the attacked target]
!#VRi^0_rangedsp_altSpellLevel^:S(SKILL_ADVANCED);  [Skill level of the alternative spell]
!#VRi^0_rangedsp_altSpellPower^:S3;                 [Power of the alternative spell]
!#VRi^0_rangedsp_altSpellChance^:S100;              [Chance of casting the alternative spell]
; ============ END SETTINGS ============


!?FU(OnBattleActionEnd);
; Check the battle action, attacking creature and attacked stack immediately after battle action ends
; Exit the function if not ideal creature shooting or the attacked target is dead
!!BG:A?(action:y) N?(stackAtk:y) E?(stackDef:y);
!!BM(stackAtk):T?(mon:y);

!!FU|(action)<>(BATTLE_ACTION_SHOOT)/(mon)<>i^0_rangedsp_triggerMon^/(stackDef)<=(NO_STACK):E;

; Check if the attacked target is processed with the initial spell
!!BM(stackDef):Gi^0_rangedsp_initSpell^/?(duration:y)/?(level:y) P?(pos:y);
!!BG:Q?(sideAtk:y);
!!VR(random:y):T0/1/100;

; Depending on whether the initial spell buff is applied, casts new spell on the target
!!if&(duration)=0;
  !!FU(rangedsp_CheckStackImmunity):P(stackDef)/i^0_rangedsp_initSpell^/(sideAtk)/?(result:y);

  !!if&(result)/i^0_rangedsp_initSpellChance^>=(random);
    !!BMi^rangedsp_stackAttacking^:Ci^0_rangedsp_initSpell^/(pos)/i^0_rangedsp_initSpellLevel^/i^0_rangedsp_initSpellPower^/1;
  !!en;
!!el;
  !!FU(rangedsp_CheckStackImmunity):P(stackDef)/i^0_rangedsp_altSpell^/(sideAtk)/?(result);

  !!if&(result)/i^0_rangedsp_altSpellChance^>=(random);   
    !!BMi^rangedsp_stackAttacking^:Ci^0_rangedsp_altSpell^/(pos)/i^0_rangedsp_altSpellLevel^/i^0_rangedsp_altSpellPower^/1;
  !!en;
!!en;

!?FU(rangedsp_CheckStackImmunity);      [Sakuya452]
!#VA(stack:x) (spell:x) (casterSide:x) (result:x); [0 for immunity, 1 for acceptance]

!!FU|(stack)<0/(stack)>41:E;

!!UN:C6919200/4/?(value:y);
!!VR(value2:y):S(stack) *1352 +21708 +(value);
!!SN:E5933392/2/(value)/(spell)/(casterSide)/(value2)/1/1;
!!VR(result):Sv1;
