ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.8.5+, Era Erm Framework

; Set up a creature to cast a spell after attack. 
; The attacking creature would try to cast the "initial spell" upon attacking if the attacked target was not processed with the initial spell buff.
; If the attacking target has applied the initial spell buff, the alternative spell would be casted instead.


; ============== SETTINGS ==============
!#VRi^0_attacksp_triggerMon^:S(MON_ARCHER);         [Creature that has this ability]
!#VRi^0_attacksp_actionMode^:S2;                    [Action for triggering the spell. 0 - Melee, 1 - Ranged, 2 - Melee & Ranged]
!#VRi^0_attacksp_chance^:S100;                      [Chance of casting a initial spell]

!#VRi^0_attacksp_initSpell^:S(SPELL_WEAKNESS);      [Initial spell the creature would cast upon attacking]
!#VRi^0_attacksp_initSpellLevel^:S(SKILL_ADVANCED); [Skill level of the initial spell]
!#VRi^0_attacksp_initSpellPower^:S3;                [Power of the initial spell]

!#VRi^0_attacksp_altSpell^:S(SPELL_DISRUPTING_RAY); [Alternative spell is only casted when the debuff of the initial spell is applied on the attacked target]
!#VRi^0_attacksp_altSpellLevel^:S(SKILL_ADVANCED);  [Skill level of the alternative spell]
!#VRi^0_attacksp_altSpellPower^:S3;                 [Power of the alternative spell]   
; ============ END SETTINGS ============


!?FU(OnBattleActionEnd);
; Check the battle action, attacking creature and attacked stack immediately after battle action ends
!!BG:A?(action:y);

; Exit the function if not attacking
!!if&i^0_attacksp_actionMode^=0;
  !!FU&(action)<>(BATTLE_ACTION_WALK_AND_ATTACK):E;
!!el&i^0_attacksp_actionMode^=1;
  !!FU&(action)<>(BATTLE_ACTION_SHOOT):E;
!!el;
  !!FU&(action)<>(BATTLE_ACTION_SHOOT)/(action)<>(BATTLE_ACTION_WALK_AND_ATTACK):E;
!!en;

; Exit if not ideal creature or the attacked target is dead
!!BG:N?(stackAtk:y) E?(stackDef:y);
!!BM(stackAtk):T?(mon:y);
!!FU|(mon)<>i^0_attacksp_triggerMon^/(stackDef)<=(NO_STACK):E;

; Exit if no luck
!!VR(random:y):T0/1/100;
!!FU&i^0_attacksp_chance^<(random):E;

; Check if the attacked target is processed with the initial spell
!!BM(stackDef):Gi^0_attacksp_initSpell^/?(duration:y)/?(level:y) P?(pos:y);
!!BG:Q?(sideAtk:y);

; Depending on whether the initial spell buff is applied, casts new spell on the target
!!if&(duration)=0;
  !!FU(attacksp_CheckStackImmunity):P(stackDef)/i^0_attacksp_initSpell^/(sideAtk)/?(result:y);
  !!BMi^attacksp_stackAttacking^&(result):Ci^0_attacksp_initSpell^/(pos)/i^0_attacksp_initSpellLevel^/i^0_attacksp_initSpellPower^/1;
!!el;
  !!FU(attacksp_CheckStackImmunity):P(stackDef)/i^0_attacksp_altSpell^/(sideAtk)/?(result);
  !!BMi^attacksp_stackAttacking^&(result):Ci^0_attacksp_altSpell^/(pos)/i^0_attacksp_altSpellLevel^/i^0_attacksp_altSpellPower^/1;
!!en;

!?FU(attacksp_CheckStackImmunity);      [Sakuya452]
!#VA(stack:x) (spell:x) (casterSide:x) (result:x); [0 for immunity, 1 for acceptance]

!!FU|(stack)<0/(stack)>41:E;

!!UN:C6919200/4/?(value:y);
!!VR(value2:y):S(stack) *1352 +21708 +(value);
!!SN:E5933392/2/(value)/(spell)/(casterSide)/(value2)/1/1;
!!VR(result):Sv1;
