ZVSE2
** More Map Options Version .94
** By Timothy Pulver
** script121.erm
** Last Updated: February 1, 2007 by Steven Lynch (added Visibility Check to Display Keymaster Keys Scriptlet)
** Previously Updated: October 20, 2006 by Timothy Pulver

** Scriptlets: ERM Option Numbers follow in parenthesis:
** 1) Upgrade All Troops (321)
** 2) Remove All Monoliths (278)
** 3) Upgrade at External Dwellings (279) - commented
** 4) AI Berserk Spell Casting (280)
** 5) 50% Experience from Fleeing Monsters (281) - commented
** 6) Monster Stack Resources (282) - removed
** 7) Limited Dwelling Accumulations (283)
** 8) Monster Stack Artifacts (284) - removed
** 9) Guarded Artifacts (285)
** 10) Display Keymaster Keys (289)

** Permanent Variables: v64-v65            (50% Experience from Fleeing Monsters)
**                      v66-v67, v762-v769 (Monster Stack Resources)
**                      v68                (Monster Stack Artifacts)

** Temporary Variables: z1
** Temporary Flags: 1-2
** Function: FU195-FU198 (Upgrade All Troops)
**           FU199 (Remove All Monoliths)
**           FU158-FU159 (Upgrade at External Dwellings)
**           FU177 (50% Experience from Fleeing Monsters)
**           FU178-FU179 (Limited Dwelling Accumulations)
**           FU140 (Monster Stack Artifacts)
**           FU452-FU453, FU456 (Guarded Artifacts)
**           FU457 (not used yet but reserved for this script)
** PO numbers: PO:T for dwellings (type 17 and type 20)

================================================================================
  REMOVE ALL MONOLITHS (ERM Option: 278)
================================================================================

** Removes all one and two-way monoliths from the map at the start of the game.

** FU199

 [Timer activates only once on day 1]
!?FU(OnEveryDay)&i^day^=1/i^timerOnce^;
!!UN:P278/?y-1; [Check if script is enabled: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!UN:U43/-1/?y-1; [Number of 1 way monoliths entrances: y-1]
!!VRv2:S-2; [Set v2 to -2 to start at last monolith]
!!DO199/1/y-1/1:P43/y-1; [Delete all 1 way monolith entrances]
!!UN:U44/-1/?y-2; [Number of 1 way monoliths exits: y-2]
!!VRv2:S-2; [Set v2 to -2 to start at last monolith]
!!DO199/1/y-2/1:P44/y-2; [Delete all 1 way monolith exits]
!!UN:U45/-1/?y-3; [Number of 2 way monoliths: y-3]
!!VRv2:S-2; [Set v2 to -2 to start at last monolith]
!!DO199/1/y-3/1:P45/y-3; [Delete all 2 way monoliths]

 [Delete Monoliths]
!?FU199;
!!UN:Ux1/-1/-2/2; [Store monolith coordinates in v2/v3/v4]
!!UN:Ov2/v3/v4; [Delete monolith at this location]

--------------------------------------------------------------------------------

================================================================================
  UPGRADE AT EXTERNAL DWELLINGS (ERM Option: 279)
================================================================================

** Heroes can Upgrade Non-upgraded Troops at External Upgraded Dwellings of the
** same type for the same discount they would receive at a Hill Fort: L1=free,
** L2=pay 25% of cost, L3=pay 50% of cost, L4=pay 75% of cost, L5 and higher=pay
** normal cost.

--------------------------------------------------------------------------------

 [Creature Generator Type 1 is trigger]
*$OB17;
*!UN:P279/?y-1; [Check if script is enabled: y-1]
*!FU&y-1<>1:E; [Exit if script isn't enabled]

*!DW998:O?y-1; [Owner of dwelling: y-1]
*!HE-1:O?y-2; [Owner of hero: y-2]
*!FU&y-1<>y-2:E; [Exit if player doesn't own dwelling]

*!DO158/0/6/1:P; [Loop through hero's stacks and offer to upgrade (for standard cost) if applicable]

--------------------------------------------------------------------------------

 [Creature Generator Type 4 is trigger]
*$OB20;
*!UN:P279/?y-1; [Check if script is enabled: y-1]
*!FU&y-1<>1:E; [Exit if script isn't enabled]

*!DW998:O?y-1; [Owner of dwelling: y-1]
*!HE-1:O?y-2; [Owner of hero: y-2]
*!FU&y-1<>y-2:E; [Exit if player doesn't own dwelling]

*!DO158/0/6/1:P; [Loop through hero's stacks and offer to upgrade (for standard cost) if applicable]

--------------------------------------------------------------------------------

 [Loop through hero's stacks and check for available upgrade]
*?FU158;
*!HE-1:C0/x16/?y1/?y10; [Troop type in slot x16: y1, Number: y10]
*!MA&y1>=0/y10>0:Uy1/?y2 Oy1/?y3 Ly1/?y4; [Slot x16 troop: Upgrade: y2=-2 if none (custom), y2=-1 if default, Town type: y3, Level: y4]
*!FU|y2=-2/y1=y2/y1<0/y10<1:E; [Exit if this creature is set to custom upgrade of none or if no creatures are in this slot]
*!FU&y3<0/y2=-1:E; [Exit if it's a neutral creature with default upgrade (i.e., none)]

*!UN&y3>=0:Ty3/y4/1/?y6; [Type of creature in creature's native town's upgraded town dwelling: y6]
*!UN&y3>=0:Ty3/y4/0/?y9; [Type of creature in creature's native town's non-upgraded town dwelling: y9]
*!FU&y3>=0/y1=y6/y2=-1:E; [Exit if this creature is the same as the town upgrade (i.e., already upgraded) unless custom upgrade set]
*!FU&y3>=0/y1<>y9/y2=-1:E; [Exit if this non-neutral creature isn't the same as the town non-upgrade unless custom upgrade set]

*!VRy8&y2>=0:Sy2; [Upgraded creature if custom upgrade is set: y8]
*!VRy8&y2=-1:Sy6; [Upgraded creature if default upgrade is set: y8]

*!DO159/0/3/1:Py1/y8/y10/x16; [Determine resource cost and offer to upgrade stack]

--------------------------------------------------------------------------------

 [Determine resource cost and offer to upgrade stack]
*?FU159;
 [x1=current troop type, x2=upgraded troop type, x3=number of troops, x4=troop slot]
*!DW998:Mx16/?y1/d; [Type of creature in dwelling slot x16: y1]
*!FU&x2<>y1:E; [Skip if not an upgrade match]

 [Determine resource cost of current troop type and store in y10 to y17]
*!MA:Cx1/0/?y10 Cx1/1/?y11 Cx1/2/?y12 Cx1/3/?y13 Cx1/4/?y14 Cx1/5/?y15 Cx1/6/?y16 Cx1/7/?y17;

 [Determine resource cost of upgraded troop type and store in y20 to y27]
*!MA:Cx2/0/?y20 Cx2/1/?y21 Cx2/2/?y22 Cx2/3/?y23 Cx2/4/?y24 Cx2/5/?y25 Cx2/6/?y26 Cx2/7/?y27;

 [Determine resource cost difference (actual cost of upgrade) and store in y30 to y36]
*!VRy30:Sy20 -y10 *x3;
*!VRy30&y30<0:S0; [Wood: y30]
*!VRy31:Sy21 -y11 *x3;
*!VRy31&y31<0:S0; [Mercury: y31]
*!VRy32:Sy22 -y12 *x3;
*!VRy32&y32<0:S0; [Ore: y32]
*!VRy33:Sy23 -y13 *x3;
*!VRy33&y33<0:S0; [Sulfur: y33]
*!VRy34:Sy24 -y14 *x3;
*!VRy34&y34<0:S0; [Crystal: y34]
*!VRy35:Sy25 -y15 *x3;
*!VRy35&y35<0:S0; [Gems: y35]
*!VRy36:Sy26 -y16 *x3;
*!VRy36&y36<0:S0; [Gold: y36]

 [Give gold discount for upgrading L1-L4 creatures]
*!MA:Lx1/?y37; [Creature's level (0-6): y37]
*!VRe1:Sy36 :4; [1/4 of upgrade cost: e1]
*!VRy36&y37=0:S0; [L1 are free: y36]
*!VRy36&y37=1:Se1; [L2 cost 25%: y36]
*!VRy36&y37=2::2; [L3 cost 50%: y36]
*!VRy36&y37=3:Se1 *3; [L4 cost 75%: y36]

 [Determine which resource other than gold (if any) is needed for upgrade: y2]
*!VRy2:S-1; [Initialize y2 to -1]
*!VRy2&y30>0:S0; [Wood: y2]
*!VRy2&y31>0:S1; [Mercury: y2]
*!VRy2&y32>0:S2; [Ore: y2]
*!VRy2&y33>0:S3; [Sulfur: y2]
*!VRy2&y34>0:S4; [Crystal: y2]
*!VRy2&y35>0:S5; [Gems: y2]
*!VRy3&y2>=0:S30 +y2; [Index for resource amount: y3]
*!VRy4&y2>=0:Syy3; [Additional resource amount: y4]

*!VRy5:Sx3*65536+x1; [Subtype for IF display of current troops: y5]
*!VRy8&y36>0:S6; [Type for IF display of gold cost (if any): y8]
*!VRy8&y36<1:S-1; [Type for IF display if no gold cost: y8]

*!UN:N3/-1/x1/1 N3/-2/x2/1; [Name of current troop type: z-1, Upgraded: z-2]
*!VRz-3:S^^; [Initialize z-3 to null string]
*!VRz-3&y36<1/y2<0:S^ (at no cost)^; [Set z-3 to "(at no cost)" if there's no cost]

*!IF:V2/0; [Initialize Flag 2 to False]
*!IF&-1000:V2/1; [AI always upgrades]
*!IF&1000:Q2/21/y5/y8/y36/y2/y4/2^{Upgrade Troops}

Do you wish to upgrade %Z-1 to %Z-2%Z-3?^;
*!OW:R-1/6/?y6; [Player's gold: y6]
*!OW&y2>=0:R-1/y2/?y7; [Player's other resource: y7]
*!FU&-2:E; [Exit if player doesn't wish to upgrade]

*!IF&y6<y36/1000:Q1/6/y36/y2/y4/1^You don't have enough resources to upgrade these troops.^;
*!IF&y6>=y36/y7<y4/1000:Q1/6/y36/y2/y4/1^You don't have enough resources to upgrade these troops.^;
*!FU|y6<y36/y7<y4:E; [Exit if player doesn't have enough resources]

*!HE-1:C0/x4/x2/d/0/5; [Upgrade stack, adjusting stack XP as per a normal upgrade]
*!VRy36:*-1; [Negative of gold cost: y36]
*!VRy4&y2>=0:*-1; [Negative of other resource: y4]
*!OW:R-1/6/dy36; [Reduce player's gold by y36]
*!OW&y2>=0:R-1/y2/dy4; [Reduce player's other resource by y4]

--------------------------------------------------------------------------------

================================================================================
  AI BERSERK SPELL CASTING (ERM Option: 280)
================================================================================

** 50% chance that an AI hero who knows the Berserk spell will cast it instead
** of Magic Arrow, Ice Bolt, Lightning Bolt, Fireball, Inferno, Meteor Shower,
** Curse, Weakness, Sorrow, Misfortune, Slow, Forgetfulness, or Blind, but only
** against creatures that can be affected by it and aren't already Berserked.

** Local/Temporary variables only.

 [Battle Action Trigger]
*?FU(OnBeforeBattleAction)&i^battle_ai_%i(battle_acting_side)^;
*!UN:P280/?y-1; [Check if script is enabled: y-1]
*!FU&y-1<>1:E; [Exit if script isn't enabled]

*!BG:A?y-1 H?y-2; [Action type: y-1, Hero number: y-2]
*!FU|y-1<>(BATTLE_ACTION_HERO_CAST)/y-2<0:E; [Exit if not a hero spell, or no hero]

*!HEy-2:O?y-3 M(SPELL_BERSERK)/?y-4; [Hero owner: y-3, Berserk Spell Level: y-4]
*!FU|y-4<1:E; [Exit if hero doesn't know Berserk or not an AI]

*!BG:S?y-6; [Spell hero is trying to cast: y-6]
*!VRy-7|y-6=15/y-6=16/y-6=17/y-6=21/y-6=22/y-6=23/y-6=42/y-6=45/y-6=47/y-6=50/y-6=52/y-6=54/y-6=61/y-6=62:S1;
*!FU&y-7<>1:E; [Exit if spell isn't Magic Arrow, Ice Bolt, Lightning Bolt, Fireball, Inferno, Meteor Shower,]

*              [Curse, Weakness, Sorrow, Misfortune, Slow, Forgetfulness, or Blind]
*!VRy-16:R0/1/100; [Random number 1-100: y-16]
*!FU&y-16<=50:E; [Exit if y-16 is less than 51 -- 50% chance of AI casting Berserk]

*!BG:E?y-8; [Destination monster stack: y-8]
*!FU&y-8<=(NO_STACK):E; [Exit if no monster there for some reason]

*!FU(WS_280_CheckStackImmunity):Py-8/(SPELL_BERSERK)/i^battke_acting_side^/?y30;
*!FU&y30=(FALSE):E;

*!BMy-8:G(SPELL_BERSERK)/?y-12/d; [Check if stack already has Berserk on it: y-12=0 if no]
*!FU&y-12>0:E; [Exit if stack already has the Berserk spell on it]

*!HEy-2:I?y-13/1 S14/?y-14; [Hero's spell points: y-13, Level of Fire Magic skill: y-14]
*!VRy-15:S20; [Base spell point cost of Berserk: y-15]
*!VRy-15&y-14>0:-4; [Subtract 4 from spell cost if hero knows Fire Magic: y-15]
*!HEy-2:C1/34/d/>0; [Check if hero has Magi in army - flag 1 set to True if yes]
*!HEy-2&-1:C1/35/d/>0; [Check if hero has Archmagi in army - flag 1 set to True if yes]
*!VRy-15&1:-2; [Subtract 2 from spell cost if hero has Magi or Archmagi in army: y-15]
*!FU&y-13<y-15:E; [Exit if hero doesn't have enough spell points to cast Berserk]

*!BG:S(SPELL_BERSERK); [Change AI hero's spell casting to Berserk spell]

*?FU(WS_280_CheckStackImmunity);
; Author: by Sakuya452
*#VA(stack:x) (spell:x) (casterSide:x) (result:x); [0 for immunity, 1 for acceptance]

*!FU|(stack)<(BATTLE_STACK_FIRST)/(stack)>(BATTLE_STACK_LAST):E;

*!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(combatMan:y);
*!BM(stack):Z?(stackStruct:y);
*!SN:E5933392/(CALLCONV_THISCALL)/(combatMan)/(spell)/(casterSide)/(stackStruct)/1/1;
*!VR(result):Sv1;

--------------------------------------------------------------------------------

================================================================================
  50% EXPERIENCE FROM FLEEING MONSTERS (ERM Option: 281)
================================================================================

; This script is disabled as there is another script by Dracolich with the same feature in Era Scripts
** Hero gains 50% basic experience from fleeing monster stacks. Commanders and
** Henchmen gain 50% experience but troop stacks don't gain stack experience.
** Learning skill bonus for heroes (if any) is included.

** v64-v65, FU177

 [Give Heroes 50% Experience for Fleeing Monster Stacks]
*?OB54;
*!UN:P281/?y-1; [Check if script is enabled: y-1]
*!FU&y-1<>1:E; [Exit if script isn't enabled]
*!MO998:G?y-1; [Number of monsters in stack: y-1]
*!OB998:U?y-2; [Type of monster: y-2]
*!MA:Py-2/?y-3; [Health of 1 Monster: y-3]
*!VRy-4:Sy-1 *y-3; [Total Health of Monsters: y-4]
*!VRv64:Sy-4 :2; [50% of Health for XP]
*!HE-1:N?y-5; [Hero number: y-5]
*!DO177/0/6/1:Py-5/y-2; [Count number of troops of type y-2 in hero's army. Result in y-6]
*!VRv65:Sy-6; [Set v65 to total number of troops of this type in hero's army]

--------------------------------------------------------------------------------

*?BA0; [Start of Battle Trigger]
*!UN:P281/?y-1; [Check if script is enabled: y-1]
*!FU&y-1<>1:E; [Exit if script isn't enabled]
*!VRv64:S0; [Reset v64 to 0 if there's a battle]

--------------------------------------------------------------------------------

*$OB54&v64>0;
*!UN:P281/?y-1; [Check if script is enabled: y-1]
*!FU&y-1<>1:E; [Exit if script isn't enabled]
*!OB998:U?y-1; [Type of monster: y-1]
*!HE-1:N?y-2; [Hero number: y-2]
*!DO177/0/6/1:Py-2/y-1; [Count number of troops of type y-1 in hero's army. Result in y-6]
*!VRv64&y-6>v65:S0; [Reset v64 to 0 if troops joined]
*!FU&y-6>v65:E; [Exit if hero has more of this troop type now (i.e., if they joined)]
*!HEy-2:B0/?z1; [Hero's name]
*!UN:N3/2/y-1/1; [Name of Monster: z2]
*!HEy-2:S21/?y-3; [Hero's Learning skill: y-3]
*!VRy-4:Sv64 :20; [5% of experience: y-4]
*!VRy-5:Sy-4 *y-3; [0%/5%/10%/15% XP bonus for Learning skill: y-5]
*!VRv64:+y-5; [Add Learning skill bonus (if any): v64]
*!IF&1000:Q1/17/v64/21/y-1/1^%Z1 receives %V64 experience for the fleeing %Z2.^;
*!HEy-2:Edv64; [Give hero half XP]
*!UN:P49/=1; [Check if Henchmen are enabled: Flag 1=True if yes]
*!IF&1:Wy-2; [Set w variables to this hero]
*!VRw117&1:+3 T2; [Henchman experience counter - gain 3-5 points from fleeing monsters]
*!VRv64:S0; [Reset v64 to 0]

--------------------------------------------------------------------------------

*?FU177;
 [x1=hero number, x2=monster type, total number in hero's army: y-6]
 [Note: this function is also used by Monster Stack Resources]
*!HEx1:C0/x16/?y1/?y2; [Type: y1, Number: y2]
*!VRy-6&y1=x2/y2>0:+y2; [Add total creatures of type x2: y-6]

--------------------------------------------------------------------------------

================================================================================
  GUARDED ARTIFACTS (ERM Option: 285)
================================================================================

** All visible artifacts on the adventure map have guards attached to them.
** The gold value of the monsters guarding is roughly equal to the gold value
** of the artifact.

** Guards will be all from one town type (or neutral) and there will be from
** one to three different types of creatures guarding. Each type has a 50%
** chance of being divided up into two separate stacks.

** FU452-FU453, FU456

--------------------------------------------------------------------------------

 [Day 1 Timer, shared with WoGify so this will occur after main WoGification]

!?FU(OnEveryDay)&i^timerDay^=1/i^timerOnce^;
!!UN:P285/=1;
!!FU&-1:E; [Exit if script is not enabled]

!!UN:U5/-1/?y-1; [Number of artifacts on map: y-1]
!!VRv1:S-2; [Initialize v1 to -2 for fast UN:U backwards looping]
!!DO452/1/y-1/1&y-1>0:P; [Loop through all artifacts]

--------------------------------------------------------------------------------

 [Loop through all artifacts and set guards]
!?FU452;
!!UN:U5/-1/-2/1; [Previous artifact on map at v1/v2/v3]
!!UN:P904/1 P905/0; [Disable ERM error messages and clear error flag]
!!ARv1/v2/v3:M1/?z-1; [Check artifact's custom text: z-1]
!!UN:P905/?y1; [Check ERM error flag: y1=1 if stack has no setup]
!!UN:P904/0; [Re-enable ERM error messages]
!!VRy-1:S0; [Initialize y-1 to 0]
!!DO453/0/6/1&y1=0:P; [Check for guards if artifact has custom setup: y-1=1 if guards found]
!!FU&y-1=1:E; [Skip artifact if existing guards found]

!!OBv1/v2/v3:U?y2; [Type of artifact: y2]
!!UN&y1=1:Ov1/v2/v3; [Delete artifact if no setup]
!!UN&y1=1:Iv1/v2/v3/5/y2/5/y2/-1/0; [Place new artifact (same type) if no setup]
!!ARv1/v2/v3:X1; [Turn Guards on]

!!UN:Ay2/1/?y3 Ay2/3/?y7; [Artifact's cost: y3, Class: y7]
!!VRy3&y3=0/y7<=2:S1000; [If cost is 0, set 1500 gp for Treasure or no class: y3]
!!VRy3&y3=0/y7=4:S4000; [If cost is 0, set 4000 gp for Minor: y3]
!!VRy3&y3=0/y7=8:S6000; [If cost is 0, set 6000 gp for Major: y3]
!!VRy3&y3=0/y7=16:S12000; [If cost is 0, set 12000 gp for Relic: y3]
!!VRy3&y2=0:S0; [Set Spell Book to 0 gp: y3]
!!VRy3|y2=150/y2=153:S1000; [Set Pendant of Sorcery, Dragon Eye Ring to 1000 gp: y3]
!!VRy3|y2=146/y2=147/y2=148/y2=151/y2=152/y2=154/y2=156:S4000; [Set Axe of Smashing, Mithril Mail, Sword of Sharpness, Boots of Haste, Bow of Seeking, Hardened Shield, Warlord's Banner to 4000 gp: y3]
!!VRy3&y2=149:S6000; [Set Helm of Immortality to 6000 gp: y3]
!!VRy3|y2=141/y2=142/y2=159:S20000; [Set Magic Wand, Gold Tower Arrow, Dragonheart to 20000 gp: y3]
!!VRy3|y2=143/y2=155/y2=160:S15000; [Set Monster's Power, Slava's Ring of Power, Gate Key to 15000 gp]
!!VRy3:*2; [Multiply Artifact cost by 2: y3]
!!FU&y3=0:E; [Skip if cost is still 0 gp]

!!VRy4:S1 R2; [Random number 1..3: y4]
!!VRy5:Sy3 :y4; [Divide cost into up to three parts for 1..3 creature types]

!!VRy6:S-1 R9; [Random town type (-1=neutral): y6]
!!DO456/0/1/0:Py6/y5/-10/-11; [Choose first creature type: y-10, number: y-11]
!!DO456/0/1/0&y5>1:Py6/y5/-12/-13; [Choose second creature type: y-12, number: y-13]
!!DO456/0/1/0&y5>2:Py6/y5/-14/-15; [Choose third creature type: y-14, number: y-15]

!!VRy7:S1 R1; [See if first stack should be split: y7=2 if split]
!!VRy7&y-11=1:S1; [No splitting if only 1 there]
!!VRy8:Sy-11 :y7; [1st Half of stack: y8]
!!VRy9&y7=2:Sy-11 %y7; [Remainder for uneven splits: y9]
!!VRy9&y7=2:+y8; [2nd Half of stack: y9]
!!ARv1/v2/v3:G0/y-10/y8; [Set guards in slot 0]
!!ARv1/v2/v3&y7=2:G6/y-10/y9; [Set guards in slot 6]
!!FU&y4<2:E; [Exit if only 1 type of creature]

!!VRy7:S1 R1; [See if second stack should be split: y7=2 if split]
!!VRy7&y-13=1:S1; [No splitting if only 1 there]
!!VRy8:Sy-13 :y7; [1st Half of stack: y8]
!!VRy9&y7=2:Sy-13 %y7; [Remainder for uneven splits: y9]
!!VRy9&y7=2:+y8; [2nd Half of stack: y9]
!!ARv1/v2/v3:G1/y-12/y8; [Set guards in slot 1]
!!ARv1/v2/v3&y7=2:G5/y-12/y9; [Set guards in slot 5]
!!FU&y4<3:E; [Exit if only 2 types of creature]

!!VRy7:S1 R1; [See if third stack should be split: y7=2 if split]
!!VRy7&y-15=1:S1; [No splitting if only 1 there]
!!VRy8:Sy-15 :y7; [1st Half of stack: y8]
!!VRy9&y7=2:Sy-15 %y7; [Remainder for uneven splits: y9]
!!VRy9&y7=2:+y8; [2nd Half of stack: y9]
!!ARv1/v2/v3:G2/y-14/y8; [Set guards in slot 2]
!!ARv1/v2/v3&y7=2:G4/y-14/y9; [Set guards in slot 4]

--------------------------------------------------------------------------------

 [Check for guards]
!?FU453;
!!ARv1/v2/v3:Gx16/?y1/?y2; [Guards in slot x16, Type: y1, Number: y2]
!!VRy-1&y1>=0/y2>0:S1; [Set y-1 to 1 if any guards found]
!!VRx16&y1>=0/y2>0:S999; [Finish loop if any guards found]

--------------------------------------------------------------------------------

 [Choose random creature type]
 [x1=Town Type, x2=maximum stack cost]
 [x3=index value for creature type, x4=index value for creature number]
!?FU456;
!!VRy1&x1>=0:S0 R6; [Random creature level: y1]
!!VRy2&x1>=0:S0 R1; [Non-upgraded=0, Upgraded=1: y2]
!!UN&x1>=0:Tx1/y1/y2/?y3; [Type of monster: y3]
!!VRy3&x1<0:S112 R44; [Random neutral creature: y3]
!!VRy3&x1<0/y3>=122:+1; [Skip Not Used (1): y3]
!!VRy3&x1<0/y3>=124:+1; [Skip Not Used (2): y3]
!!VRy3&x1<0/y3>=126:+1; [Skip Not Used (3): y3]
!!VRy3&x1<0/y3>=128:+1; [Skip Not Used (4): y3]
!!VRy3&x1<0/y3>=145:+14; [Skip War Machines and L8 creatures: y3]
!!VRy3&x1<0/y3>=160:+4; [Skip Emissaries: y3]
!!VRy3&x1<0/y3>=174:+18; [Skip Commanders: y3]
!!MA&y3>=0/y3<=196:Cy3/6/?y4; [Cost of 1 monster: y4]
!!FU|y4>x2/y4<1/y3<0/y3>196:E; [Try again if monster costs more than maximum stack cost, has 0 cost or is invalid]

!!MA:Oy3/?y5; [Town type of monster: y5]
!!FU&x1<0/y5>=0:E; [Try again if neutral town type but monster isn't neutral]

!!VRyx3:Sy3; [Monster type: yx3]
!!VRyx4:Sx2 :y4; [Number of monsters, rounding down: yx4]
!!VRx16:S999; [Exit if monster found]

--------------------------------------------------------------------------------

================================================================================
  DISPLAY KEYMASTER KEYS (ERM Option: 289)
================================================================================

** Right-clicking on a Borderguard or Bordergate will also show you if you
** have the key for it or not.

** Ctrl-clicking on any Border Guard, Bordergate or Keymaster Tent will display
** a full list of all Keymaster Tents visited.

** Uses temporary variables only.

--------------------------------------------------------------------------------

 [Display List if Keymaster Tents Visited with a ctrl-click]
 [on any Border Guard, Border Gate or Keymaster Tent]
!?FU(OnAdventureMapLeftMouseClick)&i^mouse_item^=37/i^mouse_action^=(MOUSE_LMB_PRESSED)/i^key_ctrl^; [Adventure Screen left-click trigger]
!!UN:P289/=1;
!!FU&-1:E; [Exit if script is not enabled]

!!CM:P?y-4/?y-5/?y-6; [Map location clicked: y-4/y-5/y-6]

!!OW:C?y30/?y-100; [Get current/local player: y-100]
!!FU&y-100<0:E; [Exit if for some reason Local Player cannot be found]

!!VRy-75:Sy-100; [Set y-75 to the value of LocalPlayer y-100]
!!FU3333:Py-75; [Turn y-75 (Local Player) into bit value of player colour]
!!VRy-76:Sy-100; [Get converted Local Player bit into y-76]
!!TRy-4/y-5/y-6:V?y-78; [Check square for visibility]
!!VRy-77:Sy-78; [Set y-77 to the value of y-78]
!!VRy-77:&y-100; [Check for the visibility bit for the square right-clicked]
!!FU&y-78=0:E; [Exit if the LocalPlayer doesn't have the square visible]

!!OBy-4/y-5/y-6:T?y-7 U?y-8; [Type of object clicked: y-7, Subtype: y-8]
!!FU&y-7<>9/y-7<>10/y-7<>212:E; [Exit if not a Border Guard, Keymaster Tent or Border Gate]

!!OW:C?y30/?y-100;
!!OW:Ky-100/0/?y-10 Ky-100/1/?y-11 Ky-100/2/?y-12 Ky-100/3/?y-13; [Get state of Keymaster Tents 0-3: y-10..y-13]
!!OW:Ky-100/4/?y-14 Ky-100/5/?y-15 Ky-100/6/?y-16 Ky-100/7/?y-17; [Get state of Keymaster Tents 4-7: y-14..y-17]

!!VRz-1&y-10=0:S^NO^; !!VRz-1&y-10=1:S^YES^; [Light Blue]
!!VRz-2&y-11=0:S^NO^; !!VRz-2&y-11=1:S^YES^; [Green]
!!VRz-3&y-12=0:S^NO^; !!VRz-3&y-12=1:S^YES^; [Red]
!!VRz-4&y-13=0:S^NO^; !!VRz-4&y-13=1:S^YES^; [Dark Blue]
!!VRz-5&y-14=0:S^NO^; !!VRz-5&y-14=1:S^YES^; [Brown]
!!VRz-6&y-15=0:S^NO^; !!VRz-6&y-15=1:S^YES^; [Purple]
!!VRz-7&y-16=0:S^NO^; !!VRz-7&y-16=1:S^YES^; [White]
!!VRz-8&y-17=0:S^NO^; !!VRz-8&y-17=1:S^YES^; [Black]

!!IF:Q1/10/y-100/1^{KEYMASTER TENTS VISITED}

Light Blue: %Z-1    Dark Blue: %Z-4
Red: %Z-3    Green: %Z-2
Brown: %Z-5    Purple: %Z-6
White: %Z-7    Black: %Z-8^; [Display dialog showing which keymaster tents player has visited]
!!CM:R0; [Disable standard right-click display]

--------------------------------------------------------------------------------

 [When right-clicking a Border Guard or Border Gate, show if Keymaster Tent visited or not]
!?CM0; [Adventure Screen left-click trigger]
!!UN:P289/=1;
!!FU&-1:E; [Exit if script is not enabled]

!!CM:I?y-1 F?y-2; [Location: y-1, Flags: y-2]
!!FU|y-1<>37/y-2<>512:E; [Exit if not a right-click on adventure map]

!!CM:P?y-3/?y-4/?y-5; [Map location clicked: y-3/y-4/y-5]

!!OW:C?y30/?y-100; [Get current/local player: y-100]
!!FU&y-100<0:E; [Exit if for some reason Local Player cannot be found]

!!VRy-75:Sy-100; [Set y-75 to the value of LocalPlayer y-100]
!!FU3333:Py-75; [Turn y-75 (Local Player) into bit value of player colour]
!!VRy-76:Sy-100;  [Get converted Local Player bit into y-76]
!!TRy-3/y-4/y-5:V?y-78; [Check square for visibility]
!!VRy-77:Sy-78; [Set y-77 to the value of y-78]
!!VRy-77:&y-100; [Check for the visibility bit for the square right-clicked]
!!FU&y-78=0:E; [Exit if the LocalPlayer doesn't have the square visible]

!!OBy-3/y-4/y-5:T?y-6 U?y-7; [Type of object clicked: y-6, Subtype: y-7]
!!FU&y-6<>9/y-6<>212:E; [Exit if not a Border Guard or Border Gate]

!!OW:C?y30/?y-100;
!!OW:Ky-100/y-7/?y-8; [Get state of corresponding Keymaster Tent: y-8 (0=not visited, 1=visited]

!!VRz-1&y-7=0:S^Light Blue^; [Light Blue: z-1]
!!VRz-1&y-7=1:S^Green^; [Green: z-1]
!!VRz-1&y-7=2:S^Red^; [Red: z-1]
!!VRz-1&y-7=3:S^Dark Blue^; [Dark Blue: z-1]
!!VRz-1&y-7=4:S^Brown^; [Brown: z-1]
!!VRz-1&y-7=5:S^Purple^; [Purple: z-1]
!!VRz-1&y-7=6:S^White^; [White: z-1]
!!VRz-1&y-7=7:S^Black^; [Black: z-1]

!!VRz-1&y-6=9:S^%Z-1 Border Guard^; [Colour + Border Guard: z-1]
!!VRz-1&y-6=212:S^%Z-1 Border Gate^; [Colour + Border Gate: z-1]

!!VRz-1&y-8=0:S^%Z-1

(Keymaster not visited)^; [Keymaster tent not visited: z-1]
!!VRz-1&y-8=1:S^%Z-1

(Keymaster visited)^; [Keymaster tent visited: z-1]

!!IF:Q1/-1/-1/4^%Z-1^; [Display right-click dialog for Border Guard or Border Gate]

!!CM:R0; [Disable standard right-click display]

--------------------------------------------------------------------------------
