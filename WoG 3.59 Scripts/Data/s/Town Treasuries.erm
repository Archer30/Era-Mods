ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
ERMS_ScriptDate=15.2(February).2005

** Town Treasuries by Timothy Pulver
** (Idea by RedMage)
** Version 0.1
** Last Updated: February 15, 2005

** When you capture another player's town, you gain a portion of that player's
** resources, including gold (or all resources if you capture their last town).

** The percentage of resources depends on the level of town hall in the captured
** town and the total number of towns (and their town hall levels) owned by the
** same player as follows:
** Capitol      = 4 shares
** City Hall    = 3 shares
** Town Hall    = 2 shares
** Village Hall = 1 share

** Example: the Blue player has 3 towns with Village Halls, 2 towns with
** Town Halls, 2 towns with City Halls and 1 town with a Capitol. This is a
** total of 17 shares.

** Each resource would be divided by 17 and then multiplied by the number of
** shares for the town captured. If the Red player captures a town with a
** City Hall, each of Blue's resources would be divided by 17 and then
** multiplied by 3. This amount would be subtraced from the Blue player's
** resources and added to the Red player's, e.g., 15000 gold divided by 17
** equals 882 (fractions are dropped), multiplied by 3 equals 2646 gold.

** Variables: v4799
** Functions: FU27200-FU27202

 [Town is trigger]
!?OB98;
!!CA998:O?v4799; [Town owner before visit: v4799]

 [Town is post-visit trigger]
!$OB98;
!!CA998:O?y-1; [Town owner after visit: y-1]
!!OW:C?y-2; [Current player: y-2]
!!FU&v4799<0:E; [Exit if town was neutral]
!!FU&v4799=y-2:E; [Exit if current player already owned town before visit]
!!FU&y-1<>y-2:E; [Exit if current player doesn't own town after visit]

!!VRv1:Sv998; !!VRv2:Sv999; !!VRv3:Sv1000;  [Copy coordinates of current town to v1/v2/v3]
!!FU27200:P1; [Get number of shares for current town (hall size): y-4]
!!VRy-5:Sy-4; [Copy y-4 to y-5]

!!UN:U98/-1/?y-3; [Number of towns on the map: y-3]
!!VRv1:S-1; [Initialize v1 to -1 for fast UN:U looping]
!!DO27200/1/y-3/1:P0; [Loop through all towns and count up total shares: y-4]
!!DO27201/0/7/1:P; [Loop through all resources and calculate amounts gained by conqueror]

!!CA998:N?z1; [Name of town: z1]
!!IF&1000:N0/y-10/1/y-11/2/y-12/3/y-13/4/y-14/5/y-15/6/y-16/7/y-17; [Set up 8-picture dialogue display]
!!IF&1000:N^After securing %Z1, you loot the treasury!^; [Display message with resource pictures]
!!DO27202/0/7/1:P; [Transfer resources to conqueror]

 [Count town shares: y-4]
!?FU27200;
!!UN&x1=0:U98/-1/-1/1; [Store town coordinates in v1/v2/v3]
!!CA1&x1=0:O?y1; [Owner of town: y1]
!!FU&y1<>v4799/x1=0:E; [Skip if town not owned by defeated player]
!!CA1:B3/13; [Check if Capitol is built: Flag 1=True if yes]
!!VRy-4&1:+4; [Add 4 to total shares if Capitol is built: y-4]
!!FU&1:E; [Skip to next town]
!!CA1:B3/12; [Check if City Hall is built: Flag 1=True if yes]
!!VRy-4&1:+3; [Add 3 to total shares if City Hall is built: y-4]
!!FU&1:E; [Skip to next town]
!!CA1:B3/11; [Check if Town Hall is built: Flag 1=True if yes]
!!VRy-4&1:+2; [Add 4 to total shares if Town Hall is built: y-4]
!!FU&1:E; [Skip to next town]
!!CA1:B3/10; [Check if Village Hall is built: Flag 1=True if yes]
!!VRy-4&1:+1; [Add 1 to total shares if Village Hall is built: y-4]

 [Calculate amount of captured resources: y-10..y-17]
!?FU27201;
!!VRy1:S-10 -x16; [Index for y- variables for storing resource amounts (y-10..y-17): y1]
!!OW:Rv4799/x16/?y2; [Number of resource x16 that conquered player has: y2]
!!VRy2::y-4; [Resource amount divided by total number of shares (y-4): y2]
!!VRyy1:Sy2 *y-5; [Resource amount multiplied by shares for captured town (y-5): yy1 (y-10..y-17)]

 [Transfer resources (y-10..y-17) to conqueror]
!?FU27202;
!!VRy1:S-10 -x16; [Index for y- variables for stored resource amounts (y-10..y-17): y1]
!!VRy2:Syy1 *-1; [Convert resource amount into a negative number: y2]
!!OW:Rv4799/x16/dy2; [Remove resources (y2) from conquered player (v4799)]
!!OW:Ry-1/x16/dyy1; [Give resources (yy1) to conquering player (y-1)]
