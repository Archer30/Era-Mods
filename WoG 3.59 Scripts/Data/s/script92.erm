ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
ERMS_ScriptDate=24.12(December).2005

** Random Battle Positions version 0.1
** Last Updated: December 24, 2005
** Script92.erm (TEMPORARY NUMBER ONLY)
** Idea by Treupp, Script by Timothy Pulver

** Functions: FU7048-FU7049
** Variables: v7115

!?BA0; [Start of Battle Trigger]
!!VRv7115:S0; [Initialize v7115 to 0 at start of every battle]

!?BR&v997=0/v7115<1; [Start of Battleround Trigger]
!!DO7048/0/41/1:P; [Loop through all stacks and find new random positions for them]
!!VRv7115:S1; [Set v7115 to 1 so repositioning occurs only once]

!?FU7048; [Loop through all stacks and find new random positions for them]
!!BMx16:T?y1 N?y2; [Type of stack: y1, Number: y2]
!!FU|y1<0/y2<1:E; [Skip if there's no creatures in the stack]
!!FU&y1>=145/y1<=149:E; [Skip if it's a war machine or arrow tower]
!!BMx16:F?y3; [Stack's Flags: y3]
!!VRy3:&1; [Check if Stack is 2-hex]
!!DO7049/0/1/0:Px16/y3; [Find new position for stack]

!?FU7049; [Find new position for stack]
 [x1=stack #, x2=0 for 1-hex x2=1 for 2-hex]

!!VRy1:S1 R164; [Random number 1-165: y1]
!!VRy1&y1>=16:+2; [Skip out of bounds hexes]
!!VRy1&y1>=33:+2; [Skip out of bounds hexes]
!!VRy1&y1>=50:+2; [Skip out of bounds hexes]
!!VRy1&y1>=67:+2; [Skip out of bounds hexes]
!!VRy1&y1>=84:+2; [Skip out of bounds hexes]
!!VRy1&y1>=101:+2; [Skip out of bounds hexes]
!!VRy1&y1>=118:+2; [Skip out of bounds hexes]
!!VRy1&y1>=135:+2; [Skip out of bounds hexes]
!!VRy1&y1>=152:+2; [Skip out of bounds hexes]
!!VRy1&y1>=169:+2; [Skip out of bounds hexes]

!!VRy2:S0; [Initialize y2 to 0]
!!VRy2|y1=15/y1=32/y1=49/y1=66/y1=83/y1=100/y1=117/y1=134/y1=151/y1=168:S1; [Set y2 to 1 if 2-hex stack on the right edge]
!!VRy1&y2=1/x1<=20:-1; [Subtract 1 from position of 2-hex Attacking stack on the right edge: y1]
!!BU:Oy1/?y3; [Check for obstacle: y3=0 if no obstacle]
!!BU:Ey1/?y4; [Check for monster: y4<0 if no monster]

 [2-hex check]
!!VRy6:S0; [Initialize y6 to 0]
!!VRy7:S-1; [Initialize y7 to -1]
!!VRy5&x1<=20/x2=1:Sy1 +1; [2nd hex to check for 2-hex creature (Attacker): y5]
!!VRy5&x1>=21/x2=1:Sy1 -1; [2nd hex to check for 2-hex creature (Defender): y5]
!!BU&x2=1:Oy5/?y6; [Check for obstacle: y6=0 if no obstacle]
!!BU&x2=1:Ey5/?y7; [Check for monster: y7<0 if no monster]

!!BMx1&y3=0/y4<0/y6=0/y7<0:Py1; [If no obstacles, place stack x1 in new position: y1]
!!VRx16&y3=0/y4<0/y6=0/y7<0:S999; [If no obstacles, Exit Loop]
