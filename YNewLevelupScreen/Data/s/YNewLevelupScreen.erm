ZVSE

; скрипт мода **New level up screen**:
; "Расширенное диалоговое окно повышения уровня героя"
; Автор: [igrik]
; Дата:  07.10.2014г.
; Версия v1.5 (18.02.2020г.)

; Особая благодарность: gamecreator, Berserker, Master Of Puppets, Valery.

* FU 31870 - 31876;
* для временного хранения z1 - z101!

!#UN:P124/0;                           [отключение опции: обучение 2м дополнительным навыкам.]
!#UN:P182/1;                           [включение опции: отображение скрытых навыков]

!?FU31873;                             [функция, на которую установлен хук]
!!SN:W^NewLevelupScreenOn^/?y1;        [проверка на вызов в dll]
!!UN:J8/1/^YNewLevelupScreen.txt^;     [проверка на наличие файла мода]
!!FU|-1/y1<>1:E;                       [если файла нет, то выход]
!!SN:W^NewLevelupScreenOn^/0;          [проверка на другие вызовы. На всякий]
!!DL336:N^YNewLevelupScreen.txt^;      [файл с настройкаим диалога]
!!SN:W^YNewLevelupScreen2^/0;          [обнуление кол-во навыков]
!!SN:W^YNewLevelupSkill^/-1;           [обнуление запоминалки втор.навыков]
!!VRy1:Sv10;                           [получить номер текущего героя (v10 установлено через плагин)]
!!SN:W^DlgLvlUp_HeroID^/y1;            [записать номер героя в глобальную переменную]
!!HEy1:O?y99;                          [получить получить цвет героя]
!!HEy1:B0/?z5;                         [узнать имя героя]
!!FU31871:Py1/1/2;                     [получить название большого портрета героя в Z2]
!!HEy1:E?y3/?y4;                       [получить уровень героя]
!!FU31875:P2/445;                      [получение описания - поднялся на уровень]
!!VRz91:M1/z100/2/40;                  [скопировать текст без первых двух символов]
!!VRz6:Sz5 +z91;                       [добавить полученное описание]
!!FU31875:P2/446;                      [получение описания - теперь на уровне]
!!VRz100:M6/?y30;                      [получить порядковый номер последнего символа (не пробел)
!!VRy30:-8;                            [убрать ненужные символы]
!!VRz92:M1/z100/2/y30;                 [скопировать текст без первых двух символов и последних символов]
!!VRz91:S^ %Y4.^;                      [установить номер уровня в текст]
!!VRz7:Sz5 +z92 +z91;                  [установить описание достижения на уровень]
!!HL:S?y89/?y88/?y87;                  [проверить бонусный первичный навык]
!!SN:W^YNewLevelupSkill_1^/y89;        [запомнить первичный навык]
!!SN:W^YNewLevelupSkill_2^/-1;         [обнулить выбранный первичный навык]
!!FU31875:P0/y89;                      [получение описание перв.навыка]
!!VRz91:S^ +1.^;                       [подготовка окончания описания про перв.навык +1]
!!VRz4:Sz100 +z91;                     [установка описания увеличения перв.навыка]
!!VRy88:S94 +y89;

!!DL336:A10/3/z6/1 A13/11/z2/1 A17/3/z7/1 A9/13/y90 Ay88/4/1 A89/3/z4/1;
; [уровень / портрет / про уровень / цвет подложки / деф перв.нав./ опис.перв.навыков /  текст герой может Изучить]

!!DL336:E30720/0;                      [кнопка ок выключена]
!!DL336:E30721/0;                      [кнопка ок выключена]

!!DO31870/0/27/1:P;                    [проверка всех втор.навыков и постоение списка]
!!SN:W^YNewLevelupScreen^/1;           [обнуление]
!!SN:W^YNewLevelupScreen2^/?y50;       [проверка кол-ва втор.навыков, которые на эксперте]
!!if&y50=28:;                          [если их 27]
  !!DL336:E30720/1;                    [кнопка ок выключена]
  !!DL336:E30721/1;                    [кнопка ок выключена]
!!en:;
; подсказки
!!FU31875:P2/418;                      [узнать текст, для подсказки на окне героя]
!!VRz10:Sz100;                         [копирование текста]
!!DL336:H13/z10;                       [установка описания]

; подсказки для всех перв.навыков
!!FU31875:P0/0;                        [получение описание перв.навыка атака]
!!VRz91:S^ +1.^;                       [подготовка окончания описания про перв.навык +1]
!!VRz94:Sz100 +z91;                    [установка описания увеличения перв.навыка]
!!DL336:H94/z94;                       [установка подсказки атаки +1]
!!FU31875:P0/1;                        [получение описание перв.навыка защита]
!!VRz91:S^ +1.^;                       [подготовка окончания описания про перв.навык +1]
!!VRz95:Sz100 +z91;                    [установка описания увеличения перв.навыка]
!!DL336:H95/z95;                       [установка подсказки защиты +1]
!!FU31875:P0/2;                        [получение описание перв.навыка сила магии]
!!VRz91:S^ +1.^;                       [подготовка окончания описания про перв.навык +1]
!!VRz96:Sz100 +z91;                    [установка описания увеличения перв.навыка]
!!DL336:H96/z96;                       [установка подсказки колдовской +1]
!!FU31875:P0/3;                        [получение описание перв.навыка знания]
!!VRz91:S^ +1.^;                       [подготовка окончания описания про перв.навык +1]
!!VRz97:Sz100 +z91;                    [установка описания увеличения перв.навыка]
!!DL336:H97/z97;                       [установка подсказки знаний +1]
!!DL336:S?v1;                          [вызов диалога]

** ВТОРОЙ щелчек ЛКМ
!?DL&v998=336/v999>9/v999<83/v1000=13; [закрываем диалог при повторном нажатии ЛКМ на выбранный навык]
!!SN:W^YNewLevelupSkill^/?y2;          [узнать выбранный втор.навык]
!!SN:W^DlgLvlUp_HeroID^/?y1;           [получить номер текущего героя]
!!VRy3:Sv999 -50;                      [переход к номеру втор.навыка]
!!SN&y2<>y3:W^YNewLevelupSkill^/-1;    [обнулить ранее выбранный втор.навык]
!!FU&y2<>y3:E;                         [выход, если нажат другой скилл]
!!if&y2>=0/y2=<27:;                 [если клик на втор.навыке]
  !!HEy1:Sy2/?y3;                      [узнаем уровень навыка]
  !!VRy3:+1;                           [повышаем уровень]
  !!HEy1&y3<4:Sy2/y3;                  [установка повышенного уровня втор.навыка]
!!en:;
!!SN:W^YNewLevelupSkill_1^/?y11;
!!SN:W^YNewLevelupSkill_2^/?y12;
!!if&y12<>-1:;                         [если перв. навык выбирался]
; отобрать автоматически данный навык
  !!VRy10:Sy1;                         [перезаписать номер текущего героя]
  !!HEy10&y11=0:Fd-1/d/d/d;            [-1 атака]
  !!HEy10&y11=1:Fd/d-1/d/d;            [-1 защита]
  !!HEy10&y11=2:Fd/d/d-1/d;            [-1 колдовская]
  !!HEy10&y11=3:Fd/d/d/d-1;            [-1 знания]
; и дать выбранный перв.навык
  !!HEy10&y12=0:Fd1/d/d/d;             [+1 атака]
  !!HEy10&y12=1:Fd/d1/d/d;             [+1 защита]
  !!HEy10&y12=2:Fd/d/d1/d;             [+1 колдовская]
  !!HEy10&y12=3:Fd/d/d/d1;             [+1 знания]
!!en:;
!!VRz1:S^Button.wav^; !!SN:Pz1;
!!DL:C1;                               [закрыть диалог]
!!UN:R1;                               [обновить экран, чтобы видеть увеличенные перв.навыки]

** ПЕРВЫЙ щелчек ЛКМ
!?DL&v998=336/v999>9/v999<83/v1000=13;[ЛКМ. Работа со втор.навыками]
!!SN:W^DlgLvlUp_HeroID^/?y1;          [получить номер текущего героя]
!!if&v999>49/v999<78:;                [работа со вторичными навыками]
  *!DO31874/120/147/1:Py1;            [инициализация обводок, если скил есть]
  !!VRy2:Sv999+70;                    [получить id клика]
  !!VRy3:Sy2-120;                     [получить кликнутый скил]
  !!HEy1:Sy3/?y4;                     [получить уровень скила]
  !!VRy4:+1;                          [получить улучшенный скил]
  *!DL336&y4<4:Ay2/4/y4/1;            [перерисовать выбранный навык на улучшенный]
  !!VRy3:Sv999 -50;                   [переход к номеру втор.навыка]
  !!HEy1:Sy3/?y2;                     [получить уровень вторичного навыка]
  !!if&y2<>3;                      [если навык не эсперт, то]
    !!DL336:E30720/1;                 [включить кнопку ок]
    !!DL336:E30721/1;                 [включить кнопку ок]
    !!SN:W^YNewLevelupScreen^/?y99 W^YNewLevelupScreen^/v999;
    !!DL336:Ay99/4/0/1 Av999/4/1/1;   [включить желтую обводку]
    !!SN:W^YNewLevelupSkill^/y3;      [запомнить выбранный втор.навык]
  !!el:;                           [если эксперт, то]
    !!DL336:E30720/0;                 [откл. кнопку ок]
    !!DL336:E30721/0;                 [откл. кнопку ок]
    !!SN:W^YNewLevelupScreen^/?y99;   [запомнить номер элемента]
    !!DL336:Ay99/4/0/1;               [откл. желтую обводку]
  !!en:;
!!en:;
!!SN&v999=13:E5118576/3/y1/1/1/0;     [открытие окна героя: ЛКМ]

!?DL&v998=336/v999>93/v999<98/v1000=13;[ЛКМ. работа с первичн.навыками]
!!SN:W^DlgLvlUp_HeroID^/?y1;           [получить номер текущего героя]
!!VRy89:Sv999 -94;                     [переход к номеру перв.навыка]
!!SN:W^YNewLevelupSkill_2^/y89;        [запомнить выбранный первичный навык]
!!FU31875:P0/y89;                      [получение описание перв.навыка]
!!VRz91:S^ +1.^;                       [подготовка окончания описания про перв.навык +1]
!!VRz4:Sz100 +z91;                     [установка описания увеличения перв.навыка]
!!DL336:A94/4/0/1 A95/4/0/1 A96/4/0/1 A97/4/0/1 Av999/4/1/1 A89/3/z4/1;

!?DL&v998=336/v999>9/v999<83/v1000=14; [диалог по правой кнопке мыши]
!!SN:W^DlgLvlUp_HeroID^/?y1;           [получить номер текущего героя]
!!if&v999>49/v999<78:;                 [показ описания при ПКМ на втор.навыке]
  !!VRy2:Sv999 -50;                    [переход к номеру втор.навыка]
  !!HEy1:Sy2/?y3;                      [получить уровень вторичного навыка]
  !!VRy4:Sy3+1;                        [необходимое смещения для получения описания]
  !!VRy5:Sy3;                          [необходимое смещения для получения описания]
  !!UN:N4/z9/y2;
    !!if&y4<=3:;                       [если навык не на эксперте]
       !!FU31875:P2/356;               [получение описания - выучить]
       !!VRz101:M2/z100/0;             [получить только первое слово]
       !!VRz101:M4/?y10;               [получить длину текста]
       !!VRvy10:-1;                    [отнять еденицу текста]
       !!VRz1:M1/z101/1/y10;           [установить описание]
       !!FU31875:P1/y3;                [получение описания уровня втор.навыка]
       !!VRz91:S^ ^;                   [ставим пробел]
       !!VRz2:Sz1;                     [занести слово выучить в z2]
       !!VRz1:+z91 +z100 +z91 +z9;     [создаем текст подсказки]
       !!FU31876:Py2/y4;               [получить описание втор навыка в z4]
       !!VRz2:+z91 +z4;                [создание: выучить +z4]
       !!IF:M0/4/^{%Z2}^;              [отображение описания]
    !!el:;                             [если навык на эксперте]
       !!FU31875:P1/2;                 [получение описания уровня втор.навыка]
       !!VRz91:S^ ^;                   [ставим пробел]
       !!VRz1:Sz100 +z91 +z9;          [создаем текст подсказки]
       !!FU31875:P2/355;               [получение описания - уже изучено]
       !!VRz91:S^
^;                                     [ставим пробел и переход на другую строку]
       !!VRz1:+z91 +z100;              [закончить текст подсказки]
       !!VRy3:S2;                      [для несмещения в кадре дефа]
       !!FU31876:Py2/y5;               [получить описание втор навыка в z4]
       !!IF:M0/4/^%Z4^;                [отображение описания]
    !!en:;
!!en:;
!!SN&v999=13:E5118576/3/y1/1/1/1;      [открытие окна героя: ПКМ]

!?DL&v998=336/v999=30720/v1000=13;  [закрываем диалог если Item number 30720]
!!SN:W^YNewLevelupSkill^/?y2;          [узнать выбранный втор.навык]
!!if&y2>=0/y2=<27:;                 [если клик на втор.навыке]
  !!SN:W^DlgLvlUp_HeroID^/?y1;         [получить номер текущего героя]
  !!HEy1:Sy2/?y3;                      [узнаем уровень навыка]
  !!VRy3:+1;                           [повышаем уровень]
  !!HEy1&y3<4:Sy2/y3;                  [установка повышенного уровня втор.навыка]
!!en:;
!!SN:W^YNewLevelupSkill_1^/?y11;
!!SN:W^YNewLevelupSkill_2^/?y12;
!!if&y12<>-1:;                         [если перв. навык выбирался]
; отобрать автоматически данный навык
  !!SN:W^DlgLvlUp_HeroID^/?y10;        [получить номер текущего героя]
  !!HEy10&y11=0:Fd-1/d/d/d;            [-1 атака]
  !!HEy10&y11=1:Fd/d-1/d/d;            [-1 защита]
  !!HEy10&y11=2:Fd/d/d-1/d;            [-1 колдовская]
  !!HEy10&y11=3:Fd/d/d/d-1;            [-1 знания]
; и дать выбранный перв.навык
  !!HEy10&y12=0:Fd1/d/d/d;             [+1 атака]
  !!HEy10&y12=1:Fd/d1/d/d;             [+1 защита]
  !!HEy10&y12=2:Fd/d/d1/d;             [+1 колдовская]
  !!HEy10&y12=3:Fd/d/d/d1;             [+1 знания]
!!en:;
!!DL:C1;                               [закрыть диалог]
!!UN:R1;                               [обновить экран, чтобы видеть увеличенные перв.навыки]

!?FU31870;   [функция построения графики диалога]
!!SN:W^DlgLvlUp_HeroID^/?y1;           [получить номер текущего героя]
!!HEy1:Sx16/?y2;                       [получить параметр вторичного навыка
!!VRy3:Sx16 +20;                       [номер навыка +20 (для элемента диалога)
!!VRy13:Sx16 +50;                      [номер навыка +50 (для элемента обводки диалога)
!!VRy23:Sx16 +120;                     [для HPbar]
!!VRy90:Sy2;                           [если 3, то хранит, что навык на эксперте]
!!VRy2&y2=3:-1;                        [для постройки отображения]
!!VRy5:Sx16 *3 +3 +y2;                 [определение кадра дефа]
!!DL336:Ay3/4/y5/1;                    [установка кадра дефа скила]
!!DL336:Ay13/4/0/1;                    [установка кадра дефа обводки / 0 кадр - без обводки]
!!VRz80&y90=3:S^YSecSkillN.def^;       [название дефа для пустого места]
!!DL336&y90=3:Ay3/9/z80/1 Ay3/4/x16/1; [установка названия дефа и кадра]
!!DL336&y90=3:Ay13/4/2/1;              [установка кадра дефа обводки / 2 кадр - если скил на эксперте]
!!DL336:Ay23/4/y90/1;
!!DL336&y90=3:Ay23/4/4/1;
!!SN&y90=3:W^YNewLevelupScreen2^/?y4;  [запоминалка кол-ва втор.навыков]
!!VRy4&y90=3:+1;                       [если есть навык эксперт, то +1]
!!SN&y90=3:W^YNewLevelupScreen2^/y4;   [записать в запоминалку кол-ва втор.навыков]

; подсказка
!!UN:N4/z9/x16;                        [получить название втор.навыка]
!!if&y90<3:;                        [если навык не на эксперте]
   !!FU31875:P2/356;                   [получение описания - выучить]
   !!VRz101:M2/z100/0;                 [получить только первое слово]
   !!VRz101:M4/?y10;                   [получить длину текста]
   !!VRvy10:-1;                        [отнять еденицу текста]
   !!VRzy3:M1/z101/1/y10;              [установить описание]
   !!FU31875:P1/y2;                    [получение описания уровня втор.навыка]
   !!VRz91:S^ ^;                       [ставим пробел]
   !!VRzy3:+z91 +z100 +z91 +z9;        [создаем текст подсказки]
!!el:;                              [если навык на эксперте]
   !!FU31875:P1/y2;                    [получение описания уровня втор.навыка]
   !!VRz91:S^ ^;                       [ставим пробел]
   !!VRzy3:Sz100 +z91 +z9;             [создаем текст подсказки]
   !!FU31875:P2/355;                   [получение описания - уже изучено]
   !!VRzy3:+z91 +z100;                 [закончить текст подсказки]
!!en:;
!!DL336:Hy13/zy3;                      [установка описания]

!?FU31874;
!!VRy1:Sx16-120;
!!HEx1:Sy1/?y2;
!!DL336&y2<>3:Ax16/4/y2/1;
!!DL336&y2=3:Ax16/4/4/1;

!?FU31871;
; получение назнавния потретета героя
; x1 - номер героя
; ?z1 - малый портрет
; ?z2 - большой портрет
!!UN:C6806760/4/?y1;
!!SN:E7411341/1/x1;
!!VRy5:Sv1 +52;
!!UN:Cy5/1/?y6;
!!VRy7:Sy6;
!!VRy6&y6<0:S256 +y7;
!!VRy3:Sy6 *92 +48 +y1;     [HPSName]
!!VRy4:Sy6 *92 +52 +y1;     [HPLName]
!!UN:Cy3/4/?y13 Cy4/4/?y14;
!!SN:X?y9 Xy13 X?z1 Xy14 X?z2 Xy9;

!?FU31875;
; x1 - тип текстовика
; x2 - номер строки
!!VRy100&x1=0:S6974556;  [адрес названия первичного навыка]                     PriSkill.txt
!!VRy100&x1=1:S6979432;  [адрес названия вторичного навыка]                     SkillLev.txt
!!VRy100&x1=2:S6970820;  [адрес описания что поднялся на уровень навыка]        GENRLTXT.txt

; © Master Of Puppets
!!UN:Cy100/4/?y1;
!!VRy1:+32;
!!UN:Cy1/4/?y2;
!!VRx2:*4+y2;
!!UN:Cx2/4/?y1;
!!SN:X?y4 Xy1 X?z101 Xy4;
!!VRz100:S^^;
!!VRz100:+z101;

!?FU31876; [получение описания вторичного навыка]
; x1 - номер втор.навыка     (0...27)
; x2 - уровень втор. навыка  (0...4)
!!VRy1:Sx1 *16;
!!VRy2:Sx2 *4 +6917512 +y1;
!!UN:Cy2/4/?y1;
!!SN:X?y3 Xy1 X?z4 Xy3;
*** конец скрипта ***
